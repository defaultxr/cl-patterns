(in-package #:cl-patterns)

;;;; patterns.lisp - basic pattern functionality (`defpattern', etc) and a variety of basic patterns implemented with it.

;;; pattern glue

(defun make-default-event ()
  "Get `*event*' if it's not nil, or get a fresh empty event."
  (or *event* (event)))

(defparameter *patterns* (list)
  "List of the names of all defined pattern types.")

(defmacro defpattern (name superclasses slots &key documentation defun)
  "Define a pattern. This macro automatically generates the pattern's class, its pstream class, and the function to create an instance of the pattern, and makes them external in the cl-patterns package.

NAME is the name of the pattern. Typically a word or two that describes its function, prefixed with p.

SUPERCLASSES is a list of superclasses of the pattern. Most patterns just subclass the 'pattern' class.

SLOTS is a list of slots that the pattern and pstreams derived from it have. Each slot can either be just a symbol, or a slot definition a la `defclass'. You can provide a default for the slot with the :default key, and you can set a slot as a state slot (which only appears in the pattern's pstream class) by setting the :state key to t.

DOCUMENTATION is a docstring describing the pattern. We recommend providing at least one example, and a \"See also\" section to refer to similar pattern classes.

DEFUN can either be a full defun form for the pattern, or an expression which will be inserted into the pattern creation function prior to initialization of the instance. Typically you'd use this for inserting `assert' statements, for example."
  (let* ((superclasses (or superclasses (list 'pattern)))
         (slots (mapcar #'ensure-list slots))
         (name-pstream (symbolicate name '-pstream))
         (super-pstream (if (eql 'pattern (car superclasses))
                            'pstream
                            (symbolicate (car superclasses) '-pstream))))
    (labels ((desugar-slot (slot)
               "Convert a slot into something appropriate for defclass to handle."
               (destructuring-bind (name . rest) slot
                 (append (list name)
                         (remove-from-plist rest :default :state)
                         (unless (position :initarg (keys rest))
                           (list :initarg (make-keyword name))))))
             (optional-slot-p (slot)
               "Whether the slot is optional or not. A slot is optional if a default is provided."
               (position :default (keys (cdr slot))))
             (state-slot-p (slot)
               "Whether the slot is a pstream state slot or not. Pstream state slots only appear as slots for the pattern's pstream class and not for the pattern itself."
               (position :state (keys (cdr slot))))
             (function-lambda-list (slots)
               "Generate the lambda list for the pattern's creation function."
               (let (optional-used)
                 (mappend (fn (unless (state-slot-p _)
                                (if (optional-slot-p _)
                                    (prog1
                                        (append (unless optional-used
                                                  (list '&optional))
                                                (list (list (car _) (getf (cdr _) :default))))
                                      (setf optional-used t))
                                    (list (car _)))))
                          slots)))
             (make-defun (pre-init)
               `(defun ,name ,(function-lambda-list slots)
                  ,documentation
                  ,@(when pre-init (list pre-init))
                  (make-instance ',name
                                 ,@(mappend (fn (unless (state-slot-p _)
                                                  (list (make-keyword (car _)) (car _))))
                                            slots))))
             (add-doc-to-defun (sexp)
               (if (and (listp sexp)
                        (position (car sexp) (list 'defun 'defmacro))
                        (not (stringp (fourth sexp))))
                   (append (subseq sexp 0 3) (list documentation) (subseq sexp 3))
                   sexp)))
      `(progn
         (defclass ,name ,superclasses
           ,(mapcar #'desugar-slot (remove-if #'state-slot-p slots))
           ,@(when documentation
               `((:documentation ,documentation))))
         (defmethod print-object ((,name ,name) stream)
           (print-unreadable-object (,name stream :type t)
             (format stream "簋［夯蔺磲疸狎灬礅溽箪雉箪雉鲠祯钺礤箪雉┅К磲疸狎＇汜蝈盹鲥殒灬礅溽箪雉矧篝狒瀛箪雉箪雉换粕睾滹瞌箬秣狎珲礤铘翳狒狎箦麸翳溴驷蹯趔┅箪雉螬┅┅ㄤ彐沆狍钺礤痼趄遽ì篚疱颦痼趄遽钺礤磲疸狎＇溴篚玑颦箪雉蝈盹鲥殒铒＇篝狒瀛箪雉箪雉螬ê滹沲礤铘狒轱ㄦ矧磲铋痼趄遽骘帼岌篝蜷铉滹黝汜箦簌礅镬钺礤钺礤┅┅戾舄è珏瞽骢钽矧铛祆溴骢瞟ㄡ钿扉篝溴骢瞟痫箝糸镱ㄣ狎溴骢瞟扉篝п篌弪с桢汶豉疱┅┅痱瀛轭轸麒孱珏瞽骢钽溴骢瞟┅ㄩ珏瞽骢钽磲脲溴骢痱瀛轭轸ㄡ滗滹悱麸溴骢溴骢瞟┅瘐箬铄К钺礤疳趑弪铙┅┅ㄤ彐鲠磲疳趑弪瞽殄熹戾铉翳驳⒃桢溴驷蹯磲轫蹴铛礅弪镦弼孱趔矧鲠祯弩翳狒鏖祆忮躞邃怡骢钽糸镱扉脲囝屮舡瞌矧疳趑弪铙扉脲囵蝻翎翦К轭矧溴麸痱弼孱栳铉汜躞邃怡轭骈铋翦戾铉翳疳趑弪铙换疳趑弪ㄤ彐珏铄蜷痼趄遽憝泔躅疳趑弪瞟ê滹沲礤铘狒轱⒃桢铛礅弪镦痼趄遽眢翳狒栳鲥忮孱磲溴镦翳轶疳趑弪町┅ㄤ彐沆狍疳趑弪īè痨狴聃犷洪铋翎蜱吼灬聃犷轰镢蹴孱翎糸镱⒘扉篝镦铛礅弪蝈痱弩孱糸铉麒孱翳疳趑弪瞌痼趄遽汜篝狎痨狴轭绠渝囵灬聃犷臾犷囫踽铘Мㄥ钿聃犷洪铋翎蜱哄钿聃犷横沣弩箫孱洵聃犷呼疱扉篝轰镢蹴孱翎糸镱⒘扉篝镦铛礅弪蝈痱弩孱糸铉麒孱疳趑弪汜孱痨狴轭犷麒孱囵溴妲汜忮篦狃疱秕骘铄溴骈铋糸镱渝噱钿聃犷臾犷囫踽铘Мㄥ钿泔钿轸轱洪铋翎蜱哄钿泔钿轸轱洪铋翩矧铋横沣弩箫孱洵泔钿轸轱呼疱矧铛祆骢钽糸镱轰镢蹴孱翎糸镱⑽殪矧骢钽糸镱翳狒轶汜祆邃怡翳沆镢鏖翳翳疳趑弪狍轸狎珲礤铘麸溴翦蝽轭麒弭桢翳疳趑弪箬秕熹孱矧篦狃麸铄溴骈铋糸镱疳蝈铘洪铋翎蜱吼狎孱洪铋翩矧铋轰镢蹴孱翎糸镱⒆桢疳趑弪轶屙忮滗邃轭犷雉桢疳趑弪瞵翳屙忮滗邃疳趑弪瞌疳蝈铘箪雉痫轭趔麸翳疳趑弪轸轶屙忮滗邃轭祜镳洪铋翎蜱红镲瓠轰镢蹴孱翎糸镱⒆桢翳弪矧铒翳疳趑弪箬秕熹祜镳麒孱痨狴邃ㄣ戾犷躔洪铋翎蜱恒戾犷躔洪铋翩矧扉篝轰镢蹴孱翎糸镱⒘扉篝镦骢钽糸镱翳狒狎蝓麒孱翳疳趑弪孱潴矧轶篝镳疱洚痼趄遽憝泔躅洪铋翩矧横沣弩箫痼趄遽憝泔躅轰镢蹴孱翎糸镱⒃桢铛礅弪镦痼趄遽眢翳狒栳鲥忮孱磲溴镦翳轶疳趑弪町礤翎溽翎洪铋翎蜱喉弭徜狒洪铋翩矧磲脲栳箬翎忪濠呼疱栳箬翎忪轰镢蹴孱翎糸镱⑷狍翎忪镦徜溟糸镱犰溽翎狍箫汩狒邃鏖翳翳疳趑弪瞵徙沐篌殁戾鏖翳翳囵狒翦蝾礤翎溽翎骢钽糸镱┅ê滹沲礤铘狒轱⒘怏趄徙疳趑弪篚疱蜚灬篌┅ㄤ彐躅箦舡疳蝈铘疳趑弪瞟⑻镲翳蝻蹒辛栽乓围箪雉犷箦翳堍疳蝈铘堍箪雉镦犷疳趑弪铙麸翳轶疳趑弪町灬忮祗è箦舡疳蝈铘扉篝疳蝈铘⒁邈躜箦翳蝻蹒躺釉箦趑轭翳疳蝈铘镦犷疳趑弪骘躅麸辛遗卧豉疱汜箦扉篝扉篝磲疸灬礅溽箦舡疳蝈铘疳蝈铘┅扉篝┅疳趑弪箦翩箪雉鲠祯扉篝ю狎孱舂疳蝈铘┅┅ㄤ镬轶箪雉磲疸狎＇沆矬弪盹鸷箪雉溴骈铋糸镱钺礤ㄣ祜箦颦盹鸷沆狍蟓箪雉ㄣ灬篌镦疳趑弪瞟┅疳趑弪瞟麒孱ㄡ钿铒ㄥ耢箪雉ю狎孱舂箪雉怙躅漯疳趑弪箪雉┅箦舡疳蝈铘箪雉鲠祯疳趑弪箪雉疳趑弪瞟┅┅ㄤ彐礤翳镤轭轸獒扉瀛轭篝犷沐横骠弪è疳趑弪疳趑弪瞟脲箦舡疳蝈铘疳趑弪瞟ㄤ彐躅疳趑弪瞽镡赍泗⒃蝓殒下逝迷轶疳趑弪町渝犰箫囵狒翦蝾К噤彐疳趑弪瞌豉疱镡赍泗ю狒翦蝾┅ㄤ彐躅犰飙疳趑弪铙ī⑶弭扉篝镦翳钺礤镦犰溴骈铄疳趑弪沆狍箦螽渝犰箫噌祆痄彐螫疳趑弪铙ㄤ彐礤翳镤痨狴聃犷è疳趑弪疳趑弪瞟ㄩ箪雉怙躅漯疳趑弪ю灬聃犷舂箪雉鲠祯疳趑弪ю灬聃犷舂扉篝暴┅ㄤ彐礤翳镤箦翩痨狴聃犷舂鲠祯疳趑弪疳趑弪瞟箦翩箪雉鲠祯疳趑弪ю灬聃犷舂ㄥ铙躜瀛扉篝鲠祯濠┅ㄤ彐礤翳镤孱洵聃犷è疳趑弪疳趑弪瞟麒孱箪雉怙躅漯疳趑弪у钿聃犷舂箪雉鲠祯疳趑弪у钿聃犷舂┅ㄤ彐礤翳镤箦翩孱洵聃犷舂鲠祯疳趑弪疳趑弪瞟箦翩箪雉鲠祯疳趑弪у钿聃犷舂ㄥ铙躜瀛扉篝鲠祯濠┅ㄤ彐礤翳镤痨狴è疳趑弪疳趑弪瞟ㄣ祜汶徜ㄡ蟓痼趄遽疳趑弪瞟沆镢氇┅ㄤ彐礤翳镤灬躅汨è疳趑弪疳趑弪瞟痨狴疳趑弪瞟ㄤ彐礤翳镤痨狴轭绛è疳趑弪疳趑弪瞟镳糸镱犰ㄣ祜汶沆镢氇┅麒孱沆镢ㄦ轭疳趑弪ㄣ祜汶翎箅沆镢氅弘妁ㄦ箪雉鲠祯ч翦愆┅┅ㄤ彐礤翳镤祜镳è疳趑弪疳趑弪瞟麒孱箪雉怙躅漯疳趑弪ъ镲瓠皓箪雉鲠祯疳趑弪ъ镲瓠皓┅ㄤ彐礤翳镤箦翩祜镳皓鲠祯疳趑弪疳趑弪瞟箦翩箪雉鲠祯疳趑弪ъ镲瓠皓鲠祯濠ㄤ彐礤翳镤漉è疳趑弪疳趑弪瞟蝈漉沐＇铄舡躔麸疳趑弪瞟弘妁＇漉颟ㄤ彐躅疳趑弪瞽疳蝈铘疳趑弪脲铛暴ㄡ沣蹴蹯狒铋飑ㄣ灬篌ю狒翦蝾┅⑶弭翳握铜翳泔铘衢铋铉疳趑弪镦辛栽乓维矧铋殒翳弪轶瞌镱瀹涉锰劣轶箴邈殒殄洮镱禊泔铙殇弪疳趑弪铙镦翳狒沆狍螽渝犰箫囵狒翦蝾汨殪潋孱Бㄣ桢汶豉疱铛ㄩ铘彗弪癌戾è癌蝈螬躅糸矧窘铛愆铛祆疳趑弪瞟箦翩疳趑弪箪雉鲠祯疳趑弪ю狎孱舂麒孱豉疱疳趑弪沆狍螬ㄩ钽椹麒孱徙沲眭灬翦ㄡ痧孱滏蝈疳趑弪瞟┅ㄩ徙沲眭灬翦蝈疳趑弪瞟┅蹰镳瑚轸璀溴痱邈狒轱ê麽蝾轭绌ㄤ彐躅疳蝈铘疳趑弪疳趑弪瞟⒛屦蝈汜翦犰獒骘囵狒翦蝾疳蝈铘М疳趑弪瞽疳蝈铘疳趑弪瞟ㄤ彐躅疳蝈铘疴轭疳趑弪瞟⒛屦蝈汜翦骘蝼狎麸囵狒翦蝾疳蝈铘М阵疳趑弪瞽疳蝈铘辛栽乓恒灬篌ю忾钿轭篝遽洚疳趑弪瞽疳蝈铘疳趑弪恒灬篌ю忾钿┅ㄤ彐躅疳趑弪瞽汨殪潋孱疳趑弪脲铛暴ㄡ沣蹴蹯狒铋飑ㄣ灬篌ю狒翦蝾┅⑶弭扉篝镦犰翳溟蝈泗汨殪疳趑弪铙镦辛栽乓维轭沆蹁轭犷轭箪雉矧扉篝螽渝犰箫囵狒翦蝾疳蝈铘Б戾è沲扉篝疳趑弪瞟蝈螬ㄤ雉轫弩铛蝈螬箦翩沲蝈盹鲥殒铒灬礅溽疳趑弪瞟豉疱疳趑弪沆狍螬磲疸犷＇ヰ狒翦蝾汨殪潋孱沲颟┅ㄩ徙沲眭灬翦ㄡ痧孱滏蝈沲颟箦翩蝈沲颟┅┅ㄤ彐礤翳镤ヰ狒翦蝾汨殪潋孱è镡赍泗舂铋飑ㄤ彐礤翳镤ヰ狒翦蝾汨殪潋孱è疳趑弪疳趑弪瞟磲疸犷灬礅溽箪雉ㄣ镳扉篝ㄥ铙躜瀛扉篝箪雉鲠祯疳趑弪ㄣ祜箦颦盹鸷箪雉溴骈铋糸镱钺礤箪雉┅┅ㄣ祜箦颦盹鸷沆狍蟓溟蝈泗箪雉ㄣ灬篌镦疳趑弪瞟┅ㄤ彐珏铄蜷疳趑弪瞽礤翎溽翎疳趑弪镳糸镱犰脲ê滹沲礤铘狒轱⑶弭翳鲠祯镦辛栽乓围礤翎溽翎骘伺佼义趱蝾趄蹂狍箦泔钿鲠祯殒翳礤翎溽翎栳犷孱趄骘伺佻矧铋殒轸溟铒舢┅ㄤ彐礤翳镤疳趑弪瞽礤翎溽翎è疳趑弪疳趑弪瞟镳糸镱犰脲鏖翳箪雉礤翎溽翎疳趑弪ㄩ脲ㄧ弭栳箬脲礤翎溽翎礤翎溽翎┅ㄤ彐躅箦翩疳趑弪瞽礤翎溽翎鲠祯疳趑弪脲箦翩ㄧ弭栳箬脲箪雉鲠祯疳趑弪ы弭徜狒岍鲠祯濠ㄤ彐珏铄蜷疱咫疳趑弪瞟ê滹沲礤铘狒轱④⑿邋胲狒翳铄鲠祯镦痼趄遽憩鏖翳秕徜鲠钽轭轸沲蝌孱痫箝糸镱渝犰箫囝屮臾囵邋氕瞌囵邋氕躔麸瞌┅ㄤ彐躅疱咫痼趄遽瞟⑿邋狒翳铄蝈篚祠镦痼趄遽憩鏖翳秕徜鲠钽轭轸骘蝼狎轭翳痱镢弩螽渝犰箫囵邋毵囵邋氕躔麸瞌囝屮臾囝屮舡瞌ㄣ桢汶豉疱ㄩ铘彗弪癌躅戾篌痼趄遽憝痼趄遽愆蝈趱蝾骝镯疱咫疱咫ㄡ蟓痼趄遽痼趄遽愆瞟┅鏖翳箪雉铛礅弪骢趱蝈铛礅弪痼趄遽祜镳烘矧烘蝻衡屐秣恒镬戾泗痼趄遽憝屐舡骢趱蝈痼趄遽ǐ铛礅弪ō骢趱蝈铛礅弪椹┅┅ㄤ彐躅疱咫躔麸痼趄遽镳糸镱犰磲疳趑弪瞽殄熹戾铉翳┅⑿邋狒躔麸翳铄蝈篚祠镦痼趄遽憩鏖翳秕徜鲠钽轭轸骘蝼狎轭翳痱镢弩螽渝犰箫囵邋毵囵邋氕瞌囝屮臾囝屮舡躔麸瞌ㄣ桢汶豉疱ㄩ铘彗弪癌躅戾篌痼趄遽憝痼趄遽愆蝈趱蝾骝镯疱咫躔麸疱咫躔麸ㄡ蟓痼趄遽痼趄遽愆瞟┅鏖翳箪雉铛礅弪骢趱蝈铛礅弪痼趄遽祜镳烘矧烘蝻衡屐秣烘矧蝈航痼趄遽憝屐舡骢趱蝈痼趄遽ǐ铛礅弪ō骢趱蝈铛礅弪椹乎铘殪ㄥ镳蝈螬恒镬戾泗蝈螬┅ㄤ彐珏铄蜷铄疳趑弪瞟ê滹沲礤铘狒轱⑶弭翳铄鲠祯镦痼趄遽憩骢钽糸镱矧雉桢镡赍泗徜鲠钽轭翳痼趄遽骘蝼狎轭翳痱镢弩螽渝犰箫囝屮舡瞌囝屮舡躔麸瞌囵邋毵ê礤翳镤泔礅轭狒轱疳趑弪瞟ㄤ彐礤翳镤铄è镡赍泗舂镡赍泗ㄤ彐礤翳镤铄è疳趑弪疳趑弪瞟铄ㄡ蟓痼趄遽疳趑弪瞟┅ㄤ彐礤翳镤铄è骢钽糸镱骢钽糸镱┅ㄦ躅汜祆骢钽糸镱┅ㄤ彐躅铄舡痼趄遽镳糸镱犰磲疳趑弪瞽殄熹戾铉翳┅⑶弭翳铄秕麴豸镦痼趄遽憩骢钽糸镱矧雉桢镡赍泗徜鲠钽轭翳痼趄遽骘蝼狎糸礤轭翳痱镢弩螽渝犰箫囝屮臾囝屮舡躔麸瞌囵邋毵囵邋氕瞌ㄣ桢汶豉疱ㄩ铘彗弪癌戾è痼趄遽疳趑弪瞽狍痼趄遽痼趄遽愆┅祜镳候屦遽恒镬戾泗铄痼趄遽愆┅ㄤ彐躅铄舡躔麸痼趄遽镳糸镱犰磲疳趑弪瞽殄熹戾铉翳┅⑶弭扉篝镦躔麸蝈篚祠骝镯杏砸帕同铒轭沆蹁轭翳孱镦疳趑弪町渝犰箫囝屮臾囝屮舡瞌囵邋毵囵邋氕躔麸瞌ㄣ桢汶豉疱ㄩ铘彗弪癌戾è痼趄遽疳趑弪瞽狍痼趄遽痼趄遽愆┅祜镳烘矧铛礅弪烘蝻乎痿瑚栝戾铛礅弪瞟烘矧鲠航铄痼趄遽愆洪ㄥ镳鲠飑轰祜镳骈铋箬哄祗恒镬戾泗鲠飑┅ㄤ彐珏铄蜷怏踱箦镡赍泗篝狎舡忮狒镳糸镱犰孱洵忮狒ê滹沲礤铘狒轱④⒙遽篚怏羼堍珏扉篝镦犰弼孱趔骝镯下逝迷麒矬噔遽臾轶釉烈原屡猎矧徕秭瀣犷忮祜盼沫屡猎渝犰箫噱鲥铘蟓轭蜥铉濮┅ㄤ彐珏铄蜷弼孱趔轭蜥铉痼趄遽黹磲ê滹沲礤铘狒轱⑶弭犰翳弼孱趔骝镯杏砸帕麒矬篝狎忮狒狎蜕矧珧遽翦颥犷戾篌翳犷土禺┅ㄤ彐礤翳镤弼孱趔轭蜥铉è疳趑弪疳趑弪瞟黹磲ㄥ鲥铘蟓轭蜥铉ㄡ蟓痼趄遽疳趑弪瞟黹磲┅换痼趄遽ㄤ彐沆狍痼趄遽疳趑弪＋．ㄣ旌殒ㄣ旌骈钿疳汶徵⒂叛张蚊泞Ж横钿Ж猴颟箦聃孱沐è铛礅弪洪铋翩矧轰镢蹴孱翎糸镱⒃桢铛礅弪镦秕麴豸殄熹邃骝镯翳轶痼趄遽犷犷篚猸痼趄遽眢翳狒栳鲥孱溴洚换粕睾蝈钺礤麸翳轶轭溴疳趑弪瞽篝徙洪铋翩矧扉篝轰镢蹴孱翎糸镱⒃桢篝徙镦疳趑弪痼趄遽眢屙忮滗邃轭翳轶痼趄遽懋箫躜沐洪铋翎蜱后秕蜚轰镢蹴孱翎糸镱⒃桢箫躜沐镡赍泗ㄩ瀹疳趑弪瞟翳狒翳轶痼趄遽麽泸遽翦骝镯痼趄遽憝泔躅洪铋翎蜱吼篝蝈犴泔躅横沣弩箫痼趄遽憝泔躅呼疱轭翦珏轰镢蹴孱翎糸镱⑷秣磲铢糸礤痼趄遽麽磲溴镦翳轶痼趄遽恣箫躜沐痱轱麸翳轶痼趄遽懋骑屮犴痨瀣殒轸麽翳骈蝮糸礤噌蟓痼趄遽恣麽汜祆邃镱翳疳趑弪瞵翳轶鏖祆忮爱ㄢ遽洪铋翩矧候遽溴忮狒呼疱铛礅弪轰镢蹴孱翎糸镱⒃桢铛礅弪镦忮狒翳狒栳鲥屐狃箦箝钽翳篝狎镦翳痼趄遽懋ㄨ轶麸蝙呼疱鲥泗矧轰镢蹴孱翎糸镱⒃桢栝篝矧镦秕麴豸殄熹邃怡翳痼趄遽懋ㄨ轶麸蝙铛礅弪洪铋翩矧轰镢蹴孱翎糸镱⒃桢铛礅弪镦轸屙轭翳轶痼趄遽恣栝篝矧拈骀弪骝镯翳铛礅弪箪雉轭翳狒犰秕麴豸狎轫礤溟狒屐轭沆蹁邃轭轸泔躅舢篝狎舡忮狒洪铋翎蜱后翎螋忮狒洪铋翩矧铋轰镢蹴孱翎糸镱⒃桢忮狒铛礅弪镦翳疳蝈铘痼趄遽麒孱翳轶痼趄遽篝狎翦洚ㄦ豸躜瀛铛礅弪洪铋翩矧轰镢蹴孱翎糸镱⒃桢铛礅弪镦疱咫轭麸翳骢趱蝈翳狒栳鲥忮孱磲溴轭翳痼趄遽懋骑屮犴痨瀣殒囵邋毵轶躞邃镱沐翳轶黠蹯忮碑涉囝屮臾轶汜祆邃徭翦翳狒骢趱蝈铛礅弪溴泸遽箦忉汶麸爱ㄦ豸躜瀛忮狒洪铋翩矧轰镢蹴孱翎糸镱⒃桢沲蝌孱忮狒轭沆蹁轭犰骢趱蝈秕麴豸翳噔遽臾箪雉滹弩铒轭沆蹁疱咫邃秕麴豸螬┅ê滹沲礤铘狒轱④⑿狒翦蝾篝蝈犴堍隋屦趄徙镦翳沲蝌孱篝狒镦疳趑弪轭痱镢弩镦殄熹轭轸秕麴豸螽┅ㄤ彐礤翳镤轭轸獒扉瀛轭篝犷沐衡彐矧è痼趄遽痼趄遽愆脲鏖翳箪雉ㄨ轶麸蝙痼趄遽箦翩栝篝矧磲脲狎蜥磲疳趑弪瞽殄熹戾铉翳洪铋糸犰屐屙孱铋飑┅ㄤ彐礤翳镤轭轸獒扉瀛轭篝犷沐横骠弪è痼趄遽痼趄遽愆脲箦舡疳蝈铘痼趄遽愆ㄤ彐礤翳镤痱轭舡镡赍泗è痼趄遽痼趄遽愆篝蝈犴鏖翳箪雉铛礅弪痼趄遽痱轭舡躅蝈徜徕戾镡赍泗痼趄遽篝蝈犴呼疱舂ㄦ矧磲篝蝈犴螈侯蹴忮铛礅弪┅┅ㄤ彐躅痼趄遽憝镡赍泗⒃蝓殒下逝迷轶痼趄遽懋渝犰箫囵篝蝈犴К噌蟓痼趄遽恣豉疱镡赍泗ю篝蝈犴┅ㄤ彐礤翳镤祜镳è痼趄遽痼趄遽愆ㄩ箪雉怙躅漯痼趄遽ъ镲瓠皓箪雉鲠祯痼趄遽ъ镲瓠皓祜镳箪雉鲠祯痼趄遽箫躜沐┅┅ㄤ彐礤翳镤孱溴洵è痼趄遽痼趄遽愆鏖翳箪雉铛礅弪骢趱蝈铛礅弪痼趄遽ㄡ钿铒ō铛礅弪骢趱蝈铛礅弪┅ㄥ镳痼趄遽憝屐痼趄遽暴┅┅ㄤ彐礤翳镤弼孱趔轭蜥铉è痼趄遽痼趄遽愆黹磲麒殪ㄡ钿冀ㄢ遽痼趄遽愆磲铒ㄥ钿邃痼趄遽愆┅戾è铄铄痼趄遽愆┅躅戾篌豉疱铄Ж矧铛祆弼孱舂ㄥ蝌矧㈠鲥铘蟓轭蜥铉汜镱禊忮躞邃镱弼孱篝蝈犴螽┅┅祜镳烘矧横泸矬箪雉鲠祯痼趄遽ц轶麸蝙洪ㄡ钿窘ㄢ遽椹黹瞟ㄢ遽椹磲┅恒镬戾泗洪矧ㄥ镳椹窘ㄢ遽椹磲┅轰祜镳骈铋箬┅ㄤ彐珏铄蜷灬篝秕麴豸痼趄遽愆ê滹沲礤铘狒轱⒁弭躜铙翳灬篝秕麴豸殄熹邃怡杏砸帕彤砒犴痨搴换ㄤ彐疳蜥礤翦痼趄ㄡ蟓痼趄遽痼羼Ж畅暴┅换铄痼趄唤换灬篝秕麴豸痼趄唤渝犰箫噱钿邃皈┅ㄤ彐礤翳镤灬篝秕麴豸è痼趄遽痼趄遽愆鏖翳箪雉铛礅弪骢趱蝈铛礅弪痼趄遽戾è殇ō铛礅弪骢趱蝈铛礅弪┅麒孱痨躞殇痼趄遽憝屐痼趄遽ō殇ㄩㄥ钿邃痼趄遽愆暴┅┅┅ㄤ彐躅鲠祯瀛蝈磲轭轭绛鲠祯濠⒃蝓殒至陶蝈痱弩孱趔翳狒痼趄遽栳秕麴豸堍蝈磲轭轭畿⒒楫瀹至陶轶簌礅镬ㄩ瀹洪铈┈矧铛礅弪珧遽翦翳犷爱渝犰箫囹屙衢铋铉皈噤邈姝蝈磲轭轭绉豉疱汜箦鲠祯铛祆铋飑簌礅镬ㄥ耢鲠祯洪铈┅铛礅弪痨躞鲠祯濠雉桢蝼轶铋飑┅ㄤ彐躅蝈磲轭轭绛疳趑弪镳糸镱犰蝈疱狒蟓脲蝈疱狒螬蝈磲轭轭绛脲с躜蝈铘蝈疱狒蟓蝈磲轭轭绌⒃蝓殒辛栽乓围遗土晌晌黔伺箪雉鲠祯蝈痱弩孱趔秕麴豸堍蝈磲轭轭畿箦圉犰蹂蝈磲轭轭绛皈┊涉辛栽乓围遗土晌晌黔伺箪雉轶躅怙躅矧艾犷遗信猎迎伺轶铒铋飕翳孱轸轶狨麸磲糸汜祆箦麸翳囝屮臾镦辛栽乓围遗信猎迎伺箪雉澡孱殒翳狒铄鲠祯轶矧铋飕蝈磲轭轭绛蝈趱蝾铋飚萧桢蝼轶瀣候弩弭轶蝈趱蝾邃狍珏铄蜥扉邃趄蹂鲠祯犷麸轭溟汜翦翳狒囝屮臾麽汜祆邃镱辛栽乓围遗信猎迎伺箪雉渝犰箫圉犰蹂蝈磲轭轭绛皈噤邈姝蝈磲轭轭绉灬忮祗è箦舡铄ī箦翩箪雉鲠祯疳趑弪蝈磲轭轭绛脲铄箪雉鲠祯疳趑弪蝈疱狒蟓脲┅麒孱鲠祯瀛蝈磲轭轭绛箪雉鲠祯疳趑弪蝈磲轭轭绛脲┅候弩弭┅ㄩ铒箪雉怙躅漯疳趑弪蝈磲轭轭绛脲┅箦舡铄舂戾è蝈憝脲箪雉鲠祯疳趑弪蝈磲轭轭绛脲┅豉疱汜箦蝈憝脲铛祆铋飑簌礅镬ㄥ耢蝈憝脲洪铈┅铛礅弪ㄩ痨躞蝈憝脲箦舡铄舂┅换殒轸犰蝈徜箦麸艾轸麽溴沔т麸轭翳疳趑弪瞵箫麇珏翳铄镱瀹殒翳铄轶艾匀盼麇蝈趱蝾铋飚雉桢蝼轶铋飑┅┅ㄤ彐躅溴沔蝈磲轭轭疳趑弪镳糸镱犰脲с躜蝈铘蝈疱狒蟓蝈磲轭轭绌⒛邈蝈狍辛栽乓围伺鲠祯瀹渝犰箫囹屙衢铋铉皈麒孱铛礅弪箪雉鲠祯疳趑弪脲┅ㄤ邈箪雉鲠祯疳趑弪脲┅┅ㄤ彐礤翳镤疱咫è痼趄遽痼趄遽愆鏖翳箪雉铛礅弪骢趱蝈铛礅弪痼趄遽痼趄遽憝屐舡骢趱蝈痼趄遽ō铛礅弪骢趱蝈铛礅弪┅┅ㄤ彐礤翳镤疱咫è疳趑弪疳趑弪瞟铄ㄡ蟓痼趄遽疳趑弪瞟┅ㄤ彐礤翳镤铄è痼趄遽痼趄遽愆换驷祆忉汶礤翳镤疳趑弪铙箬秕熹秭弪蜷溴翳彘痼趄遽篚忏灬篌弩鏖翳翳彘秣忮栳鲩矧铋飑ㄤ彐礤翳镤铄横蝻躅è痼趄遽痼趄遽愆灬忮祗è珏舡鲠祯瀛骝镯篝徙疳趑弪瞟鏖翳箪雉铛礅弪疳趑弪瞽篝徙氅疳趑弪ㄩ疳趑弪瞽篝徙戾舄è痫痧邃痫疳趑弪瞽篝徙氅铞铄痫痧邃┅ㄩㄥ镳铞ㄧ弭鲠祯瀛骝镯篝徙疳趑弪瞟痱镧瘐箬痫痧邃疳趑弪瞽篝徙氅铞┅痱镧戾è蝈ㄣ犰飙铄舡礤翳镤┅豉疱汜箦蝈疳趑弪ㄩ豉疱疳趑弪Ж矧骢钽糸镱舡痼趄遽愆蝈痱镧换殒囝屮臾蝈趱蝾疳趑弪瞵麇瘐箬轸麸翳疳趑弪篝徙狍痼趄遽戾è痼趄ㄡ蟓痼趄遽蝈螬┅箦翩箪雉鲠祯痼趄篝狎舡忮狒ㄢ遽疳趑弪瞟瘐箬痼趄疳趑弪瞽篝徙氅ㄧ弭鲠祯瀛骝镯篝徙疳趑弪瞟┅蝈螬┅ㄩ钽铛礅弪┅┅┅鏖翳箪雉铛礅弪栝篝矧栝篝矧铛礅弪骢趱蝈铛礅弪痼趄遽戾è蝈篚祠ㄩ痨躞骢趱蝈铛礅弪戾è蝈篚祠ㄥ祠栝篝矧ō铛礅弪骢趱蝈铛礅弪┅┅ㄤ邈骢趱蝈铛礅弪麒孱ㄥ鲥铘蝈篚祠ㄩ钽箪雉鲠祯痼趄遽р遽舂ㄥ鲥铘鲠祯蝈篚祠轰屐翎┅蝈篚祠戾è蝈篚祠蝈篝狎舡汜箦ㄧ弭鲠祯瀛骝镯篝徙痼趄遽愆殄熹秕麴豸é镳糸镱犰鲠祯暴候屦矧灬礅溽螬ㄦ矧磲①殄熹犷犰翦蝾狒秕麴豸骘赢痼趄遽愆洪铘弪徙糸鲥灬礅溽īㄦ矧磲聃弪轱ε铘弪骘蝽麸殄熹ㄦ轭轶璀秕麴豸聃弪轱扉篝ㄥ鲠蝈徜聃弪轱┅┅鲠祯濠┅麒孱ㄥ鲥铘蝈篚祠箦翩蝈篚祠ㄣ镳弼孱蝈篚祠┅麒孱ㄡ钿铛祆蜥鳝弼孱舡鲠祯蝈篚祠衡遽舂铛祆箪雉鲠祯痼趄遽ю狎孱舂┅箦翩ㄢ遽蝈篚祠箪雉鲠祯痼趄遽ф豸躜瀛忮狒┅ㄩ钽箪雉鲠祯痼趄遽р遽舂ㄥ鲥铘鲠祯蝈篚祠轰屐翎┅ㄩ钽箪雉鲠祯痼趄遽ф豸躜瀛忮狒ㄥ鲥铘鲠祯蝈篚祠轰屐翎┅箦翩ㄥ祠栝篝矧盹栝篝矧铛礅弪戾铉翳箪雉鲠祯痼趄遽ц轶麸蝙┅┅蝈篚祠ㄩ钽栝篝矧铛礅弪蝈篚祠┅┅躅戾篌疳趑弪瞽疳蝈铘痼趄遽愆ㄤ镬轶痱镢痫篝疳趑弪瞽秕麴豸痱镢弩箫蝮箦翩蝈篚祠ㄦ躅汜祆痱镢蝈篚祠痼趄遽愆┅蝈篚祠┅┅ㄤ彐鲠痫篝疳趑弪瞽秕麴豸痱镢弩箫蝮扉篝蝈磲瓠轭篝蝓礤铘麸疳蜥礤翦蝮⑻轶镦骢钽糸镱翳狒狎狃痨殄狍翳灬篝篝屦镦疳趑弪秕麴豸珏铄蜥糸镱裴汨秕麴豸殄熹邃怡犷堍秕翦蝽矬糗疳趑弪ㄩ瀹镱鏖翳秕囵狒翦蝾疳蝈铘З鏖祆忮痱镢弩箦ㄡ祜铉鏖翳翳痼趄遽狍箦泔钿狎珲礤铘翳蝻蹒遽汨骢钽糸镱轭翳轶扉篝犰祜鏖铉骘狎忾趄狎趄犷箧矧磲糸镱镦翳珏铄蜥翦秕麴豸螽澡蝈趱蝾鲠祯镦遽汨骢钽糸镱轶躞邃狍翳轭瘐麸翳铄骢钽糸镱犷翳蝈趱蝾鲠祯镦翳灬篝骢钽糸镱轶躞邃狍翳秕麴豸殄熹邃怡翳疳趑弪町澡轶汜忮躞邃骘屮犴痨瀣麸轫痨屙孱磲痧轭珞骝镯骝殄钿禊轭篝蝓礤铘钺礤麸翳骢祆疳蜥礤翦蝮铄邃邃麸箴邈殒翳轭篝蝓礤铘轭聃弩糸镱骘忉汶孱潴篚汨狍蜕纳麒殂蝈聃轵轸轭驷泗翳轶驽狒躜轶犰蝈徜轫痨屙孱翦盹蝈泔铞孱殄铘禊鏖翳翳箦翩徕戾嚅铙趄蹴孱舡磲痧轭绉骢钽糸镱渝犰箫嗒轭篝蝓礤铘磲皙К囹屙狃轭篝蝓礤铘麸疳蜥礤翦蝮Бㄤ彐鲠轭篝蝓礤铘磲皙磲脲栳箬翎忪呼弩＇羼踽飑⑷狍翎忪磲痧轭轭篝蝓礤铘钺礤ㄡ簌礅镬螬麸狎忾趄狎疳蜥礤翦扉篝螽阵邃怡囹屙狃轭篝蝓礤铘麸疳蜥礤翦蝮狍疳螋镦痫篝疳趑弪秕麴豸痱镢弩箝铉令弼孱趔麒矬洪铙趄蹴孱轶铒骘躅轭翳轶翎忪鏖祆铒忮徭驽泗邃渝犰箫囹屙狃轭篝蝓礤铘麸疳蜥礤翦蝮Бㄤ彐躅蝈磲瓠轭篝蝓礤铘麸疳蜥礤翦蝮秕麴豸镳糸镱犰痼趄遽愆⒁屙狃险孕赵轭篝蝓礤铘脲麸狎忾趄狎疳蜥礤翦蝮箴邈殒殄轭嗒轭篝蝓礤铘磲皙М涉险孕赵轶铒犷弼孱矧翳轭篝蝓礤铘轶铒骘躅轭翳磲瓞轸轶疳篌邃翳蝻蹒躅汨犷珏洚渝犰箫嚅铙趄蹴孱舡磲痧轭绉嗒轭篝蝓礤铘磲皙К嗒痫篝疳趑弪瞽秕麴豸痱镢弩箫蝮Бㄤ邈灬蝈ㄩ珙矧痼趄遽愆躅戾篌ㄥ鲥铘秕麴豸蝈趱蝾骝镯蝈磲瓠轭篝蝓礤铘麸疳蜥礤翦蝮秕麴豸┅麒孱戾è磲痧轭ㄧ弭栳箬ㄥ鲥铘鲠祯秕麴豸洪铙趄蹴孱舂轭篝蝓礤铘磲皙┅ㄥ豉疱汜箦磲痧轭簌礅镬箦翩ㄥ鲥铘鲠祯秕麴豸洪铙趄蹴孱舂磲痧轭绌扉篝ㄤ镳扉篝脲鲠祯磲痧轭绌箦翩ㄥ鲥铘鲠祯秕麴豸脲鲠祯濠┅┅秕麴豸ㄤ彐躅轭篝蝓礤铘磲痧轭ㄩ铙趄蹴孱舂⑶弭磲痧轭骝镯晌釉艺团卧ㄡ轭篝蝓礤铘钺礤狍篝蜷铉矧簌礅镬麸痨轶镦疳蜥礤翦蝮麒殂箬秕熹忮箦轭翳弼孱怡囹屙狃轭篝蝓礤铘麸疳蜥礤翦蝮М渝犰箫囹屙狃轭篝蝓礤铘麸疳蜥礤翦蝮К嗒轭篝蝓礤铘磲皙Бㄧ弭栳箬轭篝蝓礤铘轭篝蝓礤铘磲皙┅ㄤ彐躅箦翩轭篝蝓礤铘磲痧轭绌鲠祯轭篝蝓礤铘⒂弭磲痧轭骝镯晌釉艺团卧ㄡ轭篝蝓礤铘钺礤狍篝蜷铉矧簌礅镬麸痨轶镦疳蜥礤翦蝮麒殂鏖祆忮箦轭翳弼孱怡囹屙狃轭篝蝓礤铘麸疳蜥礤翦蝮М渝趑轭犷轭篝蝓礤铘麸铋鏖翳翳轶骢钽糸镱蝈盹鲥轸骝镯翳磲甬渝犰箫嚅铙趄蹴孱舡磲痧轭绉囹屙狃轭篝蝓礤铘麸疳蜥礤翦蝮К嗒轭篝蝓礤铘磲皙Бㄡ篌弪矧豉疱鲠祯Ж矧簌礅镬铛礅弪┅ㄡ钿扉篝鲠祯濠ㄥ鲥铕扉篝戾铉翳鲠祯濠┅鲠祯濠螫至陶狎珲礤铘眭篝忮簌礅镬铛礅弪矧痨轶艋顼轭篝遽洧ч铙趄蹴孱舡磲痧轭鲠祯濠ㄩ鲠祯箦翩ㄧ弭栳箬轭篝蝓礤铘轭篝蝓礤铘磲皙鲠祯濠蝈龛狍轭篝蝓礤铘轭篝蝓礤铘磲皙┅ㄤ彐珏铄蜷狍痼趄遽翳轭绌ê滹沲礤铘狒轱⒁弭躜匀晌狍痼趄遽镡赍泗渝犰箫囵狒翦蝾狍痼趄遽恣┅ㄤ彐躅疳趑弪瞽狍痼趄遽翳轭绌⑻殡噌蟓痼趄遽恣怩镱禊泔铞弪趔匀晌麸痼趄遽殒轸轶疳趑弪町ㄩ豉疱翳轭ю狒翦蝾ㄡ蟓痼趄遽翳轭绌翳轭绌ㄤ彐沆狍舡痼趄遽痼趄遽愆è鲠祯洪铋翎蜱忽犰蹂洪铋翩矧铋轰镢蹴孱翎糸镱⒃桢鲠祯翳狒轶殄熹邃怡翳舡痼趄遽懋戾铉翳洪铋翎蜱红孱玺洪铋翩矧轰镢蹴孱翎糸镱⒃桢铛礅弪镦糸礤麸殄熹翳鲠祯瀹┅ê滹沲礤铘狒轱⑿狒翦蝾篝蝈犴镡赍泗翳狒怡溴驷蹯殄熹轸鲠祯镱禊镱沐┅ㄤ彐躅舡痼趄遽鲠祯镳糸镱犰戾铉翳暴⑼犭舡痼趄遽镡赍泗鏖翳翳鲠祯至陶女ㄣ桢汶豉疱戾铉翳矧ㄩ铘彗弪癌ㄥ耢洪铈┅磲脲轭篝犷沐舡痼趄遽忽犰蹂鲠祯红孱玺戾铉翳┅ㄤ彐礤翳镤痱轭舡镡赍泗è舡痼趄遽舡痼趄遽愆篝蝈犴鏖翳箪雉鲠祯戾铉翳舡痼趄遽痱轭舡躅蝈徜徕戾镡赍泗舡痼趄遽篝蝈犴呼疱舂ㄦ矧磲篝蝈犴螈鲠祯戾铉翳┅┅ㄤ彐礤翳镤狍痼趄遽è鲠祯舂舡痼趄遽鲠祯濠ㄤ彐礤翳镤铄è舡痼趄遽舡痼趄遽愆鏖翳箪雉鲠祯戾铉翳铛礅弪舡痼趄遽麒孱ㄡ钿铒ㄥ耢洪铈戾铉翳┅窘铛礅弪戾铉翳┅蝈趱蝾骝镯铄屣皓ㄩㄦ躅泗轱铕鲠祯濠ㄦ躅汜祆鲠祯濠鲠祯濠┅ㄤ彐礤翳镤狍痼趄遽è疳趑弪疳趑弪瞟戾舄è沆狍ㄣ灬篌镦疳趑弪瞟钺礤ㄣ灬篌钺礤沆狍螬箪雉蝈盹鲥ю狎孱磲疸狎＇沆矬弪盹鸷箪雉溴骈铋糸镱钺礤ㄣ祜箦颦盹鸷沆狍蟓箪雉沆狍螬┅┅ㄡ痧禊＇磲脲轭篝犷沐ㄩ铘弪ㄣ镱汜钺礤Л痼趄遽愆簌礅镬疳汶徵钺礤┅磲疸犷ㄦ麒孱箪雉怙躅漯疳趑弪擤扉篝磲脲脲黠蜾擤疳趑弪瞽狍痼趄遽箪雉鲠祯疳趑弪擤┅┅箪雉螬┅ㄤ彐礤翳镤狍痼趄遽横蝻躅è镡赍泗舂戾è痼趄遽ㄣ犰飙铄舡礤翳镤┅鏖翳箪雉痼趄遽憝泔躅箫躜沐栝篝矧痼趄遽箦翩痼趄遽憝泔躅ㄩ箪雉屮轶趔镡赍泗ю篝蝈犴泔躅舂箪雉鲠祯镡赍泗ю篝蝈犴泔躅舂癌箫躜沐镡赍泗┅麒孱箪雉屮轶趔镡赍泗ю篝蝈犴泔躅舂ㄩ钽箪雉鲠祯镡赍泗ю篝蝈犴泔躅舂┅痼趄遽愆ㄤ彐礤翳镤狍痼趄遽è痼趄遽痼趄遽愆换痱弼孱痼趄遽眢骝镯忮轭Ⅱ瀛泔铞弪翦洧麸痼趄遽眢痼趄遽愆ㄤ彐轭瀛泔钿轸轱痼趄遽憝秕舡镦蜥铉īè轭溴洪铋翎蜱洪钿屮候遽溴痼趄遽憝屐舡轭溴┅ê蝈痫螋灬礅溽ㄣ镱溟糸镱篝蝈犴ㄦ矧磲篝蝈犴⒃桢轭溴驷祆秕趔殇翳筱镳镦翳痼趄遽恣栝篝矧痼趄遽憝屐舡轭溴泔钿轸轱瞟┅┅ㄤ彐躅痼趄遽憝屐舡轭溴麸栝篝矧轭溴痼趄遽轭溴⑶轹孱晌呐噩犷徕箫祯翦轭溴轭麸杏砸帕艇栝篝矧蝈趱蝾翳徙趱犰轭溴轭麸翳沲蝌孱蝈泔蜾邃栝篝矧镦翳痼趄遽懋渝犰箫囵篝蝈犴栝篝矧徜鲠钽瀛怡Бㄣ桢汶豉疱轭溴ㄩ铘彗弪癌鏖翳箪雉ㄨ轶麸蝙痼趄遽盹轭溴戾铉翳栝篝矧┅┅ㄤ彐躅痼趄遽憝屐痼趄遽瞟⑶弭翳昔轸屙轭杏砸帕艇栝篝矧骑铄玑糸鲥维珏翳昔盹篝蝈沐铘轸屙砒犴痨搴换戾è痼趄遽ㄡ蟓痼趄遽痼羼Ж畅┅┅换铄痼趄遽愆唤换痼趄遽憝屐痼趄遽癌唤换骈蝮轸屙轭翳痼趄遽恣栝篝矧换铄痼趄遽愆唤换痼趄遽憝屐痼趄遽暴唤换箦泔钿轸屙轭翳痼趄遽恣栝篝矧换痼趄遽憝屐痼趄遽暴唤换盹篝蝈沐铘轸屙轭翳痼趄遽恣栝篝矧渝犰箫囵篝蝈犴屐舡骢趱蝈К囵栝篝矧Бㄣ桢汶豉疱轭翦珏颟躅戾篌痼趄遽憝痼趄遽愆蝈趱蝾骝镯痼趄遽憝屐痼趄遽憝屐ㄡ蟓痼趄遽痼趄遽愆瞟┅鏖翳箪雉ㄨ轶麸蝙栝篝矧铛礅弪痼趄遽戾è蝈犰轭溴ㄩ黹铛箴瞟ǐ栝篝矧铛礅弪瞟瞟┅ㄩㄡ钿窘蝈犰轭溴磲ō栝篝矧铛礅弪戾铉翳栝篝矧┅┅蝈犰轭溴栝篝矧铛礅弪┅ㄥ祠栝篝矧痼趄遽憝屐舡轭溴麸栝篝矧轭溴痼趄遽蝈犰轭溴┅ㄥ蝌矧ю篝蝈犴秕舡镦蜥铉洪钿屮瞟┅┅ㄤ彐躅痼趄遽憝栝篝矧徜鲠钽瀛怡痼趄遽轭溴换粕睾徜翦篝骘翳轶⒚镱鲥螋栝篝矧轭溴ㄩ瀹痫箝糸鲥铛礅弪痱秭殇邃麸囵篝蝈犴屐舡骢趱蝈З麸翳犴秕铘翳狒翳栝篝矧眭篝忮徜鲠钽邃怡涉翳痱秭殇邃轭溴轶忮骘蝈翳遽蜢殄篝轸屙轭栝篝矧翳蝈篚祠鏖祆忮铄玑糸鲥铛礅弪溴铒糸铉栾驷忮镱翳遽蜢殄篝栝篝矧翳轭溴轶涉翳痱秭殇邃轭溴轶鏖翳轭翳沲蝌孱栝篝矧翳蝈篚祠鏖祆忮弪锂涉翳痱秭殇邃轭溴轶轭翳骢趱蝈翳蝈篚祠鏖祆忮痫箝糸鲥铛礅弪溴铒糸铉栾驷轭翳骢趱蝈轸轶渝犰箫囵篝蝈犴屐舡轭溴麸栝篝矧轭溴Бㄣ桢汶豉疱轭溴ㄩ铘彗弪癌鏖翳箪雉ㄨ轶麸蝙栝篝矧铛礅弪痼趄遽戾è栝篝矧戾铉翳戾铉翳栝篝矧┅ㄩ轭溴ō栝篝矧铛礅弪栝篝矧戾铉翳┅ō栝篝矧铛礅弪栝篝矧戾铉翳ㄩ窘轭溴栝篝矧铛礅弪ō轭溴ū栝篝矧铛礅弪┅癌┅┅ㄤ彐躅痼趄遽憝屐舡骢趱蝈痼趄遽瞟⑶弭翳屐屙孱狩狴骝镯翳盹篝蝈沐铘轭杏砸帕艇栝篝矧疹扉脲囵篝蝈犴屐臾翳轶骢钽糸镱鏖祆狨麸磲糸汜祆疱咫轭麸翳骢趱蝈骘犷痫箝糸鲥萎砒犴痨搴换戾è痼趄遽ㄡ蟓痼趄遽痼羼Ж畅┅┅换痼趄遽憝屐舡骢趱蝈痼趄遽癌唤换铄痼趄遽愆唤换痼趄遽憝屐舡骢趱蝈痼趄遽暴唤换铄痼趄遽愆唤渝犰箫囵篝蝈犴屐臾囵栝篝矧Бㄣ桢汶豉疱轭翦珏颟躅戾篌痼趄遽憝痼趄遽愆蝈趱蝾骝镯痼趄遽憝屐舡骢趱蝈痼趄遽憝屐舡骢趱蝈ㄡ蟓痼趄遽痼趄遽愆瞟┅麒孱黹铛箴瞟蝈趱蝾骝镯痼趄遽憝屐舡骢趱蝈痼趄遽憝屐痼趄遽瞟┅鏖翳箪雉ㄨ轶麸蝙栝篝矧铛礅弪骢趱蝈铛礅弪痼趄遽戾è徜鲠钽瀛怡痼趄遽憝栝篝矧徜鲠钽瀛怡痼趄遽瞟┅麒孱矧黹铛箴徜鲠钽瀛怡ǐ骢趱蝈铛礅弪徜鲠钽瀛怡戾铉翳栝篝矧┅换翳骢趱蝈犷栝篝矧狎蝈泔蜾邃麸翳筢礤狎蜥换箝钽翳狎蜥轶镦骈铋翦箝瀣蝈聃弩糸铉盹蝈骝镯翳骢趱蝈翳犷栝篝矧轶徕戾麸栾熹黠蹯蝈篚祠轭翳镬溴篝屐屙孱趔镦翳骢趱蝈忮轭秭弪黩轸翦鏖翳翳铄麇篝翳躞箦鲥蜷铉翳糸礤扉铄ㄥ蝌矧ю篝蝈犴秕舡镦蜥铉洪钿屮瞟戾è痱弼骢趱蝈铛礅弪骢趱蝈铛礅弪┅箦翩骢趱蝈铛礅弪癌换翦眇矧狎殪箦轸麸箫翳囝屮臾礤翳镤蝓铙铒蝽犰禊祜镳候屦遽徜鲠钽瀛怡烘矧铄航铄痼趄遽愆洪ㄥ鲥铘铄舂轰ㄤ邈箪雉鲠祯痼趄遽р遽舂ㄥ鲥铘鲠祯铄轰屐翎┅箦翩骢趱蝈铛礅弪ǐ痱弼骢趱蝈铛礅弪徜鲠钽瀛怡┅┅戾è蝈犰轭溴痼趄遽憝屐舡轭溴麸栝篝矧轭溴痼趄遽瞟┅ㄥ祠栝篝矧蝈犰轭溴┅┅换疴轭ㄤ彐鲠疴轭洵箴邈獒飙轭轸脲螵扉篝⒃桢扉篝镦箴邈獒脲骘疴轭翳狒犰翦蝮轸漉蜷铉轸轭轸獒扉狒轱町渝犰箫噤彐轭瀛疴轭洵箴邈獒飙轭轸脲Бㄤ彐鲠疴轭洵箴邈獒飙黩狃脲螵扉篝⒃桢扉篝镦箴邈獒脲骘疴轭翳狒汜躞弩翳疴轭麸忮蝈痨徙邃怡犷雉桢疳趑弪漉蜷铉轸轭轸獒扉狒轱町渝犰箫噤彐轭瀛疴轭洵箴邈獒飙黩狃脲Бㄤ彐鲠疴轭洵箴邈獒飙痱镢弩蟓脲螵扉篝⒃桢扉篝镦箴邈獒脲骘疴轭翳狒犰翦翳秕麴豸镦翳疴轭洚渝犰箫噤彐轭瀛疴轭洵箴邈獒飙痱镢弩蟓脲Бㄤ彐沆狍疴轭疳趑弪瞟è疳轵洪铋翎蜱吼衢蝮洪铋翩矧扉篝轰镢蹴孱翎糸镱⒃桢疳趑弪疳轵镦翳疴轭浠痨轶磲痧轭轸脲麸翳彘鲠祯弩┅ê滹沲礤铘狒轱⑿戾狍蝈驽麸翳囵忾钿滹沲礤铘狒轱町┅ㄤ彐躅疴轭é蝈篝疳轵螬疴轭殄熹弼孱趔溴翦蝽轭邃怡轸辛梢蝇麒殂狎扉篝镦脲犷鲠祯弩裴汨脲泔蝌弩痫钿麸脲轭翳蝈篚祠轭弼孱趔犷遽汨鲠祯轶趄遽翦狍疳趑弪翳狒轶弼犰踽翦骘遽汨篝屦镦翳疳趑弪麸珏铄蜥翦翳鲠祯骘轸脲砒犴痨搴换铄舡疴轭烘镲痼羼Ж畅衡狎鸿屐祜穿换换唤è胖盼浩舷郝烈喝盘滔ㄅ峙卧浩舷郝烈喝盘滔ㄅ峙卧浩舷郝烈喝盘滔畔些渝犰箫囵盹铒К囵猝ㄡ篌弪ㄥ鲥铕戾铉翳疳轵螬疳轵螬螫辛梢狎珲礤铘眭篝忮扉篝镦脲鲠祯疳轵螽ю忾钿麒孱ㄣ秕铘吼溴脲疳轵螬暴麽蝾⑼矧翳犷镱吼溴脲溴翦泗邃轭疴轭洚┅戾舄è蝈蟓疳轵扉篝┅疳趑弪瞽汨衢扉篝┅疳趑弪磲脲轭篝犷沐ю忾钿┅ㄤ镳扉篝脲鲠祯疳轵螬麒孱疳趑弪瞽鲠祯濠箦翩箪雉鲠祯鲠祯ю狎孱舂疳趑弪瞟ㄣ镱è痫箝糸镱脲疴轭洵箴邈獒飙轭轸脲螵麒孱戾è蝈篚祠ㄦ躅汜祆ㄧ弭疴轭洵箴邈獒飙轭轸脲螵脲鲠祯疳趑弪瞟┅ㄡ痧孱滏蝈蟓疳轵蝈篚祠┅è痫箝糸镱脲疴轭洵箴邈獒飙黩狃脲螵躅戾篌铛祆蝈蟓疳轵螬箦翩箪雉鲠祯疳趑弪ю衢蝮蝈蟓疳轵螬箦翩蝈蟓疳轵扉篝┅躅戾篌铛祆疳趑弪瞽汨衢瞟箦翩疳趑弪ㄡ痧禊＇疸栳轭ㄡ痧孱疳趑弪瞽汨衢扉篝疳趑弪瞟┅箦翩疳趑弪瞽汨衢扉篝┅箦翩疳趑弪ㄦ躅汜祆ㄧ弭疴轭洵箴邈獒飙黩狃脲螵脲鲠祯疳趑弪瞟┅躅戾篌豉疱疳趑弪ю忾钿ㄡ痧孱滏疳趑弪瞽汨衢扉篝疳趑弪瞟箦翩疳趑弪磲脲轭篝犷沐ю忾钿┅ㄡ痧孱滏蝈蟓疳轵扉篝脲ㄩㄡ钿ㄥ耢脲哄礅邃豉疱鲠祯簌礅镬┅痄彐鲠祯濠鲠祯濠┅┅躅戾篌铛祆蝈蟓疳轵螬箦翩箪雉鲠祯疳趑弪ю衢蝮蝈蟓疳轵螬ㄡ痧孱滏疳趑弪瞽汨衢扉篝疳趑弪瞟躅戾篌戾铉翳疳趑弪瞽汨衢瞟箦翩疳趑弪ㄡ痧禊＇疸栳轭疳趑弪瞽汨衢瞟┅换痱镢弩聃犷脲螽ㄤ镳扉篝疳轵螬麒孱礤礅弪扉篝厚踽铘吼灬聃犷哄钿聃犷舂ㄦ躅汜祆ㄦ溴骈铋糸镱扉篝箦翩ㄥ铙躜瀛簌礅镬с飙疳趑弪铙┅铄雯疳趑弪瞟┅换痱镢弩吼溴脲麒孱戾è痄彐钺礤ㄧ弭疳轵吼溴姗┅痄彐痄彐钺礤疳趑弪瞟疳趑弪瞟瘐箬铄ю忾钿疳趑弪铙箦翩ㄤ镢蹴孱翎糸镱ю忾钿豉疱ㄤ镢蹴孱翎糸镱ю忾钿ф躅泗轱瞟ㄤ彐礤翳镤痱轭舡镡赍泗è疴轭疴轭洎篝蝈犴ㄦ矧磲篝蝈犴簋簋ю忾钿箪雉鲠祯疴轭ю衢蝮┅ㄤ彐礤翳镤ヰ狒翦蝾汨殪潋孱è疴轭疴轭洎磲疸犷灬礅溽箪雉戾è箪雉钺礤ㄣ祜箦颦盹鸷箪雉溴骈铋糸镱钺礤箪雉┅ㄣ镳扉篝ㄥ铙躜瀛扉篝ㄩㄥ耢箪雉钺礤ю衢蝮祜镳烘矧雯猴箪雉鲠祯疴轭箪雉钺礤衡＇沅潋恒镬戾泗雯箪雉鲠祯疴轭箪雉钺礤┅┅┅ㄣ祜箦颦盹鸷沆狍蟓溟蝈泗箪雉ㄦ轭洵沆狍ю忾钿┅┅ㄤ彐礤翳镤脲è疴轭疴轭洎脲箪雉鲠祯疴轭ю衢蝮┅ㄤ彐鲠疳趑弪瞽骢钽糸镱趄犷箪狒轱铙扉篝⒃桢扉篝镦钺礤镦骢钽糸镱犷翳骘蝽翳妁鏖祆忮趄犷箪狒邃麸轭囵猝犷雉桢疳趑弪磲泸矬渝犰箫噤彐轭瀛疳趑弪瞽骢钽糸镱趄犷箪狒轱瞌ㄤ彐磲泸溴骈铄疳趑弪瞽骢钽糸镱趄犷箪狒轱ㄦ躅泗轱疳趑弪瞟⒛彐轭趄犷箪狒轱骝镯普蚊陨衔麸辛栽乓轭囵猝啜箦翩ㄧ弭疳趑弪瞽骢钽糸镱趄犷箪狒轱铙К骢钽糸镱К疳趑弪瞟ㄤ彐轭瀛疳趑弪瞽骢钽糸镱趄犷箪狒轱皤ㄤ彐轭瀛疳趑弪瞽骢钽糸镱趄犷箪狒轱瓠ㄤ彐轭瀛疳趑弪瞽骢钽糸镱趄犷箪狒轱皙ㄤ彐轭瀛疳趑弪瞽骢钽糸镱趄犷箪狒轱鸠ㄤ彐轭瀛疳趑弪瞽骢钽糸镱趄犷箪狒轱蝻躅痤狎蝻躅洎ㄤ彐躅疳趑弪瞽趄犷箪狒瀛箦箦皓⒃蜥铙灬翦优匦麸翳羼蹰鲠戾铘疳趑弪狍疱嗒疳趑弪瞽骢钽糸镱趄犷箪狒轱铙К矧疳篌轸翳蝻蹒躅汨犷珏殒翳弪轶铒趄犷箪狒轱町渝犰箫囵猸趄犷箪狒瀛怙澌骢钽糸镱螫豉疱汜箦箦铛祆箦皓ㄡ麸箦皓扉篝戾舄è骈蝮ㄣ狎箦皓蝈篝ㄣ潋箦皓趄犷箪狒邃ㄧ弭疳趑弪瞽骢钽糸镱趄犷箪狒轱铙骈蝮舂ㄨ遽扉篝ㄩㄦ轭洵殒ㄦ豉疱Ж矧疳趑弪扉篝┅蝈篝矧趄犷箪狒邃骈蝮舂骈蝮舂┅啜黎遽括殒趄犷箪狒邃磲疸狎＇疳趑弪瞽趄犷箪狒瀛箦蝈篝蝈篝┅┅┅ㄤ彐躅疴趄犷箪狒瀛怙澌骢钽糸镱ㄢ镤⒃蜥铙灬翦骢钽糸镱轭孪馁麸翳彘羼蹰鲠戾铘疳趑弪狍疱嗒疳趑弪瞽骢钽糸镱趄犷箪狒轱铙М渝犰箫囵狒翦蝾趄犷箪狒瀛箦皈祜镳烘矧雯猴怙澌衡＇沅潋恒镬戾泗恒镬戾泗疳趑弪瞽趄犷箪狒瀛箦雯┅换粕睾犰祜脲麸忮扉篝蟋轭麒殂汜箦蝈篚祠狎溴篝蝓泗躜邃楫瀹疴衡灬扉篝烘镲衡狎疸沆弩ㄡ薄穿┅蝈篚祠轭骘躜ㄅ峙卧浩舷耗找悲穿ㄤ彐磲泸疴钺礤怙澌疳轵螬疴轶泔铞孱殄钽磲泸铿黩狃痖铉翳骢钽糸镱犰轸镦囵忾钿犷囵溴妲麒殪犰箫痱秭殇轭徜溟糸镱犰簌铘狲篚玑虍瘟团轶翳钺礤镦翳疳趑弪筢礤狍疴轭洄吼溴脲矧囵溴妲轸箦戽┈犷辛梢轶翳筢礤狍轭蝈珲灬疴轭洚涉辛梢轶镱禊镱屐屙孱衄疴镳弪狒弩扉脲囵溴妲雉桢蝼轶轸镳弪狒弩扉脲囵忾钿М澡屮痱弩箝镱轭辛梢狎犰箫狨麸磲糸汜祆趄犷箪狒邃麸羼蹰鲠戾铘疳趑弪铙殒狃痨殂徕戾骘屮犴痨搴换疴烘镲衡狎ǐ痼弪殄螬痼羼扉篝暴┅轶翳筢礤狍换疴烘镲衡狎皤痼弪殄螬痼羼扉篝暴┅渝犰箫囵忾钿К囵溴妲ㄩ戾铉翳疳轵螬啜痄彐钺礤括疴趄犷箪狒瀛怙澌骢钽糸镱疳轵螬啜痄彐钺礤疴轭括疴趄犷箪狒瀛怙澌骢钽糸镱疳轵螬┅┅瘐箬铄ю疳趑弪铙ㄤ彐沆狍疴轭洵痼趄遽疴轭痼趄遽愆īê滹沲礤铘狒轱痼趄遽骘囵忾钿Б┅ㄤ彐礤翳镤痱轭舡镡赍泗è疴轭疴轭洵痼趄遽愆篝蝈犴痱轭舡躅蝈徜徕戾镡赍泗疴轭篝蝈犴呼疱舂ㄦ矧磲篝蝈犴簋" (slot-value pbind 'pairs))))

(defmethod as-pstream ((pbind pbind))
  (let ((name (class-name (class-of pbind)))
        (slots (mapcar #'closer-mop:slot-definition-name (closer-mop:class-slots (class-of pbind)))))
    (apply #'make-instance
           (intern (concat name "-PSTREAM") (symbol-package name))
           (loop :for slot :in slots
                 :for slot-kw := (make-keyword slot)
                 :for bound := (slot-boundp pbind slot)
                 :if bound
                   :collect slot-kw
                 :if (eql :pairs slot-kw)
                   :collect (mapcar 'pattern-as-pstream (slot-value pbind 'pairs))
                 :if (and bound (not (eql :pairs slot-kw)))
                   :collect (slot-value pbind slot)))))

(defmacro define-pbind-special-init-key (key &body body)
  "Define a special key for pbind that alters the pbind during its initialization, either by embedding a plist into its pattern-pairs or in another way. These functions are called once, when the pbind is created, and must return a plist if the key should embed values into the pbind pairs, or NIL if it should not."
  `(setf (getf *pbind-special-init-keys* ,(make-keyword key))
         (lambda (value pattern)
           (declare (ignorable value pattern))
           ,@body)))

;; (define-pbind-special-init-key inst ;; FIX: this should be part of event so it will affect the event as well. maybe just rename to 'synth'?
;;   (list :instrument value))

(define-pbind-special-init-key loop-p
  (setf (loop-p pattern) value)
  nil)

(defmacro define-pbind-special-wrap-key (key &body body)
  "Define a special key for pbind that replaces the pbind with another pattern during the pbind's initialization. Each encapsulation key is run once on the pbind after it has been initialized, altering the type of pattern returned if the return value of the function is non-NIL."
  `(setf (getf *pbind-special-wrap-keys* ,(make-keyword key))
         (lambda (value pattern)
           (declare (ignorable value pattern))
           ,@body)))

(define-pbind-special-wrap-key parp
  (parp pattern value))

(define-pbind-special-wrap-key pfin
  (pfin pattern value))

(define-pbind-special-wrap-key pfindur
  (pfindur pattern value))

(define-pbind-special-wrap-key psync
  (destructuring-bind (quant &optional maxdur) (ensure-list value)
    (psync pattern quant (or maxdur quant))))

(define-pbind-special-wrap-key pdurstutter
  (pdurstutter pattern value))

(define-pbind-special-wrap-key pr
  (pr pattern value))

(define-pbind-special-wrap-key pn
  (pn pattern value))

(define-pbind-special-wrap-key ptrace
  (if value
      (if (eql t value)
          (ptrace pattern)
          (pchain pattern
                  (pbind :- (ptrace value))))
      pattern))

(define-pbind-special-wrap-key pmeta
  (if (eql t value)
      (pmeta pattern)
      pattern))

(define-pbind-special-wrap-key pchain ;; basically the same as the :embed key, but we have it anyway for convenience.
  (pchain pattern value))

(define-pbind-special-wrap-key pparchain
  (pparchain pattern value))

(defmacro define-pbind-special-process-key (key &body body)
  "Define a special key for pbind that alters the pattern in a nonstandard way. These functions are called for each event created by the pbind and must return an event if the key should embed values into the event stream, or `eop' if the pstream should end."
  `(setf (getf *pbind-special-process-keys* ,(make-keyword key))
         (lambda (value)
           ,@body)))

(define-pbind-special-process-key embed
  value)

(defmethod next ((pbind pbind-pstream))
  (labels ((accumulator (pairs)
             (let ((key (car pairs))
                   (val (cadr pairs)))
               (when (and (pstream-p val)
                          (null (slot-value val 'start-beat)))
                 (setf (slot-value val 'start-beat) (beat pbind)))
               (let ((next-val (next val)))
                 (when (eop-p next-val)
                   (return-from accumulator eop))
                 (if (position key (keys *pbind-special-process-keys*))
                     (setf *event* (combine-events *event*
                                                   (funcall (getf *pbind-special-process-keys* key) next-val)))
                     (setf (event-value *event* key) next-val))
                 (if-let ((cddr (cddr pairs)))
                   (accumulator cddr)
                   *event*)))))
    (let ((*event* (make-default-event)))
      (when (eop-p *event*)
        (return-from next eop))
      (setf (slot-value *event* '%beat) (+ (or (slot-value pbind 'start-beat) 0) (beat pbind)))
      (if-let ((pairs (slot-value pbind 'pairs)))
        (accumulator pairs)
        *event*))))

(defmethod as-pstream ((pbind pbind-pstream))
  pbind)

;;; prest

(defclass prest ()
  ((value :initarg :value :initform 1))
  (:documentation "An object representing a rest. When set as a value in an event, the event's :type becomes :rest and the prest's value slot is used as the actual value for the event key instead."))

(defun prest (&optional (value 1))
  "Make a prest object, which, when used in a `pbind' or similar event pattern, turns the current event into a rest and yields VALUE for the key's value.

Note that this is not a pattern; it is just a regular function that returns a prest object.

Example:

;; (next-upto-n (pbind :degree (pseq (list 0 1 (prest 2) 3) 1)))
;; ;=> ((EVENT :DEGREE 0) (EVENT :DEGREE 1) (EVENT :TYPE :REST :DEGREE 2) (EVENT :DEGREE 3))

See also: `pbind', `pbind''s :type key"
  (make-instance 'prest :value value))

(defmethod rest-p ((prest prest))
  t)

;;; pmono

(defun pmono (instrument &rest pairs)
  "pmono defines a mono instrument event pstream. It's effectively the same as `pbind' with its :type key set to :mono.

See also: `pbind'"
  (assert (evenp (length pairs)) (pairs) "s's PAIRS argument must be a list of key/value pairs." 'pmono)
  (apply #'pbind
         :instrument instrument
         :type :mono
         pairs))

(pushnew 'pmono *patterns*)

;;; pseq

(defpattern pseq (pattern)
  (list
   (repeats :default :inf)
   (offset :default 0)
   (current-repeats-remaining :state t))
  :documentation "Sequentially yield items from LIST, repeating the whole list REPEATS times. OFFSET is the offset to index into the list.

Example:

;; (next-n (pseq '(5 6 7) 2) 7)
;; ;=> (5 6 7 5 6 7 EOP)
;;
;; (next-upto-n (pseq '(5 6 7) 2 1))
;; ;=> (6 7 5 6 7 5)

See also: `pser', `eseq'")

(defmethod as-pstream ((pseq pseq))
  (with-slots (repeats list offset) pseq
    (make-instance 'pseq-pstream
                   :list (next list)
                   :repeats (as-pstream repeats)
                   :offset (pattern-as-pstream offset))))

(defmethod next ((pseq pseq-pstream))
  (with-slots (number list offset) pseq
    (when (and (plusp number)
               (zerop (mod number (length list))))
      (decf-remaining pseq))
    (let ((off (next offset)))
      (if (and (not (eop-p off))
               (remaining-p pseq)
               list)
          (elt-wrap list (+ off number))
          eop))))

;;; pser

(defpattern pser (pattern)
  (list
   (length :default :inf)
   (offset :default 0)
   (current-repeats-remaining :state t)
   (current-index :state t))
  :documentation "Sequentially yield values from LIST, yielding a total of LENGTH values.

Example:

;; (next-n (pser '(5 6 7) 2) 3)
;;
;; ;=> (5 6 EOP)

See also: `pseq'")

(defmethod as-pstream ((pser pser))
  (with-slots (list length offset) pser
    (make-instance 'pser-pstream
                   :list (next list)
                   :length (as-pstream length)
                   :offset (pattern-as-pstream offset))))

(defmethod next ((pser pser-pstream))
  (with-slots (list offset current-index) pser
    (let ((remaining (remaining-p pser 'length))
          (off (next offset)))
      (when (or (not remaining)
                (eop-p off))
        (return-from next eop))
      (decf-remaining pser)
      (when (eql :reset remaining)
        (setf current-index 0))
      (prog1
          (elt-wrap list (+ off current-index))
        (incf current-index)))))

;;; pk

(defpattern pk (pattern)
  (key
   (default :default 1))
  :documentation "Yield the value of KEY in the current `*event*' context, returning DEFAULT if that value is nil.

Example:

;; (next-upto-n (pbind :foo (pseq '(1 2 3) 1) :bar (pk :foo)))
;; ;=> ((EVENT :FOO 1 :BAR 1) (EVENT :FOO 2 :BAR 2) (EVENT :FOO 3 :BAR 3))

See also: `pbind', `event-value', `*event*'")

(defmethod as-pstream ((pk pk))
  (with-slots (key default) pk
    (make-instance 'pk-pstream
                   :key key
                   :default default)))

(defmethod next ((pk pk-pstream))
  (with-slots (key default) pk
    (or (event-value *event* key)
        (if (string= :number key)
            (slot-value pk 'number)
            default))))

;;; prand

(defpattern prand (pattern)
  (list
   (length :default :inf)
   (current-repeats-remaining :state t))
  :documentation "Yield random values from LIST.

Example:

;; (next-n (prand '(1 2 3) 5) 6)
;; ;=> (3 2 2 1 1 EOP)

See also: `pxrand', `pwrand', `pwxrand'")

(defmethod as-pstream ((prand prand))
  (with-slots (list length) prand
    (make-instance 'prand-pstream
                   :list (pattern-as-pstream list)
                   :length (as-pstream length))))

(defmethod next ((prand prand-pstream))
  (unless (remaining-p prand 'length)
    (return-from next eop))
  (decf-remaining prand)
  (random-elt (next (slot-value prand 'list))))

;;; pxrand

(defpattern pxrand (pattern)
  (list
   (length :default :inf)
   (last-result :state t)
   (current-repeats-remaining :state t))
  :documentation "Yield random values from LIST, never repeating equal values twice in a row.

Example:

;; (next-upto-n (pxrand '(1 2 3) 4))
;; ;=> (3 1 2 1)

See also: `prand', `pwrand', `pwxrand'"
  :defun (assert (or (not (listp list))
                     (position-if-not (lambda (i) (eql i (car list))) list))
                 (list)
                 "s's LIST argument must have at least two non-eql elements." 'pxrand))

(defmethod as-pstream ((pxrand pxrand))
  (with-slots (list length) pxrand
    (make-instance 'pxrand-pstream
                   :list (pattern-as-pstream list)
                   :length (as-pstream length))))

(defmethod next ((pxrand pxrand-pstream))
  (with-slots (list last-result) pxrand
    (unless (remaining-p pxrand 'length)
      (return-from next eop))
    (decf-remaining pxrand)
    (let ((clist (next list)))
      (setf last-result (loop :for res := (random-elt clist)
                              :if (or (not (slot-boundp pxrand 'last-result))
                                      (not (eql res last-result)))
                                :return res)))))

;;; pwrand

(defpattern pwrand (pattern)
  (list
   (weights :default :equal)
   (length :default :inf)
   (current-repeats-remaining :state t))
  :documentation "Yield random elements from LIST weighted by respective values from WEIGHTS.

Example:

;; (next-upto-n (pwrand '(1 2 3) '(7 5 3) 10))
;; ;=> (1 1 2 2 2 1 2 1 1 3)

See also: `prand', `pxrand', `pwxrand'")

(defmethod as-pstream ((pwrand pwrand))
  (with-slots (list weights length) pwrand
    (make-instance 'pwrand-pstream
                   :list (pattern-as-pstream list)
                   :weights (pattern-as-pstream weights)
                   :length (as-pstream length))))

(defmethod next ((pwrand pwrand-pstream))
  (with-slots (list weights) pwrand
    (unless (remaining-p pwrand 'length)
      (return-from next eop))
    (decf-remaining pwrand)
    (let* ((clist (next list))
           (cweights (cumulative-list (if (eql weights :equal)
                                          (let ((len (length clist)))
                                            (make-list len :initial-element (/ 1 len)))
                                          (normalized-sum (mapcar #'next (next weights))))))
           (num (random 1.0)))
      (nth (index-of-greater-than num cweights) clist))))

;;; pwxrand

(defpattern pwxrand (pattern)
  (list
   (weights :default :equal)
   (length :default :inf)
   (last-result :state t)
   (current-repeats-remaining :state t))
  :documentation "Yield random elements from LIST weighted by respective values from WEIGHTS, never repeating equivalent values twice in a row. This is effectively `pxrand' and `pwrand' combined.

Example:

;; (next-upto-n (pwxrand '(1 2 3) '(7 5 3) 10))
;; ;=> (1 2 1 2 1 3 1 2 1 2)

See also: `prand', `pxrand', `pwrand'"
  ;; FIX: maybe also take the weights into account to see that it doesn't get stuck?
  :defun (assert (or (not (listp list))
                     (position-if-not (lambda (i) (eql i (car list))) list))
                 (list)
                 "s's input list must have at least two non-eql elements" 'pwxrand))

(defmethod as-pstream ((pwxrand pwxrand))
  (with-slots (list weights length) pwxrand
    (make-instance 'pwxrand-pstream
                   :list (pattern-as-pstream list)
                   :weights (pattern-as-pstream weights)
                   :length (as-pstream length))))

(defmethod next ((pwxrand pwxrand-pstream))
  (with-slots (list weights last-result) pwxrand
    (unless (remaining-p pwxrand 'length)
      (return-from next eop))
    (decf-remaining pwxrand)
    (let* ((clist (next list))
           (cweights (cumulative-list (if (eql weights :equal)
                                          (let ((len (length clist)))
                                            (make-list len :initial-element (/ 1 len)))
                                          (normalized-sum (mapcar #'next (next weights)))))))
      (setf last-result (loop :for res := (nth (index-of-greater-than (random 1.0) cweights) clist)
                              :if (or (not (slot-boundp pwxrand 'last-result))
                                      (not (eql res last-result)))
                                :return res)))))

;;; pfunc

(defpattern pfunc (pattern)
  (func
   (length :default :inf)
   (current-repeats-remaining :state t))
  :documentation "Yield the result of evaluating FUNC. Note that the current event of the parent pattern is still accessible via the `*event*' special variable.

Example:

;; (next-upto-n (pfunc (lambda () (random 10)) 4))
;; ;=> ((5 2 8 9))
;;
;; (next-upto-n (pbind :foo (pwhite 0 10 4) :bar (pfunc (lambda () (if (> (event-value *event* :foo) 5) :greater :lesser)))))
;; ;=> ((EVENT :FOO 0 :BAR :LESSER) (EVENT :FOO 6 :BAR :GREATER)
;;      (EVENT :FOO 7 :BAR :GREATER) (EVENT :FOO 8 :BAR :GREATER))

See also: `pf', `pnary', `plazy', `pif'"
  :defun (check-type func function))

(defmethod as-pstream ((pfunc pfunc))
  (with-slots (func length) pfunc
    (make-instance 'pfunc-pstream
                   :func func
                   :length (as-pstream length))))

(defmethod next ((pfunc pfunc-pstream))
  (unless (remaining-p pfunc 'length)
    (return-from next eop))
  (decf-remaining pfunc)
  (funcall (slot-value pfunc 'func)))

;;; pf

(defmacro pf (&body body)
  "Convenience macro for `pfunc' that automatically wraps BODY in a lambda."
  `(pfunc (lambda () ,@body)))

(pushnew 'pf *patterns*)

;;; pr

(defpattern pr (pattern)
  (pattern
   (repeats :default :inf)
   (current-value :state t :initform nil)
   (current-repeats-remaining :state t))
  :documentation "Repeat each value from PATTERN REPEATS times. If REPEATS is 0, the value is skipped.

Example:

;; (next-upto-n (pr (pseries) (pseq '(1 3 0 2) 1)))
;; ;=> (0 1 1 1 3 3)

See also: `pdurstutter', `pn', `pdrop', `parp'")

(defmethod as-pstream ((pr pr))
  (with-slots (pattern repeats) pr
    (make-instance 'pr-pstream
                   :pattern (as-pstream pattern)
                   :repeats (pattern-as-pstream repeats))))

(defmethod next ((pr pr-pstream))
  (with-slots (pattern repeats current-value current-repeats-remaining) pr
    (while (or (not (slot-boundp pr 'current-repeats-remaining))
               (and current-repeats-remaining
                    current-value
                    (not (value-remaining-p current-repeats-remaining))))
      (setf current-value (next pattern))
      (when (eop-p current-value)
        (return-from next eop))
      (setf current-repeats-remaining
            (let ((*event* (if (event-p current-value)
                               (if *event*
                                   (combine-events *event* current-value)
                                   current-value)
                               *event*)))
              (if (typep repeats 'function)
                  (let ((arglist (function-arglist repeats)))
                    (if (null arglist)
                        (funcall repeats)
                        (funcall repeats current-value)))
                  (next repeats)))))
    (when (value-remaining-p current-repeats-remaining)
      (decf-remaining pr)
      current-value)))

;;; plazy

(defpattern plazy (pattern)
  (func
   (repeats :default :inf)
   (current-pstream :state t :initform nil)
   (current-repeats-remaining :state t :initform nil))
  :documentation "Evaluates FUNC to generate a pattern, which is then yielded from until its end, at which point FUNC is evaluated again to generate the next pattern. The pattern is generated a total of REPEATS times.

Example:

;; (next-n (plazy (lambda () (if (= 0 (random 2)) (pseq '(1 2 3) 1) (pseq '(9 8 7) 1)))) 6)
;; ;=> (9 8 7 1 2 3)

See also: `pfunc'")

(defmethod as-pstream ((plazy plazy))
  (with-slots (func repeats) plazy
    (make-instance 'plazy-pstream
                   :func func
                   :repeats (as-pstream repeats))))

(defmethod next ((plazy plazy-pstream))
  (with-slots (func repeats current-pstream current-repeats-remaining) plazy
    (labels ((set-current-pstream ()
               (setf current-pstream (as-pstream (funcall func)))
               (decf-remaining plazy)))
      (when (null current-repeats-remaining)
        (setf current-repeats-remaining (next repeats)))
      (when (null current-pstream)
        (set-current-pstream))
      (unless (remaining-p plazy)
        (return-from next eop))
      (let ((nv (next current-pstream)))
        (if (eop-p nv)
            (progn
              (set-current-pstream)
              (next current-pstream))
            nv)))))

;;; protate

(defpattern protate (pattern)
  (pattern
   (shift :default 0))
  :documentation "Rotate PATTERN N outputs forward or backward, wrapping the shifted items to the other side, a la `alexandria:rotate'.

Example:

;; (next-upto-n (protate (pseq '(1 2 3 4 5) 1) 2))
;; ;=> (4 5 1 2 3)

See also: `pdrop', `phistory', `pscratch'")

(defmethod as-pstream ((protate protate))
  (with-slots (pattern shift) protate
    (make-instance 'protate-pstream
                   :pattern (pattern-as-pstream pattern)
                   :shift (pattern-as-pstream shift))))

(defmethod next ((protate protate-pstream))
  (with-slots (pattern shift number) protate
    (when (zerop number)
      (next-upto-n pattern))
    (let ((actual-index (- number (next shift)))
          (hn (slot-value pattern 'history-number)))
      (if (>= number (1- hn))
          eop
          (pstream-elt pattern (mod actual-index (1- hn)))))))

;;; pn

(defpattern pn (pattern)
  (pattern
   (repeats :default :inf)
   (current-repeats-remaining :state t)
   (current-pstream :state t :initform nil))
  :documentation "Embed the full PATTERN into the pstream REPEATS times.

Example:

;; (next-upto-n (pn (pwhite 0 5 1) 5))
;; ;=> (2 4 2 1 0)

See also: `pr'")

(defmethod as-pstream ((pn pn)) ;; need this so that PATTERN won't be automatically converted to a pstream when the pn is.
  (with-slots (pattern repeats) pn
    (make-instance 'pn-pstream
                   :pattern pattern
                   :repeats (as-pstream repeats))))

(defmethod next ((pn pn-pstream))
  (with-slots (pattern current-pstream) pn
    (let ((rem (remaining-p pn)))
      (when (eql :reset rem)
        (setf current-pstream (as-pstream pattern)))
      (let ((nv (next current-pstream)))
        (while (and (eop-p nv) rem)
          (decf-remaining pn)
          (setf rem (remaining-p pn))
          (setf current-pstream (as-pstream pattern))
          (setf nv (next current-pstream)))
        (if rem
            nv
            eop)))))

;;; pshuf

(defpattern pshuf (pattern)
  (list
   (repeats :default :inf)
   (shuffled-list :state t)
   (current-repeats-remaining :state t))
  :documentation "Shuffle LIST, then yield each item from the shuffled list, repeating it REPEATS times.

Example:

;; (next-upto-n (pshuf '(1 2 3) 2))
;;
;; ;=> (3 1 2 3 1 2)

See also: `prand'")

(defmethod as-pstream ((pshuf pshuf))
  (with-slots (list repeats) pshuf
    (let ((list (typecase list
                  (pattern (next-upto-n list))
                  (function (funcall list))
                  (list list))))
      (make-instance 'pshuf-pstream
                     :list (next list)
                     :repeats (as-pstream repeats)))))

(defmethod next ((pshuf pshuf-pstream))
  (with-slots (list number shuffled-list) pshuf
    (when (and (zerop (mod number (length list)))
               (plusp number))
      (decf-remaining pshuf))
    (let ((rem (remaining-p pshuf)))
      (unless rem
        (return-from next eop))
      (when (eql :reset rem)
        (setf shuffled-list (shuffle (copy-list list)))) ;; alexandria:shuffle destructively modifies the list, so we use copy-list in case the user provided a quoted list as input.
      (nth (mod number (length shuffled-list))
           shuffled-list))))

;;; pwhite

(defpattern pwhite (pattern)
  ((lo :default 0)
   (hi :default 1)
   (length :default :inf)
   (current-repeats-remaining :state t))
  :documentation "Linearly-distributed random numbers between LO and HI, inclusive.

Example:

;; (next-upto-n (pwhite 0 10 16))
;; ;=> (7 2 4 5 7 10 4 8 10 2 3 5 9 2 5 4)

See also: `pexprand', `pbrown', `pgauss', `prand'"
  ;; if only one argument is provided, we use it as the "hi" value
  :defun (defun pwhite (&optional (lo 0 lo-provided-p) (hi 1 hi-provided-p) (length :inf))
           (make-instance 'pwhite
                          :lo (if hi-provided-p
                                  lo
                                  0)
                          :hi (if hi-provided-p
                                  hi
                                  (if lo-provided-p
                                      lo
                                      1))
                          :length length)))

(defmethod as-pstream ((pwhite pwhite))
  (with-slots (lo hi length) pwhite
    (make-instance 'pwhite-pstream
                   :lo (pattern-as-pstream lo)
                   :hi (pattern-as-pstream hi)
                   :length (as-pstream length))))

(defmethod next ((pwhite pwhite-pstream))
  (with-slots (lo hi) pwhite
    (unless (remaining-p pwhite 'length)
      (return-from next eop))
    (decf-remaining pwhite)
    (let ((nlo (next lo))
          (nhi (next hi)))
      (when (or (eop-p nlo)
                (eop-p nhi))
        (return-from next eop))
      (random-range nlo nhi))))

;;; pbrown

(defpattern pbrown (pattern)
  ((lo :default 0.0)
   (hi :default 1.0)
   (step :default 0.125)
   (length :default :inf)
   (current-repeats-remaining :state t)
   (current-value :state t :initform nil))
  :documentation "Brownian motion within a range; each output randomly a maximum of STEP away from the previous. LO and HI define the lower and upper bounds of the range. STEP defaults to 1 if LO and HI are integers.

Example:

;; (next-upto-n (pbrown 0 10 1 10))
;; ;=> (2 3 3 3 4 3 4 5 6 5)

See also: `pwhite', `pexprand', `pgauss'"
  ;; if only one argument is provided, we use it as the "hi" value
  :defun (defun pbrown (&optional (lo 0.0 lo-provided-p) (hi 1.0 hi-provided-p) (step 0.125 step-provided-p) (length :inf))
           (let ((lo (if hi-provided-p
                         lo
                         (if (integerp lo) 0 0.0)))
                 (hi (if hi-provided-p
                         hi
                         (if lo-provided-p
                             lo
                             1))))
             (make-instance 'pbrown
                            :lo lo
                            :hi hi
                            :step (if step-provided-p
                                      step
                                      (if (and (integerp lo)
                                               (integerp hi))
                                          1
                                          0.125))
                            :length length))))

(defmethod as-pstream ((pbrown pbrown))
  (with-slots (lo hi step length) pbrown
    (make-instance 'pbrown-pstream
                   :lo (pattern-as-pstream lo)
                   :hi (pattern-as-pstream hi)
                   :step (pattern-as-pstream step)
                   :length (as-pstream length))))

(defmethod next ((pbrown pbrown-pstream))
  (unless (remaining-p pbrown 'length)
    (return-from next eop))
  (decf-remaining pbrown)
  (with-slots (lo hi step current-value) pbrown
    (let ((nlo (next lo))
          (nhi (next hi))
          (nstep (next step)))
      (when (member eop (list nlo nhi nstep))
        (return-from next eop))
      (unless current-value
        (setf current-value (random-range nlo nhi)))
      (incf current-value (random-range (* -1 nstep) nstep))
      (setf current-value (clamp current-value nlo nhi)))))

;;; pexprand
;; FIX: integer inputs should result in integer outputs

(defpattern pexprand (pattern)
  ((lo :default 0.0001)
   (hi :default 1.0)
   (length :default :inf)
   (current-repeats-remaining :state t))
  :documentation "Exponentially-distributed random numbers between LO and HI. Note that LO and HI cannot be 0, and that LO and HI must have the same sign or else complex numbers will be output.

Example:

;; (next-upto-n (pexprand 1.0 8.0 4))
;; ;=> (1.0420843091865208d0 1.9340168112124456d0 2.173209129035095d0 4.501371557329618d0)

See also: `pwhite', `pbrown', `pgauss', `prand'"
  :defun (defun pexprand (&optional (lo 0.0001) (hi 1.0) (length :inf))
           (assert (and (numberp lo)
                        (not (zerop lo)))
                   (lo)
                   "s's LO argument must be a nonzero number; got s" 'pexprand lo)
           (assert (and (numberp hi)
                        (not (zerop hi)))
                   (hi)
                   "s's HI argument must be a nonzero number; got s" 'pexprand hi)
           (make-instance 'pexprand
                          :lo lo
                          :hi hi
                          :length length)))

(defmethod as-pstream ((pexprand pexprand))
  (with-slots (lo hi length) pexprand
    (make-instance 'pexprand-pstream
                   :lo (pattern-as-pstream lo)
                   :hi (pattern-as-pstream hi)
                   :length (as-pstream length))))

(defmethod next ((pexprand pexprand-pstream))
  (with-slots (lo hi) pexprand
    (unless (remaining-p pexprand 'length)
      (return-from next eop))
    (decf-remaining pexprand)
    (let ((nlo (next lo))
          (nhi (next hi)))
      (when (or (eop-p nlo)
                (eop-p nhi))
        (return-from next eop))
      (exponential-random-range nlo nhi))))

;;; pgauss

(defpattern pgauss (pattern)
  ((mean :default 0.0)
   (deviation :default 1.0)
   (length :default :inf)
   (current-repeats-remaining :state t))
  :documentation "Random numbers distributed along a normal (gaussian) curve. MEAN is the \"center\" of the distribution, DEVIATION is the standard deviation (i.e. the higher the value, the further the outputs are spread from MEAN).

Example:

;; (next-n (pgauss) 4)
;; ;=> (0.08918811646370092d0 0.1745957067161632d0 0.7954678768273173d0 -1.2215823449671597d0)

See also: `pwhite', `pexprand', `pbrown'")

(defmethod as-pstream ((pgauss pgauss))
  (with-slots (mean deviation length) pgauss
    (make-instance 'pgauss-pstream
                   :mean (pattern-as-pstream mean)
                   :deviation (pattern-as-pstream deviation)
                   :length (as-pstream length))))

(defmethod next ((pgauss pgauss-pstream))
  (with-slots (mean deviation) pgauss
    (unless (remaining-p pgauss 'length)
      (return-from next eop))
    (decf-remaining pgauss)
    (let ((nmean (next mean))
          (ndev (next deviation)))
      (when (or (eop-p nmean)
                (eop-p ndev))
        (return-from next eop))
      (random-gauss nmean ndev))))

;;; pseries

(defpattern pseries (pattern)
  ((start :default 0)
   (step :default 1)
   (length :default :inf)
   (current-repeats-remaining :state t)
   (current-value :state t))
  :documentation "Yield START, then generate subsequent outputs by adding STEP, for a total of LENGTH outputs.

Example:

;; (next-upto-n (pseries 1 2 4))
;; ;=> (1 3 5 7)

See also: `pseries*', `pgeom', `paccum'")

(defmethod as-pstream ((pseries pseries))
  (with-slots (start step length) pseries
    (make-instance 'pseries-pstream
                   :start (pattern-as-pstream start)
                   :step (pattern-as-pstream step)
                   :length (as-pstream length))))

(defmethod next ((pseries pseries-pstream))
  (with-slots (start step current-value) pseries
    (unless (slot-boundp pseries 'current-value)
      (setf current-value (next start)))
    (unless (and (remaining-p pseries 'length)
                 current-value)
      (return-from next eop))
    (decf-remaining pseries)
    (let ((nxt (next step)))
      (prog1
          current-value
        (if (numberp nxt)
            (incf current-value nxt) ;; FIX: current-value should be CURRENT value, not the next one! also write tests for this!
            (setf current-value eop))))))

;;; pseries*

(defun pseries* (&optional (start 0) (end 1) length)
  "Syntax sugar to generate a `pseries' whose values go from START to END linearly over LENGTH steps. If LENGTH is not provided, it is calculated such that the step will be 1. Note that LENGTH cannot be infinite since delta calculation requires dividing by it.

Based on the Pseries extension from the ddwPatterns SuperCollider library.

Example:

;; (pseries* 0 10 16)
;; ;=> (pseries 0 2/3 16)
;;
;; (next-upto-n *)
;; ;=> (0 2/3 4/3 2 8/3 10/3 4 14/3 16/3 6 20/3 22/3 8 26/3 28/3 10)

See also: `pseries', `pgeom', `pgeom*'"
  (check-type length (or null (integer 2) pattern))
  (let ((length (or length
                    (max 2 (round (1+ (abs (- end start))))))))
    (pseries start (/ (- end start) (1- length)) length)))

(pushnew 'pseries* *patterns*)

;;; pgeom

(defpattern pgeom (pattern)
  ((start :default 1)
   (grow :default 2)
   (length :default :inf)
   (current-repeats-remaining :state t)
   (current-value :state t))
  :documentation "Yield START, then generate subsequent outputs by multiplying by GROW, for a total of LENGTH outputs.

Example:

;; (next-upto-n (pgeom 1 2 4))
;; ;=> (1 2 4 8)

See also: `pseries', `paccum'")

(defmethod as-pstream ((pgeom pgeom))
  (with-slots (start grow length) pgeom
    (make-instance 'pgeom-pstream
                   :start (pattern-as-pstream start)
                   :grow (pattern-as-pstream grow)
                   :length (as-pstream length))))

(defmethod next ((pgeom pgeom-pstream))
  (with-slots (start grow current-value) pgeom
    (unless (slot-boundp pgeom 'current-value)
      (setf current-value (next start)))
    (unless (remaining-p pgeom 'length)
      (return-from next eop))
    (decf-remaining pgeom)
    (if (zerop (slot-value pgeom 'number))
        current-value
        (let ((n (next grow)))
          (if (eop-p n)
              eop
              (setf current-value (* current-value n)))))))

;;; pgeom*

(defun pgeom* (&optional (start 0.01) (end 1) (length 16))
  "Syntax sugar to generate a `pgeom' whose values go from START to END exponentially over LENGTH steps. LENGTH cannot be infinite since delta calculation requires dividing by it.

Based on the Pgeom extension from the ddwPatterns SuperCollider library.

Example:

;; (pgeom* 1 100 8)
;; ;=> (pgeom 1 1.9306977 8)
;;
;; (next-upto-n *)
;; ;=> (1 1.9306977 3.7275934 7.196856 13.894953 26.826954 51.79474 99.999985)
;; ;; Note that due to floating point rounding errors the last output may not always be exactly END.

See also: `pgeom', `pseries', `pseries*'"
  (check-type length (or (integer 2) pattern))
  (pgeom start (expt (/ end start) (/ 1 (1- length))) length))

(pushnew 'pgeom* *patterns*)

;;; ptrace

(defpattern ptrace (pattern)
  ((trace :default t)
   (prefix :default nil)
   (stream :default t))
  :documentation "Print the PREFIX and each output of TRACE to STREAM. If TRACE is t, print `*event*'. If TRACE is a different symbol, print the value of that symbol in `*event*'. If TRACE is a pattern, ptrace yields its output unaffected. Otherwise, it yields t.

See also: `debug-recent-events'")

(defmethod as-pstream ((ptrace ptrace))
  (with-slots (trace prefix stream) ptrace
    (make-instance 'ptrace-pstream
                   :trace (pattern-as-pstream trace)
                   :prefix (pattern-as-pstream prefix)
                   :stream (pattern-as-pstream stream))))

(defmethod next ((ptrace ptrace-pstream))
  (with-slots (trace prefix stream) ptrace
    (let ((prefix (next prefix))
          (stream (next stream)))
      (if (eql trace t)
          (progn
            (format stream "&@[a ]s%" prefix *event*)
            t)
          (typecase trace
            ((or list symbol)
             (progn
               (format stream "&@[a ]蠛簋［夯蔺ア痱彐轼磲疸狎灬礅溽簌礅镬扉篝簌礅镬ㄥ鲥铘鲠祯弼孱舄簌礅镬┅ㄥ铙躜瀛扉篝趄徙濠┅舂疳趑弪戾è蝈铄趄徙濠┅ㄦ矧磲篝蝈犴累蔺簋ア痱彐轼蝈螬蝈螬┅┅┅换痨徙ㄤ彐疳趑弪痨徙疳趑弪瞟扉篝蝈疱狒轰彐狨祠洪铈ㄣ躜蝈铘蝈疱狒后翎翦舂ㄣ躜蝈铘蝈疱狒蟓蝈磲轭轭后翎翦舂轰镢蹴孱翎糸镱①殄熹遽汨鲠祯骝镯躺釉轭箦聃孱沐涉翳鲠祯轶扉篝翳骈蝮屐屙孱镦翳狒扉篝轶殄熹邃澡箦泔钿糸礤翳狒篚猸扉篝轶孱泔躅翦蝈洮轸箦泔钿屐屙孱鏖祆忮殄熹邃翳翳轵糸礤翳翳轵屐屙孱衄犷箫镱遗信猎泔铘蝻祗翳铛礅弪镦糸礤躺釉轶蝈疱狒邃砒犴痨搴换铄舡躔麸痨徙扉篝扉篝旦畅换唤ū旦渝犰箫囵疳綮徙濮ㄤ彐礤翳镤狍痼趄遽è痨徙痨徙濠鏖翳箪雉扉篝蝈疱狒螬痨徙磲脲轭篝犷沐ю灬沐痼趄遽红轶铄扉篝候屦遽趔ㄡ蟓痼趄遽蝈疱狒螬恒躜蝈铘蝈疱狒癌┅ㄤ彐礤翳镤铄è痨徙痨徙瀛痼趄遽愆鏖翳箪雉铛礅弪扉篝沲蝌孱舡蝈疱狒痨徙麒孱ㄡ钿铒铛礅弪癌盹铛礅弪戾铉翳扉篝┅┅ㄩ钽沲蝌孱舡蝈疱狒ㄤ邈姝蝈磲轭轭痨徙濠躅戾篌ㄩ痨躞铛礅弪ㄡ钿铒ㄥ钿邃痨徙濠蝈磲轭轭绛痨徙濠蝈磲轭轭绛痨徙濠蝈趱蝾骝镯铄屣皓戾舄è盹盹铛礅弪戾铉翳扉篝┅蝈篚祠铄铘盹扉篝┅┅ㄩ扉篝蝈篚祠ㄥ祠黩狃蝈篚祠沲蝌孱舡蝈疱狒蝈篚祠┅┅换痧狒灬沐ㄤ彐疳趑弪痧狒灬沐疳趑弪瞟扉篝蝈疱狒轰彐狨祠洪铈ㄣ躜蝈铘蝈疱狒蟓蝈磲轭轭后翎翦舂轰镢蹴孱翎糸镱①殄熹遽汨鲠祯骝镯躺釉轭箦聃孱沐矧镱秕麴豸骝镯遽汨疳趑弪轭躺釉疱泫沆镦翳扉篝涉镱镦翳疳趑弪铙屙忮滗邃轭躺釉孱潴箫镱弪翳犷翳雉桢蝮轸轶箝眇禊蝈盹鲥犷翳痧狒灬沐泔铘轭蹂麸殄熹骝镯翳蝈篝镦翳躺釉澡孱糸蝈躺釉轶殄熹邃翳蝻蹒麸翎镦遗信猎糸礤螽砒犴痨搴换铄舡躔麸痧狒灬沐扉篝痼羼扉篝畅暴换痼羼扉篝订博┅换唤ū订渝犰箫囵灬沐Бㄤ彐礤翳镤狍痼趄遽è痧狒灬沐痧狒灬沐┅鏖翳箪雉蝈疱狒扉篝痧狒灬沐磲脲轭篝犷沐ю疳綮徙瀛痼趄遽红轶磲疸狎＇疳趑弪瞽狍痼趄遽扉篝候屦遽趔ㄡ蟓痼趄遽蝈疱狒螬┅ㄤ彐礤翳镤铄è痧狒灬沐痧狒灬沐痼趄遽愆鏖翳箪雉铛礅弪扉篝痧狒灬沐麒孱ㄡ钿铒弪镳铛礅弪┅铒弪镳戾铉翳扉篝┅弪镳盹铛礅弪戾铉翳扉篝┅┅ㄤ邈姝蝈磲轭轭痧狒灬沐┅麒孱矧铒扉篝ㄡ钿痨躞铛礅弪ㄥ钿邃痧狒灬沐┅铒蝈磲轭轭绛痧狒灬沐┅蝈趱蝾骝镯铄屣皓戾è蝈篚祠铄ㄥ祠黩狃扉篝铛礅弪┅┅躅戾篌ㄥ镳蝈篚祠蝈趱蝾骝镯铄蝈篚祠┅箦翩扉篝蝈盹鲥殒＇孱溴洵扉篝┅铄痧狒灬沐┅┅换痤狎ㄤ彐疳趑弪痤狎疳趑弪瞟镳弪狒矧疳趑弪铙洪铋翎蜱吼狒翦蝾螬轰镢蹴孱翎糸镱①殄熹翳蝈篚祠镦狃痨轭闲乓猎弦麸遽汨鲠祯殄熹邃怡遽汨疳趑弪轭辛栽乓斡砒犴痨搴换铄舡躔麸痤狎痼羼扉篝ЙЛИН博博换唤ù暴渝犰箫囵骢钽К囵К囵К囵К囵Б轰彐躅ㄤ彐躅痤狎镳弪狒矧蝈篝疳趑弪铙磲脲轭篝犷沐ю钺蝙猴疱蜥麸镳弪狒矧吼狒翦蝾疳趑弪铙┅ㄤ彐礤翳镤狍痼趄遽è痤狎痤狎┅鏖翳箪雉镳弪狒矧疳趑弪铙痤狎磲脲轭篝犷沐ю钺蝙痼趄遽猴疱蜥麸疳趑弪瞽狍痼趄遽镳弪狒矧吼狒翦蝾磲疸狎＇疳趑弪瞽狍痼趄遽疳趑弪铙┅┅ㄤ彐礤翳镤铄è痤狎痤狎痼趄遽愆鏖翳箪雉镳弪狒矧疳趑弪铙痤狎戾è镳ㄩ痼趄遽憝镳弪狒矧铄镳弪狒矧镳弪狒矧┅铄趔磲疸狎＇铄疳趑弪铙┅麒孱矧痫箝糸镱屣铄趔ㄥ镳镳┅蝈趱蝾骝镯铄屣皓蝈篝狎舡汜箦ㄨ犷潇弪忾钿è豉疱弪蝻灬礅溽ㄣ麒孱戾è蝈篝狎ㄦ轭洵蝈篝狎蝈趄鏖翳痱弩舡鲠祯弩悌┅ㄩ铞镫瀛蝈篝狎蝈篝狎舂┅┅ㄡ痧禊＇眭祠榄汨犷铄飙骢钽犰镳铄趔┅蝈趄鏖翳痱弩舡鲠祯弩ī呼弩灬礅溽ㄣㄡ钿豉疱豉疱弪蝻颟ㄥ耢ь蹴忮豉疱弪蝻颦屮疱泗邃豉疱悌豉疱豉疱弪蝻颦溽趱悌ю蝈篝┅灬忮祗è蝈痨徙瀛痱弩趔扉篝磲疸狎灬礅溽ㄩ翦愆豉疱汜箦轸屙扉篝蝈痨徙瀛痱弩趔轸屙┅痱弩箪雉鲠祯轸屙鲠祯濠轸屙┅扉篝┅ㄡ痧禊＇眭祠榄汨犷铄飙骢钽犰镳蝈痨徙瀛痱弩趔铄趔┅┅┅┅ㄥ鲠飙麒孱ê泔眇殪瀛麸痨弼屐红镝洵麸痨弼屐哄邈豸濠ㄤ彐躅磲脲疳趑弪瞽骘颦骢钽糸镱ㄦ躅泗轱瞟⑶孱弪狒涕箴镦黩狃疱骢钽糸镱钺礤鹌瘴迷上麒矬溴骈铋糸镱轶痤狎普蚊陨衔┊渝犰箫囵钺蝙Б戾舄è疳舡簌ㄩ铘弪ㄣ镱汜ю骢钽糸镱с飙疳趑弪铙┅ㄡ蜱箪轶ㄦ躅泗轱瞽狎珈轶骢钽糸镱┅ㄦ躅悱钺礤篝蜷铉滹黝汜箦骢钽糸镱┅ㄦ蹯飙骢钽钺礤ㄩㄥ耢ㄦ轭洵疳汶徵с飙疳趑弪铙簌礅镬疳汶徵骢钽糸镱┅骢钽钺礤ㄣ镱汜篝蜷铉滹黝汜箦疳汶徵瀛钺礤簌礅镬疳汶徵骢钽糸镱┅⒑骢钽钺礤┅疳蝮邃眭祠轲戾鲠祯瀛扉篝疳蝮瀛矧溟钺蝙灬礅溽扉篝狎珞扉篝┅ㄨ狍蝈篝翳轵疳蝮邃┅ㄡ蜱ㄡ痧孱ㄦ轵篝疳蝮邃磲疸狎＇汜箦泔钿疳蝮邃┅ㄥ铙躜瀛扉篝翳轵疳蝮邃┅┅啜痱镧ㄤ彐躅疳舡簌狎珞扉篝ㄣ镱汜⒂铘狲篚玑骘痤狎＇骢钽钺礤┊渝犰箫囵钺蝙К啖骢祆骢钽钺礤Бìㄩ栳蟓蝈篝п痧禊ф躅汜祆＇痤狎＇骢钽糸镱泪蜱螬瘐箬铄К疳舡簌疳趑弪铙┅┅．啜痱镧括磲疸狎＇磲脲疳趑弪瞽骘颦骢钽糸镱Ж窘冀羼黩狃┅换痱弪犷珏ㄤ彐疳趑弪痱弪犷珏疳趑弪瞟ㄩ铕豸骝镯蜥铉麸蜥铉濠轰镢蹴孱翎糸镱⒁屙狃晌姓骝镯镱蜥铉瀣箴邈殒殄怡埔贤伊吻努麸犷雉桢蜥铉瀣箴邈殒殄怡韵伊吻女物翦翳狒翳轶轶彐驽泗轹屐泔铞孱殄钽黩狃疱秭弪囵钺蝙犷囹弪犷珏Щ翳躞秕箬秕熹箦囹弪犷珏骘盹蝈轭骘蝽狒轱町渝犰箫囹弪犷珏К囵钺蝙Бㄤ彐礤翳镤狍痼趄遽è痱弪犷珏痱弪犷珏┅鏖翳箪雉ㄩ铕豸骝镯蜥铉麸蜥铉濠痱弪犷珏磲脲轭篝犷沐ю蝈蜥铉瀛痼趄遽洪铕豸疳趑弪瞽狍痼趄遽轭瘐舂烘蝻憝蜥铉疳趑弪瞽狍痼趄遽骝镯蜥铉濠呼锃蜥铉疳趑弪瞽狍痼趄遽麸蜥铉濠┅ㄤ彐礤翳镤铄è痱弪犷珏痱弪犷珏痼趄遽愆鏖翳箪雉ㄩ铕豸骝镯蜥铉麸蜥铉濠痱弪犷珏戾è轭瘐铄轭瘐舂ㄦ蝻憝蜥铉铄骝镯蜥铉濠麸蜥铉铄麸蜥铉濠┅麒孱礤礅弪屣扉篝轭瘐骝镯蜥铉麸蜥铉濠蝈趱蝾骝镯铄屣皓蝈蜥铉轭瘐骝镯蜥铉麸蜥铉濠┅换痼扉溴ㄤ彐疳趑弪痼扉溴疳趑弪瞟扉篝蝈疱狒轰彐狨祠洪铈戾轰彐狨祠畅篝屦轰彐狨祠暴篝狎轰彐狨祠癌黩狃狒孱轰彐狨祠舂ㄣ躜蝈铘蝈疱狒蟓蝈磲轭轭后翎翦舂ㄣ躜蝈铘蝈疱狒后翎翦洪铋翩矧铋飑蝈磲轭轭绛沲蝌孱舡箦珥孱后翎翦洪铋翩矧铋飑ㄣ躜蝈铘鲠祯后翎翦洪铋翩矧铋飑ㄣ躜蝈铘扉篝戾铉翳后翎翦洪铋翩矧铋飑轰镢蹴孱翎糸镱④⒂扉溴堍徙蝻篌箦泗轱铙镦躺釉遗信猎轶翳麸翎铛礅弪镦箦泗轱铙麸秕麴豸膛翳戾铉翳镦翳箦泗轱町釉判轶翳铛礅弪麸轭泸屙孱翳篝狎轭溴怡徭翦遽汨箦泗轱瞵犷釉烈轶翳轭轸獒轭溴轭麸躺釉翳狒翳骈蝮箦泗轱篝狎趔骝镯滓列猎盼默麒孱趄蹂礤犷翳狒犷轭溴秕趔殇镦翳扉篝鏖祆黩狃狎秕钿阻孱驷祗瀣轭溴弩秕趔殇镦翳扉篝蝈篚祠轭铋飚砒犴痨搴换铄舡躔麸痼扉溴扉篝订舂换唤ū癌渝犰箫囵筱蜥翥瑙ㄤ彐礤翳镤狍痼趄遽è痼扉溴痼扉溴┅鏖翳箪雉扉篝蝈疱狒戾篝屦篝狎黩狃狒孱洎痼扉溴磲脲轭篝犷沐ю箪殇瀛痼趄遽红轶铄扉篝候屦遽趔疳趑弪瞽狍痼趄遽蝈疱狒螬红孱疳趑弪瞽狍痼趄遽戾瞟后翦疳趑弪瞽狍痼趄遽篝屦后翎螋疳趑弪瞽狍痼趄遽篝狎舂瑚蜥瓠狒孱铄黩狃狒孱洎恒躜蝈铘蝈疱狒候屙衢铋铉沲蝌孱舡箦珥孱戾瞟┅ㄤ彐礤翳镤铄è痼扉溴痼扉溴痼趄遽愆鏖翳箪雉扉篝蝈疱狒戾篝屦篝狎黩狃狒孱沲蝌孱舡蝈疱狒蟓蝈磲轭轭沲蝌孱舡蝈疱狒蝈磲轭轭绛沲蝌孱舡箦珥孱沲蝌孱舡鲠祯沲蝌孱舡扉篝戾铉翳痼扉溴躅戾篌沲蝌孱舡鲠祯箦翩沲蝌孱舡鲠祯铄篝狎舂┅躅戾篌沲蝌孱舡扉篝戾铉翳箦翩沲蝌孱舡扉篝戾铉翳戾铉翳扉篝┅灬忮祗è珏舡铄īㄩㄡ钿铒黩狃狒孱洎矧黹铛箴沲蝌孱舡鲠祯濠窘沲蝌孱舡鲠祯沲蝌孱舡扉篝戾铉翳┅屣ㄥ祠黩狃扉篝沲蝌孱舡鲠祯濠┅躅戾篌箪雉怙躅漯痼扉溴с躜蝈铘蝈疱狒蟓蝈磲轭轭绌箦翩沲蝌孱舡蝈疱狒蟓蝈磲轭轭铄蝈疱狒螬┅躅戾篌鲠祯瀛蝈磲轭轭绛沲蝌孱舡蝈疱狒蟓蝈磲轭轭绌蝈趱蝾骝镯铄屣皓ㄩ鲠祯瀛蝈磲轭轭绛蝈磲轭轭绛沲蝌孱舡箦珥孱舂痱镧ㄧ弭铄舂ㄤ邈姝蝈磲轭轭痼扉溴蝈磲轭轭绛沲蝌孱舡箦珥孱舂ㄩ钽沲蝌孱舡鲠祯濠痱镧ㄤ邈姝蝈磲轭轭痼扉溴箦翩蝈磲轭轭绛沲蝌孱舡箦珥孱铄戾瞟ㄩ钽沲蝌孱舡蝈疱狒螬箦翩沲蝌孱舡鲠祯ǐ铄篝狎舂í铄篝屦沲蝌孱舡蝈疱狒螬┅铄痼扉溴┅┅┅换痂轶麸蝙ㄤ彐疳趑弪痂轶麸蝙疳趑弪瞟疳趑弪篝屦疳趑弪瞟轰镢蹴孱翎糸镱⒁彐弪忉汶麸辛栽乓围栝篝矧殄熹轭翳秕麴豸狒翳轭溴痱秭殇邃怡釉判辛栽乓萎物翦翳狒辛栽乓轶篝殪徜鲠钽邃镱沐疱弼孱衄犷殒釉判辛栽乓殄熹铛礅弪痫轭糸铉麸犷弼孱轭辛栽乓翳狒栳箢忮孱殄熹邃弭ㄩ瀹殒辛栽乓栳镱禊徜鲠钽邃镱沐怩釉判辛栽乓殄熹骘轸秕麴豸┈痂轶麸蝙殄熹铋飚砒犴痨搴换铄舡痂轶麸蝙痼弪殄螬痼羼Ж暴┅畅换唤ò紊暴渝犰箫囵筱蜥翥瑙ㄤ彐礤翳镤狍痼趄遽è痂轶麸蝙痂轶麸蝙┅鏖翳箪雉疳趑弪篝屦疳趑弪瞟痂轶麸蝙磲脲轭篝犷沐ю栝篝矧痼趄遽吼狒翦蝾ㄡ蟓痼趄遽疳趑弪瞟后翦瓠疳趑弪疳趑弪瞽狍痼趄遽篝屦疳趑弪瞟┅ㄤ彐礤翳镤铄è痂轶麸蝙痂轶麸蝙痼趄遽愆鏖翳箪雉疳趑弪篝屦疳趑弪瞟痂轶麸蝙戾è铄舡篝屦铄篝屦疳趑弪瞟┅ㄩㄥ镳铄舡篝屦屣痱镧铄疳趑弪瞟ㄨ犷潇弪汜箦痼趄遽憝屐疳趑弪铄舡篝屦痼趄遽憝秕舡镦蜥铉ī铋飑┅┅┅换痼泸狒汨换换蜗耘痼泸狒汨礤汨犷轶箦屙麸忮箪殓梏禊溟骀弪孱艉换篚疱蜚镬扉溴蚝换畜泸狒汨ㄐ箦蜷弩ò暴畜羼ㄛ爆爆爆齿轭姗┊狍郁蝈犴铄粑ū博换艾爆铂艾爆铂超艾爆铂超换换扉箴换铄舡痼泸狒汨痼弪殄暴痼羼扉篝畅洪铈┅辈换ò畅换粕睾滹沲礤铘翳轶轭筱溟骀弪孱沐螽矧ㄤ彐疳趑弪痼泸狒汨疳趑弪瞟疳趑弪篝屦疳趑弪ㄣ躜蝈铘轭溴后翎翦洪铋翩矧癌轰镢蹴孱翎糸镱④⒂泸狒汨弩堍徙蝻篌翳鲠祯弩殄熹邃怡痼趄遽憩箝黹灬轭泔钽屦麸栾氖黹玷筱蜥翥蝈泔蜾犰翦蜷铉翳铒蝽犰骒秣镦痨狴忉汶辛栽乓轶翳箫躜沐疳趑弪瞵犷釉判辛栽乓溴翦蝽轭弩翳轭泸屙孱镦翳轭溴轭麸翳痼趄遽栝篝矧箩箦镱翳疳趑弪矧殓轭犰禊骝镯翳滗餍狒翦蝾吁疱蛎镬扉溴扉怛狎砒犴痨搴换铄舡躔麸痼泸狒汨痼弪殄螬痼羼Ж博博┅换唤ò穿渝犰箫囵栝篝矧Бㄤ彐礤翳镤狍痼趄遽è痼泸狒汨痼泸狒汨┅鏖翳箪雉疳趑弪篝屦疳趑弪瞟痼泸狒汨磲脲轭篝犷沐ю筱蜥翥璀痼趄遽吼狒翦蝾ㄡ蟓痼趄遽疳趑弪瞟后翦瓠疳趑弪疳趑弪瞽狍痼趄遽篝屦疳趑弪瞟┅ㄤ彐礤翳镤铄è痼泸狒汨痼泸狒汨痼趄遽愆鏖翳箪雉疳趑弪篝屦疳趑弪沲蝌孱舡轭溴痼泸狒汨戾è铠铄篝屦疳趑弪瞟┅麒孱ㄥ镳铠舂蝈趱蝾骝镯铄屣皓痱镧痼趄遽憝屐舡骢趱蝈疳趑弪沲蝌孱舡轭溴箦翩沲蝌孱舡轭溴磲ǐ沲蝌孱舡轭溴铠舂癌┅┅换痖ㄤ彐疳趑弪痖疳趑弪瞟è翦篝趄蹂ㄦ犰箦轰彐狨祠铋飑轰镢蹴孱翎糸镱④⑸孳屮痱弩箝镱骘疳趑弪铙耘釉轶弼犰踽翦骘遽汨篝屦犷殒轸铒瞽铋飕翳鲠祯镦砸张鏖祆忮殄熹邃雉桢蝼轶翳鲠祯镦屏逃鏖祆忮物翦翳狒砸张犷屏逃汜忮疳趑弪铙犷殒翳妁狎瀣翳妁狎镱禊徜鲠钽邃轭翳彘蝈箴邈糸鲥汜箦蟋铒骘弼弪篝屦砒犴痨搴换铄舡痖痼羼Ж铋铋铋飑痼羼Ж畅痼羼Ж珐┅俯换唤ū供渝犰箫囵灬К囵骢钽Бㄤ彐礤翳镤铄è痖痖姝痼趄遽愆鏖翳箪雉翦篝趄蹂驷祗濠痖戾è铠铄翦篝┅ㄩㄥ镳铠舂屣ㄩ铠铄趄蹂铄驷祗濠┅┅换疳蝠ㄤ彐疳趑弪疳蝠疳趑弪瞟疳趑弪狎疱珑獒麸ㄣ躜蝈铘疳趑弪瞽弼孱后翎翦洪铋翩矧铋飑ㄣ躜蝈铘狎疱珑獒麸颦篝蝈犴后翎翦洪铋翩矧铋飑轰镢蹴孱翎糸镱⒘蝠彗玳狒矧疳趑弪罨遽汨弼孱殄熹邃怡辛栽乓轶怙躅麸嗒弼孱舄犷翳孱翳孱糸蝈豉镦翳烈信乔闪韵疳趑弪轶殄熹邃砒犴痨搴换铄舡躔麸疳蝠疴轭烘镲痼羼Ж畅暴换疴轭衡狎痼羼Ж订暴┅换唤è胖盼浩舷郝烈穿ㄅ峙卧浩舷郝烈旦ㄅ峙卧浩舷郝烈订换ㄅ峙卧浩舷郝烈穿ㄅ峙卧浩舷郝烈旦ㄅ峙卧浩舷郝烈订换ㄅ峙卧浩舷郝烈穿ㄅ峙卧浩舷郝烈旦ㄅ峙卧浩舷郝烈订渝犰箫囵簌恣囵礤翎К囵颛ㄤ彐礤翳镤狍痼趄遽è疳蝠疳蝠┅鏖翳箪雉疳趑弪狎疱珑獒麸颟疳蝠戾è痼趄ㄡ蟓痼趄遽疳趑弪瞟┅磲脲轭篝犷沐ю狎瓠痼趄遽吼狒翦蝾痼趄横蝠彗玳狒矧狎疱珑獒麸恒躜蝈铘疳趑弪瞽弼孱铄痼趄恒躜蝈铘狎疱珑獒麸颦篝蝈犴ㄡ蟓痼趄遽狎疱珑獒麸颟┅┅ㄤ彐礤翳镤铄è疳蝠疳蝠痼趄遽愆鏖翳箪雉疳趑弪狎疱珑獒麸沲蝌孱舡疳趑弪瞽弼孱沲蝌孱舡狎疱珑獒麸颦篝蝈犴疳蝠麒孱ㄥ镳沲蝌孱舡疳趑弪瞽弼孱舂蝈趱蝾骝镯铄屣皓戾è铠戾è弼孱舄ㄣ镯忾铄弼孱趔磲脲溴驷蹯舡弼孱舂沲蝌孱舡疳趑弪瞽弼孱舂┅铄沲蝌孱舡狎疱珑獒麸颦篝蝈犴┅┅躅戾篌ㄥ镳铠舂蝈趱蝾骝镯铄铠舂箦翩沲蝌孱舡疳趑弪瞽弼孱铄疳趑弪瞟沲蝌孱舡狎疱珑獒麸颦篝蝈犴ㄡ蟓痼趄遽狎疱珑獒麸颟铄疳蝠┅┅换疰轭ㄤ彐疳趑弪疰轭疳趑弪瞟疳趑弪泔躅舂轰镢蹴孱翎糸镱①殄熹躔麸孟瘴秕麴豸骝镯辛栽乓萎砒犴痨搴换铄舡疰轭痼羼Ж畅洪铈畅旦换唤ū紊紊泰渝犰箫囵骈钿躜Бㄤ彐礤翳镤狍痼趄遽è疰轭疰轭┅鏖翳箪雉ㄣ秕铘疳趑弪瞟疰轭磲脲轭篝犷沐ю骈瞽痼趄遽吼狒翦蝾ㄡ蟓痼趄遽疳趑弪瞟恒秕铘铄泔躅舂┅换粕睾箬秕熹忮徕戾麸躞狍玑翦疳趑弪ㄤ彐礤翳镤铄è疰轭疰轭痼趄遽愆鏖翳箪雉疳趑弪泔躅铛礅弪疰轭躅戾篌铛礅弪泔躅舂蝈趱蝾骝镯铄屣皓铄疳趑弪瞟┅换疰轭漉ㄤ彐疳趑弪疰轭漉疳趑弪瞟疳趑弪漉麸戾蜥钽轰彐狨祠癌ㄣ躜蝈铘漉后翎翦舂ㄥ灬痼邃漉后翎翦洪铋翩矧癌轰镢蹴孱翎糸镱①殄熹弼孱趔骝镯辛栽乓躅糸翳彘麸翎漉蜥糸镱轶鏖翳轭韵膛伊蚊镦恼椰矧珧遽翦翳犷恼耶令弼孱趔翳狒黠蹯孱忮镱恼狎沲箬矧舢涉辛栽乓秕麴豸铛礅弪蟋翳彘麸翎篚轶扉黹翦轭篝遽洚砒犴痨搴换铄舡疰轭漉疴轭轰躜烘镲痼弪殄螬博畅换唤è胖盼耗找浩舷癌ㄅ峙卧耗找浩舷暴畔些换换铄舡躔麸疰轭漉瘅栝翦穿倍┅换唤ū博换蝈漉沐＇换唤倍渝犰箫囵骈瞌囵簌钽Бㄤ彐礤翳镤狍痼趄遽è疰轭漉疰轭漉颟鏖翳箪雉疳趑弪漉麸戾蜥钽濠疰轭漉磲脲轭篝犷沐ю骈钿躜痼趄遽吼狒翦蝾疳趑弪瞽狍痼趄遽疳趑弪瞟轰躜ㄡ蟓痼趄遽漉颟呼镬弪犷沐铄麸戾蜥钽濠┅ㄤ彐礤翳镤铄è疰轭漉疰轭漉颦痼趄遽愆灬忮祗è珏舡溴祠ㄥ雯ㄥ豉疱汜箦弼ㄥ鲥铘ㄥ鲥铘鲠祯弼轰屐翎┅扉篝蝈漉沐＇磲磲疸狎＇珏舡溴祠弼┅铛礅弪弼┅┅鏖翳箪雉疳趑弪漉麸戾蜥钽沲蝌孱舡漉屐狃箦洵漉颟疰轭漉戾è瞽弼孱铄疳趑弪瞟┅麒孱ㄥ镳瞽弼孱舂蝈趱蝾骝镯铄屣皓躅戾篌箪雉怙躅漯疰轭漉с躜蝈铘漉颟箦翩沲蝌孱舡漉铄漉颟┅麒孱ㄥ镳沲蝌孱舡漉颟蝈趱蝾骝镯铄屣皓戾è蝈ㄩ矧ㄥ耢洪铈沲蝌孱舡漉颟窘沲蝌孱舡漉ǐ屐狃箦洵漉ㄧ弭溴祠瞽弼孱舂┅瞽弼孱戾è翡躜ō沲蝌孱舡漉屐狃箦洵漉颟┅麒孱窘麸戾蜥钽翡躜蝈趱蝾骝镯铄屣皓ㄩㄥ鲥铘瞽弼孱舂ㄣ镯忾铄弼孱趔瞽弼孱ㄥ鲥铘轰躜翡躜┅翡躜┅┅ㄩ钽屐狃箦洵漉ㄧ弭溴祠蝈螬蝈螬┅┅换痼钽ㄤ彐疳趑弪痼钽疳趑弪瞟疳趑弪簌钽聃犷磲漉轰彐狨祠铋飑麸戾蜥钽轰彐狨祠爱鞍暴ㄥ灬痼邃漉后翎翦洪铋翩矧癌轰镢蹴孱翎糸镱①殄熹弼孱趔骝镯辛栽乓躅糸翳彘麸翎漉蜥糸镱轶鏖翳轭韵膛伊蚊镦土啬找沲趑轭镦犷弼孱趔翳狒黠蹯屮翦钿疳篝土啬找涉辛栽乓孱潴忮骘蝈土啬找蝈篝轶徜溴麸翳痼趄遽麸蝻躅轸漉蜥糸镱躔麸翳铄狎弩眭祠轲戾镦淤蚊颜廖援砒犴痨搴换铄舡躔麸痼钽疴轭轰躜痼羼Ж旦暴倍┅换换唤è胖盼耗找旦ㄅ峙卧涸傩阂庞耗找畅换换铄舡躔麸痼钽疴轭轰躜痼羼Ж旦旦倍┅换换唤è胖盼耗找旦ㄅ峙卧耗找旦ㄅ峙卧耗找旦ㄅ峙卧耗找耗盘粤暴渝犰箫囵骈钿躜Бㄤ彐礤翳镤狍痼趄遽è痼钽痼钽┅鏖翳箪雉疳趑弪簌钽聃犷磲漉麸戾蜥钽濠痼钽磲脲轭篝犷沐ю簌钽痼趄遽吼狒翦蝾ㄡ蟓痼趄遽疳趑弪瞟后钽聃犷铄簌钽聃犷舂喉狲漉铄磲漉颟呼镬弪犷沐铄麸戾蜥钽濠┅ㄤ彐礤翳镤铄è痼钽痼钽痼趄遽愆鏖翳箪雉疳趑弪簌钽聃犷磲漉麸戾蜥钽屐狃箦洵漉颟痼钽麒孱ㄡ钿磲漉窘屐狃箦洵漉ō磲漉麸戾蜥钽濠┅蝈趱蝾骝镯铄屣皓戾舄è钼骜铄舡忮狒骘颦聃犷簌钽聃犷屐狃箦洵漉颟瞽弼孱铄疳趑弪瞟瞽弼孱ㄩㄥ镳瞽弼孱舂戾è溟骀ō钼骜屐狃箦洵漉颟┅ㄩ痨躞溟骀ㄥ鲥铘呼疱候弩轰躜溟骀蝈趱蝾骝镯铄屣皓┅瞽弼孱舂瞽弼孱ㄩ磲漉ㄣ镯忾铄弼孱趔瞽弼孱ㄥ鲥铘轰躜黹ㄥ鲥铘鲠祯瞽弼孱轰躜ō磲漉屐狃箦洵漉颟┅瞽弼孱舂┅ㄩ钽屐狃箦洵漉ㄥ鲥铘鲠祯瞽弼孱轰躜┅瞽弼孱舂┅换痄躜篝豸翦换粕睾磲脲鲥蝮轱麒弪弼孱趔箅轲疱鏖翳狎趱蝾邃麸蝈篝轭篝遽麸脲屦翳泔蝌邈漉颟ㄤ彐疳趑弪痄躜篝豸翦疳趑弪瞟疳趑弪ㄣ躜蝈铘鲠祯后翎翦洪铋翩矧铋飑ㄣ躜蝈铘蝈疱狒蟓蝈磲轭轭后翎翦洪铋翩矧癌轰镢蹴孱翎糸镱①殄熹遽汨秕麴豸骝镯辛栽乓糸礤蟋溟鲩溟铉轸怡萎涉翳秕麴豸骝镯辛栽乓轶犷弼孱衄轸漉轶溟鲩溴浠殒轸铛礅弪翳铛礅弪轸箦戽轶溟鲩溴轭篝遽镦忮轭殄熹邃溟蝈泗禊砒犴痨搴换铄舡痄躜篝豸翦痼羼Ж旦痼羼Ж博┅供换唤ū悲悲弹弹紊泰换换铄舡痄躜篝豸翦疴轭轰躜痼羼Ж旦┅换痼羼Ж博┅换供换唤è胖盼耗找悲畅ㄅ峙卧耗找悲畅ㄅ峙卧耗找悲畅ㄅ峙卧耗找暴ㄅ峙卧耗找暴ㄅ峙卧耗找畅ㄅ峙卧耗找弹博ㄅ峙卧耗找弹博紊泰渝犰箫囵颛ㄤ彐礤翳镤狍痼趄遽è痄躜篝豸翦痄躜篝豸翦颟鏖翳箪雉疳趑弪瞟痄躜篝豸翦磲脲轭篝犷沐ю漉蝮趱趑弪痼趄遽吼狒翦蝾ㄡ蟓痼趄遽疳趑弪瞟侯疳趑弪瞽狍痼趄遽瞟┅ㄤ彐礤翳镤铄è痄躜篝豸翦痄躜篝豸翦颦痼趄遽愆鏖翳箪雉疳趑弪沲蝌孱舡鲠祯沲蝌孱舡蝈疱狒蟓蝈磲轭轭绌痄躜篝豸翦麒殪ㄡ钿沲蝌孱舡蝈疱狒蟓蝈磲轭轭弪镳沲蝌孱舡蝈疱狒蟓蝈磲轭轭绌箦翩沲蝌孱舡蝈疱狒蟓蝈磲轭轭铄瞟戾è铄疳趑弪瞟┅麒孱ㄥ镳沲蝌孱舡蝈疱狒蟓蝈磲轭轭绌蝈趱蝾骝镯铄屣皓麒孱ㄡ钿沲蝌孱舡蝈疱狒蟓蝈磲轭轭铒弪镳沲蝌孱舡蝈疱狒蟓蝈磲轭轭绌┅箦翩沲蝌孱舡鲠祯ㄩㄥ镳濠屣ㄣ豉疱汜箦ㄥ鲥铘ㄣ镯忾铄弼孱趔ㄥ鲥铘轰躜ǒㄥ鲥铘鲠祯轰躜沲蝌孱舡蝈疱狒蟓蝈磲轭轭绌┅铛礅弪ǒ沲蝌孱舡蝈疱狒蟓蝈磲轭轭绌┅┅┅麒孱沲蝌孱舡蝈疱狒蟓蝈磲轭轭ㄤ邈姝蝈磲轭轭痄躜篝豸翦颟沲蝌孱舡鲠祯濠┅换疴遽ㄤ彐疳趑弪疴遽疳趑弪瞟ī轰镢蹴孱翎糸镱①殄熹翳铛礅弪镦忮狒屐狃箦箝钽翳疴遽麽屙忮滗邃轭翳痼趄遽懋砒犴痨搴换铄舡疴轭轰躜痼羼Ж畅烘镲疴遽舂畅换唤è胖盼耗找浩舷癌ㄅ峙卧耗找浩舷暴ㄅ峙卧耗找浩舷畅渝犰箫囵忮狒К噔遽臾囵蝓瞌ㄤ彐礤翳镤铄è疴遽疴遽舡痼趄遽愆ㄢ遽疳趑弪瞽疳蝈铘疴遽恒灬篌ю忾钿┅换疴遽舄ㄤ彐疳趑弪疴遽舄疳趑弪瞟è翎箅后翎翦洪铋翩矧铋飑轰镢蹴孱翎糸镱①殄熹翳铛礅弪镦忮狒镱翳嗒沆镢氇镦翳沲蝌孱疳趑弪町深雉桢黠蜾蟋疴遽舄轶沆镢氕簌钽邃躅扉脲囵忮狒К麒殂轶疳趑弪瞽簌钽邃砒犴痨搴换铄舡疴轭轰躜痼羼Ж畅烘镲疴遽舄┅畅换唤è胖盼耗找浩舷舶癌ㄅ峙卧耗找浩舷舶暴ㄅ峙卧耗找浩舷舶畅渝犰箫囵忮狒К噔遽臾囵蝓瞌ㄤ彐礤翳镤铄è疴遽舄疴遽舄痼趄遽愆ㄢ遽沆镢氇┅换痿轫ㄤ彐疳趑弪痿轫疳趑弪瞟è灬篝忮狒汨邈脲后翎翦洪铋翩矧铋飑翦眇锃狒忮狒后翎翦洪铋翩矧铋飑ㄥ灬痼邃糸礤后翎翦洪铋翩矧癌轰镢蹴孱翎糸镱①殄熹翳铛礅弪镦箦泔钿屐狃箦箝钽痿轫麽屙忮滗邃轭翳痼趄遽懋物翦歪玳鲥轭徙沲蜥翦蝈篚祠殒翳沆镢毵翦眇汨犷珏镢沲盹蝈骝羼蹂铘禊翳犷弼孱趔轭翳疳蝈铘疴轭洚砒犴痨搴换箦翩翦眇沆镢氇暴换栋滦换铄舡疴轭轰躜呼轫痿轫濠博换唤è胖盼耗找涸赏癌ㄅ峙卧耗找涸赏碑癌渝犰箫囵忮狒К囵蝓瞌噔遽臾ㄤ彐礤翳镤铄è痿轫痿轫瀛痼趄遽愆换粕睾翎脲轭麸徙泔躅翳痱弼轱躞翦眇殒轸栳忮孱汨犷珏箝钽翳灬篝忮狒汨邈脲洚鏖翳箪雉灬篝忮狒汨邈脲翦眇锃狒忮狒屐狃箦洵糸礤痿轫鏖翳箪雉翦眇铹沆镢氇戾è忮狒ㄢ遽疳趑弪瞽疳蝈铘痿轫恒灬篌ю忾钿┅┅痱镧ㄩ钽屐狃箦洵糸礤ㄩ铛祆灬篝忮狒汨邈脲洎ㄤ躜糸礤ō忮狒灬篝忮狒汨邈脲洎翦眇铹┅箦翩灬篝忮狒汨邈脲忮狒翦眇锃狒忮狒翦眇铹┅┅换痖钿屮换韵南犰翦蝾狒鲥蝮轱翳狒镱禊汜祆＇铄镱轭溴疳遽汨糸礤翳疳趑弪瞽狍痼趄遽镦扉篝疳栳孱溴洚换韵南痖钿屮翳狒镱禊屙忮潴屐屙孱骝镯篚怵狒翦蝾蟋灬吁疱蛎镬扉溴颛畜鏖翥璞ㄤ彐疳趑弪痖钿屮疳趑弪瞟扉篝疳轭溴疳黩狃轰彐狨祠铋飑轰镢蹴孱翎糸镱⒄箦晌呐丨辛麸轭溴轭麸翳扉篝蝈趱蝾邃怡躺釉辛援滓列轶麒弭桢轭溴弩翳狒狎秕镦蜥铉鏖祆忮黩狃疱ㄩ舂矧鏖祆箝眇禊蝈趱蝾铋飚砒犴痨搴换铄舡痖钿屮扉篝构垢狗痼羼扉篝畅┅穿换换唤ü垢狗紊泰换换铄舡躔麸痖钿屮扉篝构垢狗痼弪殄订舂换换唤ü垢狗构垢狗渝犰箫囵麽祀Бㄤ彐礤翳镤狍痼趄遽è痖钿屮痖钿屮┅鏖翳箪雉扉篝疳轭溴疳黩狃皓痖钿屮磲脲轭篝犷沐ю轭溴痼趄遽红轶舡疳疳趑弪瞽狍痼趄遽扉篝疳舂洪钿屮疳疳趑弪瞽狍痼趄遽轭溴疳舂瑚蜥瓠黩狃皓┅ㄤ彐礤翳镤铄è痖钿屮痖钿屮痼趄遽愆换粕睾磲脲翳轶黠螂骘孱鲥祜疱狍麇祆翳轭溴箬秕熹铒忮铒蝽犰辁邃鏖翳箪雉扉篝疳轭溴疳黩狃皓痖钿屮戾è扉篝铄扉篝疳舂ㄩ澍铄轭溴疳舂┅麒孱矧ㄥ镳殇ㄥ镳扉篝┅蝈趱蝾骝镯铄屣皓ㄦ躅汜祆ㄩ黩狃ь翳黩狃ь翳殇扉篝┅┅换痱躅ㄤ彐疳趑弪痱躅疳趑弪瞟疳趑弪ㄤ躜轰彐狨祠暴ㄣ躜蝈铘漉后翎翦舂轰镢蹴孱翎糸镱⒁躅辛栽乓堍轭溴疱钿孱綮堍镦轸疳蝈铘栾熹轭遽汨鲠祯骘恼忮狒螽裴汨镦辛栽乓围秕麴豸轶趄遽翦狍殒轸灬篝邃恼忮狒蟋忮轭泔铘轭躏躞禊殄熹邃漉蜷铉翳狒糸礤忮骘蝈盹鲩铉镱麸翳铄秕麴豸砒犴痨搴换铄舡躔麸疴轭烘镲痼羼Ж旦换衡狎痱躅痼羼Ж俯换痼羼Ж爱爱暴┅┅换唤è胖盼浩舷郝烈穿换ㄅ峙卧浩舷郝烈旦换ㄅ峙卧浩舷郝烈旦换ㄅ峙卧浩舷郝烈订换ㄅ峙卧浩舷郝烈俯渝犰箫噔遽臾囵忮狒Бㄤ彐礤翳镤狍痼趄遽è痱躅痱躅┅鏖翳箪雉疳趑弪漉颟痱躅躅戾篌疳趑弪瞽疳蝈铘痱躅恒灬篌ю忾钿ㄥ蝌矧汜铑雉忮躞邃秕趔殇镦疴轭洧痱躅┅磲脲轭篝犷沐ю蝓瞽痼趄遽吼狒翦蝾ㄡ蟓痼趄遽疳趑弪瞟轰躜疳趑弪瞽狍痼趄遽漉颟恒躜蝈铘漉癌┅ㄤ彐礤翳镤铄è痱躅痱躅痼趄遽愆鏖翳箪雉疳趑弪漉沲蝌孱舡漉漉颦栝篝矧铛礅弪痱躅戾è忮狒ㄢ遽疳趑弪瞽疳蝈铘痱躅恒灬篌ю忾钿┅┅ㄦ戾è铄舡漉ī戾è铠铄漉颟┅躅戾篌ㄥ镳铠舂铄疳趑弪瞟ㄩ钽沲蝌孱舡漉铠舂┅┅麒孱弪镳铛礅弪铄舡漉颟麒殪ㄡ钿矧铒痼趄遽憝漉颟铒ㄥ钿邃漉颟┅冀沲蝌孱舡漉忮狒螬铄舡漉颟┅灬篝秕麴豸疳趑弪瞟┅换痼ㄤ彐疳趑弪痼疳趑弪瞟疳趑弪ㄣ躜蝈铘痼趄遽后翎翦洪铋翩矧屣皓轰镢蹴孱翎糸镱⒄箦疳趑弪镦簌礅镬麸屙忮囵溴妲螽辛栽乓轶翳箫躜沐疳趑弪翳狒殄熹簌礅镬钺黹铉翳痄彐麸屙忮洚砒犴痨搴换痄彐烘镲痼羼Ж畅暴换换痄彐衡狎痼羼Ж订暴换换铄舡躔麸痼痼羼Ж烘镲衡狎暴┅换换唤ū订渝犰箫囵溴妲囵疳颛囵礤翎Бㄤ彐礤翳镤狍痼趄遽è痼痼愆鏖翳箪雉疳趑弪瞟痼磲脲轭篝犷沐ю簌憝痼趄遽吼狒翦蝾ㄡ蟓痼趄遽疳趑弪瞟┅ㄤ彐礤翳镤铄è痼痼憝痼趄遽愆灬忮祗è磲忮痄彐ㄩ姝戾è痄彐ㄡ钿簌礅镬痄彐疳趑弪┅┅痄彐┅鏖翳箪雉疳趑弪沲蝌孱舡痼趄遽愆痼戾è铄沲蝌孱舡痼趄遽愆┅ㄩㄥ镳瞟戾è铄舡痄彐铄疳趑弪瞟┅麒孱ㄥ镳铄舡痄彐蝈趱蝾骝镯铄屣皓箦翩沲蝌孱舡痼趄遽ㄡ蟓痼趄遽ㄩ扉篝铄舡痄彐痧狎磲疸狎＇磲忮痄彐铄舡痄彐┅磲忮痄彐铄舡痄彐┅┅铄痼愆瞟┅┅换疸栳轭ㄤ彐疳趑弪疸栳轭疳趑弪瞟疳趑弪铙轰镢蹴孱翎糸镱⒚镯忾铄眭祠轲戾疳趑弪铙轭麸镱弼孱篝蝈犴砒犴痨搴换铄舡疸栳轭疴轭烘镲痼羼Ж畅┅疴轭衡狎痼羼Ж供暴┅穿换换唤è胖盼浩舷郝烈珐ㄅ峙卧浩舷郝烈俯ㄅ峙卧浩舷郝烈供紊泰渝犰箫囵忾钿Ё哄礅邃脲轰彐躅ㄤ彐躅疸栳轭é蝈篝疳趑弪铙磲脲轭篝犷沐ю汨衢吼狒翦蝾疳趑弪铙┅ㄤ彐礤翳镤狍痼趄遽è疸栳轭疸栳轭┅鏖翳箪雉疳趑弪铙疸栳轭磲脲轭篝犷沐ю汨衢瞽痼趄遽吼狒翦蝾磲疸狎＇疳趑弪瞽狍痼趄遽疳趑弪铙┅┅ㄤ彐礤翳镤铄è疸栳轭疸栳轭痼趄遽愆鏖翳箪雉疳趑弪铙疸栳轭戾è悱弼孱磲脲溴驷蹯舡弼孱舂┅ㄤ镬轶疳趑弪疳趑弪铙悱弼孱舂箦翩悱弼孱ㄣ镯忾铄弼孱趔悱弼孱戾è弼孱舄悱弼孱舂铄疳趑弪瞟┅┅┅换痄殒ㄤ彐疳趑弪痄殒疳趑弪瞟疳趑弪瞟轰镢蹴孱翎糸镱⑾豸瘐翳溟骀弪孱沐忮赭邋篚沣弩箝鲥秕麴豸镦辛栽乓萎砒犴痨搴换铄舡痄殒痼羼Ж畅暴穿换换唤ō紊泰渝犰箫囵溴祠岌ㄤ彐礤翳镤铄è痄殒痄殒姝痼趄遽愆鏖翳箪雉疳趑弪瞟痄殒麒孱弪镳箪雉鲠祯疳趑弪ц轶麸蝙铛礅弪┅铄疳趑弪瞟戾è灬篝痼趄遽憝屐疳趑弪暴铄铄疳趑弪瞟┅麒孱矧ㄥ镳灬篝ㄥ镳铄舂蝈趱蝾骝镯铄屣皓ō铄灬篝┅┅换痄屐翎ㄤ彐疳趑弪痄屐翎疳趑弪瞟疳趑弪ㄣ沆轰彐狨祠穿轰镢蹴孱翎糸镱⑾豸瘐翳溟骀弪孱沐忮赭邋篚沣弩箝鲥秕麴豸镦辛栽乓维狍篚黹铉辛栽乓蝈篝狎趔弼弪觅锰秕麴豸螽疹扉脲囵溟骀К痄屐翎轶黩轸翦鏖翳轸躞狍轭瘐骘囵忾钿Ё轰屐翎脲轭黹钿涉辛栽乓围篚沣弩箝鲥鲠祯弩黠蹯蝈篚祠轭铄玑糸鲥溟骀弪孱沐痄屐翎轭篝遽黩狃翳溴祠狎秕钿麸翳铄眭祠轲戾镦觅锰女澡轶黠蹯犰祜秕麸骘屮犴痨瀣篚痧禊翳铛礅弪镦翳忮狒翳狒遽汨弼孱镢沲蝮镱蜥翳弪翳犷箴邈殒轭翳溴祠忮赭邋遽汨弼孱舢澡轶轶镦泔躜箦徙栝弼徕戾躞轭疴轭洄衡遽脲狍麇祆栾麇鲥翳狒礤翳镤蝈聃轵弩翳疴轭麸疱咫狒骢趱蝈鲠祯弩麒殂轶铒犰麽溴箝蜥忪瀹箩箦镱翳疳趑弪矧殓轭犰禊骝镯翳滗餍狒翦蝾吁疱蛎镬扉溴扉怛狎砒犴痨搴换铄舡痄屐翎痼羼Ж畅穿俯换换唤ū暴换换铄舡痄屐翎痼羼Ж旦穿俯换换唤ū畅渝犰箫囵溟骀К囵忾钿Ё衡遽脲ㄤ彐礤翳镤狍痼趄遽è痄屐翎痄屐翎┅鏖翳箪雉疳趑弪泫沆濠痄屐翎磲脲轭篝犷沐ю溴祠岘痼趄遽吼狒翦蝾ㄡ蟓痼趄遽疳趑弪瞟恒沆铄泫沆濠┅ㄤ彐礤翳镤铄è痄屐翎痄屐翎痼趄遽愆鏖翳箪雉疳趑弪泫沆栝篝矧铛礅弪痄屐翎麒孱弪镳栝篝矧铛礅弪铄疳趑弪瞟戾è祧矧痼趄遽憝屐疳趑弪暴癌ㄣ铄疳趑弪瞟┅麒孱矧ㄥ镳祧ㄥ镳泠┅蝈趱蝾骝镯铄屣皓ō泠ō祧ㄣ彘扉铉怡ō祧泠泫沆濠┅┅换痄蝻ㄤ彐疳趑弪痄蝻疳趑弪瞟疳趑弪轰彐狨祠癌轰镢蹴孱翎糸镱⒛蝻翳骈蝮秕麴豸骝镯辛栽乓犷殄熹翳蝈篝涉轶铄玑糸鲥潋镳翳灬篝秕麴豸骝镯辛栽乓轭篝遽洚砒犴痨搴换铄舡痄蝻痼羼Ж穿暴博穿换换唤ǔ紊紊泰渝犰箫囵蝻翎翦Бㄤ彐礤翳镤狍痼趄遽è痄蝻痄蝻皓换粕睾汨邈翳狒轶铒忾珑弪矧箜犰戾翳犷栝篝矧犰祜黧鏖翳箪雉疳趑弪瞟痄蝻磲脲轭篝犷沐ю潋镳痼趄遽吼狒翦蝾ㄡ蟓痼趄遽疳趑弪瞟侯瞟┅ㄤ彐礤翳镤铄è痄蝻痄蝻瓠痼趄遽愆鏖翳箪雉疳趑弪铛礅弪痄蝻ㄩ黹铛箴瞟麒孱ㄥ镳痼趄遽憝屐舡骢趱蝈疳趑弪ō铛礅弪瞟┅蝈趱蝾骝镯铄屣皓麒孱弪镳箪雉鲠祯疳趑弪ц轶麸蝙铛礅弪┅ㄤ雉轫弩ㄩ瞟铄疳趑弪瞟┅铄疳趑弪瞟┅换痧狎ㄤ彐疳趑弪痧狎疳趑弪瞟疳趑弪铙痼趄遽眢后翎翦洪铋翩矧铋飑轰镢蹴孱翎糸镱⒚镯忾铄眭祠轲戾弼孱疳趑弪铙轭麸镱痼趄遽鏖翳犰弼孱趔轭翦眇矧犰矧溴虍辛栽乓斡轶翳扉篝镦疳趑弪铙矧疳趑弪殄熹轭扉篝镦疳趑弪铙澡痧狎孱潴麒孱犰镦翳疳趑弪铙轭辛栽乓斡孱洚砒犴痨搴换铄舡躔麸痧狎扉篝疴轭轰躜痤悲穿换疴轭轰躜痤帛穿┅┅换换唤è胖盼耗找悲耗盘粤癌ㄅ峙卧耗找帛耗盘粤悲博换ㄅ峙卧耗找悲耗盘粤悲订ㄅ峙卧耗找帛耗盘粤悲畅换ㄅ峙卧耗找悲耗盘粤悲畅ㄅ峙卧耗找帛耗盘粤悲订换ㄅ峙卧耗找悲耗盘粤悲博渝犰箫囵簌恣ㄤ彐礤翳镤铄è痧狎痧狎痼趄遽愆鏖翳箪雉疳趑弪铙痼趄遽眢栝篝矧铛礅弪痧狎灬忮祗è铄舡轭痼趄遽眢īㄩ姝戾è蝈蝈盹鲥殒灬礅溽痼趄遽愆ㄡ钿铒弪镳箪雉鲠祯痼趄遽ц轶麸蝙铛礅弪┅ㄥ钿邃痼趄遽愆┅痼趄遽眢┅盹篝＇蝈弘妁＇忮狒屣皓磲忮蝈箦舡痼趄遽眢ī躅戾篌蝈盹鲥屣痼趄遽眢戾è铄舡扉篝铄疳趑弪铙┅麒孱ㄥ镳铄舡扉篝蝈趱蝾骝镯磲忮蝈箦舡痼趄遽眢铋飑箦翩痼趄遽眢磲疸狎＇狍痼趄遽铄舡扉篝┅┅┅麒孱弪镳栝篝矧铛礅弪磲忮蝈箦舡痼趄遽眢┅戾è铠铄铄舡轭痼趄遽眢┅┅ㄩㄥ镳铠舂戾è铋铄舡轭痼趄遽眢┅ㄩㄥ镳铋皓屣戾è忮痧狎ㄢ遽痧狎┅ㄢ孱轲ㄢ遽铋皓┅ㄩ忮痧狎忮铋皓ㄥ鲥铘呼疱候弩轰屐翎ō忮铋忮痧狎┅铄痧狎┅┅ㄣ镯忾铄弼孱趔铠ㄥ鲥铘轰屐翎ōㄢ遽铄舡轭痼趄遽眢┅ㄢ遽痧狎┅┅┅┅换痦弭换粕睾囵毵滹弩瞌黠螂轭痦弭岙ㄤ彐疳趑弪痦弭疳趑弪瞟疳趑弪ㄣ躜蝈铘痼趄遽后翎翦洪铋翩矧铋飑轰镢蹴孱翎糸镱⑼弭岘泔铘蝻疳趑弪铙躞轭翳弼孱趔秕麴豸怡辛栽乓萎深雉桢黠蜾蟋轭篝遽镦趄殓珏蜷铉簌铘梵溟蝈泗禊翳弼孱趔秕麴豸怡辛栽乓狎躞邃麸屙忮疳趑弪铙轭麸翳痦弭岌痼趄遽懋澡骘祆秣轭脲狎篚痧矧翦浜吼狒翦蝾矧洪铙趄蹴孱钺礤镦翳箫躜沐疳趑弪骘翳轶堍篝屦堍轰躜箦翳漉蜥糸镱镦翳屙忮滗邃疳趑弪ㄤ彐狨祠麸洪铈麒殂汜躞弩翳疳趑弪麸痨狴麸轸孱洎烘轭漉扉黹翳漉蜥糸镱镦翳屙忮滗邃疳趑弪町后钽簌钽翳漉蜥糸镱镦翳屙忮滗邃疳趑弪麸眭祠轲戾镦翳痱秭殇邃鲠祯瀣箝黹灬麸囵簌钽后趄弭汨眭祠轲禊翳漉蜥糸镱镦遽汨镦翳箫躜沐疳趑弪瞌弼孱趔呼矧烘轸糸礤篝蝈翥疳趑弪箫轸麸翎漉蜥糸镱轶翳铛礅弪箴邈殒殄洮灬囵趔М候矧候屦遽蝈疱狒遽汨弼孱翳铛礅弪镦糸礤蝈趱蝾邃怡翳骢钽糸镱麒孱翳弼孱轶狃痨殄麸轸箝黹灬麸囵颛洪铌邈轭赍泗脲鲠祯疳轵骝镯翳秕麴豸镦翳轶鲠祯轭麸翳箫躜沐疳趑弪町后翦瓠轭赍泗矧后轭赍泗轭赍泗镱秕麴豸骝镯翳轶鲠祯疱篝屦澡骘祆秣轭脲狎痨犷铄骘骢趱蝈轫痨屙孱翎糸镱后翎螋矧哄钿徜牾篝翳篝狎矧孱痫轭趔镦翳箫躜沐疳趑弪ㄩ瀹麸箅轲翳骈蝮栳戽箦后翎螋麸爱旦后翎螋忮狒矧哄钿忮狒徜牾篝翳篝狎矧孱痫轭趔镦翳箫躜沐疳趑弪轭铛礅弪镦忮狒ㄩ瀹麸孱翳疳趑弪忮狒遽蜢箦哄钿忮狒麸博后翎螋铘矧哄钿铘徜牾篝翳篝狎矧孱痫轭趔镦翳箫躜沐疳趑弪怡箅轲痖铉翳骈蝮矧灬篝弼孱趔烘殪翦矧候屙秭瀛殒铒箅轲犰弼孱趔骝镯翳箫躜沐疳趑弪翳狒蝈趱蝾铋麒孱狃痨殄麸翳箴邈殒殄骢钽糸镱矧疳趑弪町喉狃汜矧侯狎痱镢弩遽汨弼孱骝镯翳箫躜沐疳趑弪鏖翳骢钽糸镱矧犷雉桢疳趑弪町渝滹惘箴邈獒飙脲螽矧骘盹蝈轭骘蝽狒轱镱翳弩脲螽砒犴痨搴换换溴骈铄驽疳趑弪铙换痄彐烘镲疴轭壶痼羼Ж畅暴轰躜暴换痄彐衡狎疴轭葫痼弪殄螬轰躜瘅栝翦爱碑畅┅换换换躞痦弭麸痨狴疳趑弪骘犷翳孱疳趑弪忉颥孱篚蜷铉遽汨狎忮狒祜铉轭麸翎怡麽镦痦弭岌簌钽脲麒殂黠螂箝黹灬蜢麸囵簌钽换铄舡躔麸痦弭吼狒翦蝾痼羼扉篝烘镲衡狎暴后钽博换唤è胖盼贺耗找暴ㄅ峙卧贺耗找暴换骝镯痄彐烘镲换ㄅ峙卧嘿耗找爱范沟付付ㄅ峙卧嘿耗找爱勾傅肮钩ㄅ峙卧嘿耗找爱哺惫俺卜┅换骝镯痄彐衡狎渝犰箫囵簌恣囵狎皈囵溴妲囵忾钿Б轰彐躅ㄤ彐躅痦弭é蝈篝疳轵螬磲脲轭篝犷沐ю礤翎吼狒翦蝾ㄩ戾铉翳疳轵螬ㄣ狎疳轵螬疳轵螬┅ㄤ彐礤翳镤狍痼趄遽è痦弭痦弭岍鏖翳箪雉疳趑弪瞟痦弭磲脲轭篝犷沐ю礤翎痼趄遽吼狒翦蝾ㄩ扉篝疳趑弪瞟祜镳烘矧脲鲠祯濠猴疳趑弪衡＇沅潋洪礤礅弪脲扉篝洪铌邈舂横痧孱扉篝脲鲠祯濠哄祗横痧孱扉篝脲疳趑弪瞽狍痼趄遽鲠祯濠┅ㄡ蟓痼趄遽疳趑弪瞟┅┅ㄤ彐礤翳镤铄è痦弭痦弭岘痼趄遽愆鏖翳箪雉疳趑弪沲蝌孱舡痼趄遽愆痦弭灬忮祗è磲脲痼趄遽痨轶镳糸镱犰蝈蟓疳趑弪瞟躅戾篌痨轶蝈趱蝾骝镯磲脲痼趄遽铋飑ㄤ弩趄蹉趱蜷铉忾钿脲鲠祯蝈篝蝈篝痨轶麒孱ㄥ镳鲠祯濠蝈趱蝾骝镯磲脲痼趄遽屣皓ㄣ狍脲è吼狒翦蝾洪铙趄蹴孱舂箦翩蝈蟓疳趑弪ㄥ豉疱汜箦鲠祯簌礅镬痄彐鲠祯濠疳趑弪鲠祯濠扉篝痧狎鲠祯濠┅┅è哄礅邃戾舄è铞铄鲠祯濠铞豉疱汜箦铞扉篝铞ㄥ鲥铘ㄥ鲥铘痨轶铞┅┅ㄤ镬轶ㄩ蝈鲥蝮铞皓瘐箬蝈篝┅┅è轰躜躅戾篌ㄥ耢鲠祯洪铈箦翩蝈蟓疳趑弪痼钽蝈蟓疳趑弪鲠祯鲠祯濠┅è烘轭漉颟躅戾篌ㄥ耢鲠祯洪铈箦翩蝈蟓疳趑弪疰轭漉蝈蟓疳趑弪鲠祯濠┅è后钽躅戾篌ㄥ耢鲠祯洪铈箦翩蝈蟓疳趑弪ㄡ痧禊＇痼钽蝈蟓疳趑弪ㄥ铙躜瀛扉篝鲠祯濠┅┅è后趄弭汨箦翩蝈蟓疳趑弪疸栳轭蝈蟓疳趑弪疴轭轰躜皙鲠祯痣轰躜┅┅┅è烘轸呼螬箦翩蝈蟓疳趑弪痿蝈蟓疳趑弪鲠祯濠┅è候候屦遽舂箦翩蝈蟓疳趑弪痱蝈蟓疳趑弪鲠祯濠┅è洪铌邈舂箦翩蝈蟓疳趑弪ㄩ蝈蟓疳趑弪疸栳轭蝈蟓疳趑弪鲠祯濠鲠祯濠┅è后翦瓠轭赍泗后轭赍泗箦翩蝈蟓疳趑弪ㄩ蝈蟓疳趑弪疸栳轭蝈蟓疳趑弪痤铄鲠祯濠┅痤铄鲠祯濠┅┅ㄩ蝈篝磲脲痼趄遽蝈篝蝈蟓疳趑弪瞟戾è痼趄遽ㄡ蟓痼趄遽蝈蟓疳趑弪瞟┅箦翩箪雉鲠祯痼趄遽ю狎孱舂痦弭岍痼趄遽愆┅┅躅戾篌沲蝌孱舡痼趄遽箦翩沲蝌孱舡痼趄遽磲脲痼趄遽ㄩ扉篝疳趑弪瞟祜镳烘矧脲鲠祯濠猴疳趑弪衡＇沅潋横痧孱扉篝脲铄鲠祯濠┅戾è铠铄疳趑弪瞟┅豉疱汜箦铠ㄥ鲥铘ㄥ鲥铘痨轶铠舂簌礅镬ㄩㄥ镳铠舂扉篝侯镱屣皓扉篝吼狒翦蝾铠舂┅┅┅┅麒孱ㄥ镳沲蝌孱舡痼趄遽愆蝈趱蝾骝镯铄屣皓戾è铠铄沲蝌孱舡痼趄遽愆┅ㄩㄥ镳铠舂痱镧箦翩沲蝌孱舡痼趄遽铋飑铄痦弭岍铠舂┅┅换痿ㄤ彐疳趑弪痿疳趑弪瞟疳趑弪ㄤ躜轰彐狨祠穿疳趑弪瞽秕麴豸后翎翦舂轰镢蹴孱翎糸镱⒃轫弩趄弭汨辛栽乓箫轸麸翎漉蜥糸镱轶恼耶物翦翳狒镱禊翳骈蝮嗒磲疳趑弪瞽殄熹戾铉翳弼孱趔骝镯辛栽乓狎泔铙殇弪邃犷翳狒翳妁狎汜煦蹯狒邃轫礤溟狒屐狒痼趄遽泸遽糸镱糸礤蜥翳弪翳犷灬殪狍翳痼趄遽殄熹螽砒犴痨搴换铄舡躔麸痿疴轭轰躜痤穿旦换换唤è胖盼耗找弹穿ㄅ峙卧耗找弹穿ㄅ峙卧耗找弹穿ㄅ峙卧耗找弹穿渝犰箫囵骈钿躜К囵簌钽Бㄤ彐礤翳镤狍痼趄遽è痿痿螬鏖翳箪雉疳趑弪漉颟痿戾舄è痼趄ㄡ蟓痼趄遽疳趑弪瞟蝈铄舡躔麸痼趄┅磲脲轭篝犷沐ю趔痼趄遽吼狒翦蝾痼趄轰躜铄漉颟吼狒翦蝾秕麴豸ㄣ镥蜚蝈鲥泗矧┅┅ㄤ彐礤翳镤铄è痿痿蟓痼趄遽愆鏖翳箪雉疳趑弪漉疳趑弪瞽秕麴豸铛礅弪痿麒孱窘铛礅弪戾铉翳疳趑弪瞽秕麴豸螬蝈趱蝾骝镯铄屣皓戾è眭ǒ漉ㄢ遽疳趑弪瞟┅ㄥㄥ祠疳趑弪瞽秕麴豸铛礅弪┅ㄣ镯忾铄弼孱趔弼ㄥ鲥铘轰躜í眭ㄥ鲥铘鲠祯弼轰躜┅┅┅换瘅犰ㄤ彐疳趑弪瘅犰疳趑弪瞟扉篝篝屦疳趑弪ㄤ轵邈糸镱疳趑弪轰彐狨祠暴篝狎舡痫轰彐狨祠癌ㄣ躜蝈铘轭溴后翎翦舂ㄣ躜蝈铘溟蝈泗轱轰彐狨祠后翎翦舂轰镢蹴孱翎糸镱④⒆犰胲秭弪翳鲠祯弩轭躺釉怡躞轭翳徙沲眭灬翦鲠祯镦翳秕麴豸镦釉判辛栽乓狍翳轭溴留翳忮玳铑轭镦翳瘅犰犷遽汨糸礤翳篝狎矧孱镦翳扉篝轶疳篌邃翳秕麴豸镦纳遗迷上苇辛栽乓轶翎脲犷躞邃狍翳眭祠轲扉弪骘鲠祯弩骝镯釉判辛栽乓萎釉烈原邢轶翳轭溴轭躺釉骘瘅犰麸翎脲轸骈蝮秕麴豸骝镯砒犴痨搴换换躞轭痼羼扉篝暴狍翳纳遗迷上苇辛栽乓汜躞弩翳瘅犰毵秕麴豸麸堍痖铉痫铉堍换铄舡瘅犰扉篝畅痼羼扉篝暴痼羼扉篝暴┅卑换换唤ò畅渝犰箫囵轭溴К囵怛秣瞌囵徙沲恣ㄤ彐礤翳镤狍痼趄遽è瘅犰瘅犰氅鏖翳箪雉扉篝篝屦疳趑弪溟蝈泗轱瞽疳趑弪篝狎舡痫螬瘅犰磲脲轭篝犷沐ю麽祀痼趄遽红轶铄扉篝后翦瓠疳趑弪疳趑弪瞽狍痼趄遽篝屦疳趑弪瞟轰轵邈糸镱疳趑弪疳趑弪瞽狍痼趄遽溟蝈泗轱瞽疳趑弪瞟后翎螋痫疳趑弪瞽狍痼趄遽篝狎舡痫螬┅ㄤ彐礤翳镤铄è瘅犰瘅犰氕痼趄遽愆鏖翳箪雉扉篝篝屦疳趑弪溟蝈泗轱瞽疳趑弪篝狎舡痫沲蝌孱舡轭溴沲蝌孱舡溟蝈泗轱铛礅弪瘅犰麒孱弪镳铛礅弪箦翩沲蝌孱舡轭溴铄篝狎舡痫螬箦翩沲蝌孱舡溟蝈泗轱铄溟蝈泗轱瞽疳趑弪瞟蝈趱蝾骝镯铄铘沲蝌孱舡轭溴扉篝┅戾è铙铄篝屦疳趑弪瞟┅麒孱矧ㄥ镳铙皓ㄥ镳沲蝌孱舡轭溴ㄥ镳沲蝌孱舡溟蝈泗轱瞟蝈趱蝾骝镯铄屣皓灬忮祗è铄舡轭溴īǐ沲蝌孱舡轭溴í铙沲蝌孱舡溟蝈泗轱瞟┅戾è铄舡轭溴铄舡轭溴┅麒孱矧黹铛箴铄舡轭溴窘铄舡轭溴戾铉翳扉篝┅箦翩沲蝌孱舡溟蝈泗轱铄溟蝈泗轱瞽疳趑弪瞟┅麒孱ㄥ镳沲蝌孱舡溟蝈泗轱瞟蝈趱蝾骝镯铄屣皓箦翩沲蝌孱舡轭溴盹铄舡轭溴戾铉翳扉篝┅┅┅ㄥ祠黩狃扉篝沲蝌孱舡轭溴┅换痧狎汨衢ㄤ彐疳趑弪痧狎汨衢疸栳轭疳趑弪铙轰镢蹴孱翎糸镱⒚镯忾铄眭祠轲戾疳趑弪铙轭麸箦鲥蜥弼孱篝蝈犴螽澡弼孱殄熹邃怡翳骈蝮疳趑弪鏖祆忮躞邃狍翳轭瘐弼孱麸翳箦泔钿疳趑弪瞵犷箫镱澡弼孱趔殄熹邃怡遽汨疳趑弪鏖祆忮泔祆邈翦轭麸扉篝犷殄熹邃怡翳痧狎汨衢町澡轶疳趑弪轶彐驽泗轹屐囵疳颛犷囵汨衢瞌泔礅轭邃砒犴痨搴换铄舡躔麸痧狎汨衢疴轭烘镲痼弪殄畅疴轭衡狷皤痣烘镲暴烘镲皤痣烘镲畅┅换唤èㄅ峙卧浩舷癌ㄅ峙卧浩舷郝邻暴换è胖盼浩舷暴ㄅ峙卧浩舷郝邻博换è胖盼浩舷博ㄅ峙卧浩舷郝邻畅┅渝犰箫囵疸К囵疳颛囵汨衢瞌囵忾钿Ё哄礅邃脲轰彐躅ㄤ彐躅痧狎汨衢é蝈篝疳趑弪铙磲脲轭篝犷沐ю疳蜚栳轭吼狒翦蝾疳趑弪铙┅ㄤ彐礤翳镤狍痼趄遽è痧狎汨衢痧狎汨衢瞟鏖翳箪雉疳趑弪铙痧狎汨衢磲脲轭篝犷沐ю疳蜚栳轭痼趄遽吼狒翦蝾磲疸狎＇狍痼趄遽疳趑弪铙┅┅ㄤ彐礤翳镤铄è痧狎汨衢痧狎汨衢瞽痼趄遽愆鏖翳箪雉疳趑弪铙痧狎汨衢戾è悱弼孱磲脲溴驷蹯舡弼孱舂┅祜镳烘矧疳趑弪洪疳趑弪铙轰箦翩悱弼孱ㄣ镯忾铄弼孱趔悱弼孱戾è弼孱舄ㄣ镳弼孱悱弼孱舂┅铄疳趑弪瞟┅洪ㄥ镳悱弼孱舂候弭躜屣哄祗恒镬戾泗悱弼孱舂┅换痧ㄤ彐磲泸痧é怙澌疳轵螬⒂铘狲篚玑骘囵疳蜚栳轭翳狒狨麸磲糸汜祆箴扉趔辛梢怡涵簌礅镬螽砒犴痨搴换痧烘镲痼羼扉篝畅暴换涵换衡狎皤痣烘镲博换唤èㄅ峙卧浩舷暴ㄅ峙卧浩舷郝烈畅换è胖盼浩舷博ㄅ峙卧浩舷郝烈穿换è胖盼浩舷畅ㄅ峙卧浩舷郝烈旦┅渝犰箫囵疳蜚栳轭Б灬忮祗è痧悱箴扉疳轵螬戾è痫痫箝糸镱涵疳轵螬┅ㄩ痫扉篝篚怏羼疳轵痫螬痧悱箴扉篚怏羼疳轵ū痫螬┅疳轵螬┅啜痧狎汨衢括祜镳烘矧洪痧悱箴扉疳轵螬恒镬戾泗ㄣ镱ю忾钿椹┅┅瘐箬铄ю疸疳趑弪铙换疸祯眇ㄤ彐疳趑弪疸祯眇疳趑弪瞟疳趑弪轰彐狨祠暴轰镢蹴孱翎糸镱⑶蝻躔秕麴豸镦翳箫躜沐疳趑弪轭麸扉篝镦躔麸轸屙遽汨砒犴痨搴换铄舡躔麸疸祯眇痼弪殄旦博换唤è暴ú畅ù┅渝犰箫囵徙祯眇Бㄤ彐礤翳镤铄è疸祯眇疸祯眇痼趄遽愆鏖翳箪雉疳趑弪瞟疸祯眇戾è铄铄瞟┅麒孱ㄥ镳铄舂蝈趱蝾骝镯铄屣皓矧铄舡躔麸疳趑弪铄舂屣皓┅换疳沆蹴ㄤ彐疳趑弪疳沆蹴疳趑弪瞟疳趑弪瞟轰镢蹴孱翎糸镱⒘豸镯狒殂犰禊珧秕秕麴豸镦翳箫躜沐疳趑弪轭麸扉篝镦躔麸轸屙遽汨疹扉脲囵沆蹴皈沆蹴箝轶狨麸磲糸汜祆箦麸翳戾铉翳镦翳祜铉弩扉篝轭翳鲠祯弩镦嗒弼孱舄К矧殒翳弪狎铒扉篝螽砒犴痨搴换铄舡躔麸疴轭烘镲痼羼Жūū博ū畅暴衡狎疳沆蹴痼弪殄螬┅换唤è胖盼浩舷ū郝烈ò┅ㄅ峙卧浩舷ū博郝烈ū博ㄅ峙卧浩舷ū畅郝烈ǔ旦┅渝犰箫囵沆蹴皈ㄤ彐礤翳镤铄è疳沆蹴疳沆蹴瓠痼趄遽愆鏖翳箪雉疳趑弪瞟疳沆蹴躅戾篌弼孱舄蝈趱蝾骝镯铄屣皓铄舡躔麸疳趑弪蝈漉沐＇磲磲疸狎ㄦ戾铉翳ㄥ铙躜瀛扉篝ㄥ擤┅脲弼孱舄┅┅┅换疳沣蹴换梏麴蠛疸懋疱徕镤觇醍邃醑琪蜷玷舫篝漤鸩滹泱吁疱蛎镬扉溴蜻嘛镫泔溴描ゲ安哎舶溴麂蝻馥舶犷浈舶汨蹉腱殁溴麂蝻疬扉獐滗餍狒翦蝾蟑儒祓嗅沣蹴梏盱ㄤ彐疳趑弪疳沣蹴疳趑弪瞟è镳弪狒矧轰彐狨祠＇篝狎轰彐狨祠癌篝屦轰彐狨祠暴戾铉翳轰彐狨祠洪铈祜轰彐狨祠铋飑ㄨ轰彐狨祠铋飑ㄢ秕钿怡轰彐狨祠铋飑ㄣ躜蝈铘鲠祯后翎翦舂ㄣ躜蝈铘蝈疱狒蟓蝈磲轭轭后翎翦舂轰镢蹴孱翎糸镱⑽蹴弪殂徙沲眭灬麸虍裴汨秕麴豸犷釉判轶躞邃狍翳轭瘐骘闲乓猎弦麸珏铄蜥翦翳铄秕麴豸阻孱滔壬犷孪瘴沫沦狎痱秭殇邃秕麴豸翳狒驷祆秕趔殇翳蜥铉滔壬狎黩狃疱忉汶轭箝溴鏖翳翳孪瘴沫沦骢钽糸镱翳鲠祯轶痱秭殇邃狍轸骈蝮狎珲礤铘犷滔犷壬狎痱秭殇邃狍轸箦泔钿犷翳轵洚箩箦镱翳疳趑弪矧殓轭犰禊骝镯翳滗餍狒翦蝾吁疱蛎镬扉溴扉怛狎砒犴痨搴换铄舡躔麸疳沣蹴＇暴旦换筢礤狍痼弪殄暴换唤ò穿换铄舡躔麸疳沣蹴＇洪铈红鸿衡秕钿怡＇黩狃供换筢礤狍徕秭瀣黩狃痖铉忮赭邋犷钞换唤ò博渝犰箫囵箦蜷弩К囵珏镯К囵麽祀Б轰彐躅ㄤ彐躅疳沣蹴é镳糸镱犰镳弪狒矧＇篝狎癌篝屦暴戾铉翳洪铈脲祜栝怙躅洵怡磲脲轭篝犷沐ю徙沲猴疱蜥麸镳弪狒矧后翎螋篝狎后翦篝屦红孱玺戾铉翳红祜鸿栝衡秕钿怡怙躅洵怡┅ㄤ彐礤翳镤狍痼趄遽è疳沣蹴疳沣蹴┅鏖翳箪雉镳弪狒矧篝狎篝屦戾铉翳祜栝怙躅洵怡疳沣蹴磲脲轭篝犷沐ю徙沲憝痼趄遽猴疱蜥麸疳趑弪瞽狍痼趄遽镳弪狒矧后翎螋疳趑弪瞽狍痼趄遽篝狎舂后翦疳趑弪瞽狍痼趄遽篝屦红孱玺ㄡ蟓痼趄遽戾铉翳红疳趑弪瞽狍痼趄遽祜鸿疳趑弪瞽狍痼趄遽栝衡秕钿怡疳趑弪瞽狍痼趄遽怙躅洵怡┅┅ㄤ彐礤翳镤铄è疳沣蹴疳沣蹴痼趄遽愆鏖翳箪雉镳弪狒矧篝狎篝屦戾铉翳祜栝怙躅洵怡沲蝌孱舡鲠祯濠疳沣蹴躅戾篌蝈磲轭轭绛疳沣蹴ъ孱玺瑭蝈趱蝾骝镯铄屣皓ㄤ邈姝蝈磲轭轭疳沣蹴箦翩沲蝌孱舡鲠祯ㄩ箪雉怙躅漯疳沣蹴с躜蝈铘鲠祯濠麒孱戾è蝈ㄦ躅汜祆ㄩ痼趄遽憝镳弪狒矧铄镳弪狒矧镳弪狒矧沲蝌孱舡鲠祯铄篝屦┅┅ㄩ怙躅洵怡麒孱戾è骢钽ㄩ痼趄遽憝怙躅洵怡铄怙躅洵怡怙躅洵怡┅祜铄祜┅ㄨ铄栝┅ㄦ躅汜祆骢钽蝈祜栝┅蝈螬铄篝狎舂┅┅换痼换梏麴蠛瘐篝雉岙忉箝箪徵弪矧绡忒筱桢祓儒祓渺狍箦蟑杏梏盱换梏麴蠛瘐篝雉岙忉箝箪徵弪矧绡忒筱桢祓儒祓怎麸蜷犰蟑杏唧趄遽磉疳趑弪铙梏盱ㄤ彐疳趑弪痼疳趑弪瞟疳趑弪痼趄遽愆轰镢蹴孱翎糸镱⑿蝈箦蝣痼趄遽篝狒徙蝻篌篚怏羼蹂铘汜祆麸噌蟓痼趄遽恣燥蝈箦翳痼趄遽憩箝眇禊蝈弼犰踽翦翳痼溴骈铋糸镱箩箦镱翳疳趑弪矧殓轭犰禊骝镯翳黹用屐灬铄秕吁疱蛎镬扉溴扉怛狎砒犴痨搴换ㄤ彐疳蜥礤翦痼舄痼痼弪殄螬┅换换铄舡躔麸痼舄穿换唤ò畅换换铄舡躔麸痼舄穿换唤ù珐换换ㄤ彐疳蜥礤翦痼舄痼痼弪殄螬┅换换铄舡躔麸痼舄穿换唤ò畅渝犰箫囵蝮К囵溴妲轰彐躅ㄤ彐躅痼疳趑弪瞟磲脲轭篝犷沐ю吼狒翦蝾疳趑弪瞟┅ㄤ彐礤翳镤狍痼趄遽è痼痼┅鏖翳箪雉疳趑弪痼趄遽愆痼磲脲轭篝犷沐ю蟓痼趄遽吼狒翦蝾疳趑弪吼篝蝈犴ㄩ箪雉怙躅漯痼ю篝蝈犴痼趄遽戾è痼趄ㄡ蟓痼趄遽疳趑弪瞟┅箦翩痼趄遽痼趄痼趄┅┅ㄤ彐礤翳镤铄è痼痼趄遽痼痼趄遽愆鏖翳箪雉痼趄遽愆痼痼趄遽铄痼趄遽愆┅换痱ㄤ彐躅痱疳趑弪镳糸镱犰蝈疱狒洪铈┅⒂铘狲篚玑骘痱痼辛栽乓惟遗信猎萤阵彐蹯骘屮犴痨瀣麸孱篚蝈翳狒遽汨泫沆镦疳趑弪镱禊珏趔镱鲠祯骝镯翳囵螫渝犰箫囵颛囵螫痱痼疳趑弪瞟蝈疱狒螬瘐箬铄ю蝮疳趑弪铙换轲篝蝈犴ㄤ彐疳趑弪轲篝蝈犴疳趑弪瞟è疳趑弪铙轰彐狨祠扉篝┅ㄥ钿麒孱屙痿轰彐狨祠铋飑ㄧ蜥铛灬蜷豉轰彐狨祠悲旦祜汶后翎翦洪铋翩矧ㄢ艉磲脲蝈沲蝮轹瀛祜汶㈤痼趄遽疳趑弪铙箪雉祜汶┅轰镢蹴孱翎糸镱⑸铙弪翎忪痼趄遽砘痼趄遽翳狒汜忮汨犷珏麒殪轸蝓铑轭怡轭箦螋轭铄疳趑弪铙狒箴邈殒殄忮狒ㄤ彐礤翳镤狍痼趄遽è轲篝蝈犴轲篝蝈犴┅鏖翳箪雉疳趑弪铙孱洵麒孱屙痿珧犷蹯狎轸轲篝蝈犴戾è痼趄磲脲轭篝犷沐ч痼趄遽憝痼趄遽吼狒翦蝾扉篝哄钿麒孱屙痿孱洵麒孱屙痿虹蜥铛灬蜷豉珧犷蹯狎轸红镢ㄢ艉磲脲蝈沲蝮轹瀛祜汶㈤痼趄遽疳趑弪铙箪雉祜汶┅┅ㄤ镬轶疳疳趑弪铙痼趄ㄥ豉疱汜箦疳疳趑弪ㄩ痼趄遽憝轭箦螋痼趄疳癌扉篝ㄤ弩趄蹉趱蜷铉忾钿ㄢ遽蝈篝疳趑弪铙疳ㄤ镬轶疳疳趑弪铙ㄩ痼趄遽憝轭箦螋痼趄疳忮狒┅┅┅┅ㄤ彐礤翳镤铄è轲篝蝈犴轲篝蝈犴痼趄遽愆鏖翳箪雉疳趑弪铙孱洵麒孱屙痿珧犷蹯狎轸祜汶轲篝蝈犴灬忮祗è徙趱犰忮狒痼趄遽愆ǐ箪雉鲠祯痼趄遽篝狎舡忮狒ㄢ遽痼趄遽愆┅箫螋邃痼趄ī箫螋疳趑弪铙＇弘妁＇徙趱犰忮狒┅ㄢ艉鏖翳蝈沲蝮轹瀛祜汶桢熹祜汶ㄩ疳趑弪铙戾舄è铄舡痼趄ㄣ狎箫螋邃痼趄螬┅ㄥㄩ冀ㄡ泗踽飙忮狒铄舡痼趄ㄢ遽轲篝蝈犴┅戾è铠铄铄舡痼趄┅ㄩㄥ镳铠舂痱镧ㄤ屐弭彐疳趑弪铙铄舡痼趄铄轲篝蝈犴┅铠舂ㄥ鲥铘呼疱候弩轰屐翎珧犷蹯狎轸┅铄舡痼趄ㄣ狎箫螋邃痼趄螬┅ㄣ镯忾铄弼孱趔弼ㄥ鲥铘轰屐翎ㄩ铄舡痼趄黹珧犷蹯狎轸ōㄡ泗踽飙忮狒铄舡痼趄ㄢ遽轲篝蝈犴┅珧犷蹯狎轸┅┅ㄩ孱洵麒孱屙痿屣ㄥ鲥铘呼疱候弩轰屐翎珧犷蹯狎轸┅┅┅ㄤ彐珏铄蜷轲篝蝈犴轭箦螋ㄩ痼趄遽疳趑弪镳糸镱犰篝狎舡忮狒ê滹沲礤铘狒轱⑸铙弪辛栽乓轭麸尚釉遗镣狒釉烈原屡猎釉烈原屡猎溴驷蹯趔麸翳轲篝蝈犴沲蝌孱忮狒┅ㄤ彐礤翳镤轲篝蝈犴轭箦螋è轲篝蝈犴轲篝蝈犴痼趄遽愆疳趑弪镳糸镱犰篝狎舡忮狒鏖翳箪雉疳趑弪铙祜汶轲篝蝈犴戾è痼趄ㄡ蟓痼趄遽疳趑弪瞟┅箦翩箪雉鲠祯痼趄篝狎舡忮狒矧篝狎舡忮狒ㄢ遽轲篝蝈犴┅ㄢ艉鏖翳祜汶桢熹祜汶瘐箬痼趄疳趑弪铙┅┅ㄥ痫螋ч痼趄遽憝轭箦螋换疰殪翦ㄤ彐疳趑弪疰殪翦疳趑弪瞟疳趑弪痱邃殂狒洪铋翩矧ч溴铘轸┅轰镢蹴孱翎糸镱⒂腴屐屙孱趔镦箫躜沐疳趑弪翳狒幸拍擅猎蝈趱蝾驷祗骘虍涉幸拍擅猎轶铒骢钽糸镱箅轲轸屙翳狒狎噱耢麸轸砒犴痨搴换铄舡疰殪翦痼羼扉篝畅换博换订换唤ū畅换铄舡疰殪翦痼弪殄暴у鲥铕换订换唤ū北渝犰箫囵骈祠弪秕臾囵颛囵漉蝮趱趑弪Бㄤ彐礤翳镤铄è疰殪翦疰殪翦颦痼趄遽愆鏖翳箪雉疳趑弪痱邃殂狒濠疰殪翦戾è骢钽ㄩㄦ躅泗轱瞽溴箝珙狒矧痱邃殂狒濠痱邃殂狒灬礅溽ㄩ铕豸ㄥ耢轭瘐痱邃殂狒濠┅┅祜镳烘矧蝈航铄疳趑弪瞟洪矧ㄥ镳蝈螬ㄦ躅汜祆骢钽蝈螬候弭躜蝈螬┅换疰殪翦颦秕ㄤ彐躅疰殪翦颦秕疳趑弪痱邃殂狒濠⒂腴屐屙孱趔镦箫躜沐疳趑弪翳狒幸拍擅猎蝈趱蝾趄蹂骘虍涉幸拍擅猎轶铒骢钽糸镱箅轲轸屙翳狒狎噱耢麸轸渝犰箫囵骈祠弪Б疰殪翦疳趑弪ㄩㄦ躅泗轱瞽溴箝珙狒矧痱邃殂狒濠灬礅溽铒ㄦ躅汜祆痱邃殂狒┅灬礅溽铒ㄥ耢痱邃殂狒┅┅┅瘐箬铄ю骈祠弪秕疳趑弪铙