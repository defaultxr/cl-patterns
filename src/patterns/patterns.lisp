;;;; patterns.lisp - basic pattern functionality (`defpattern', etc) and a variety of basic patterns implemented with it.

(in-package #:cl-patterns)

;;; pattern glue

(defun make-default-event ()
  "Get `*event*' if it's not nil, or get a fresh empty event."
  (or *event* (event)))

(defvar *patterns* nil
  "List of the names of all defined pattern types.

See also: `all-patterns'")

(defmacro defpattern (name superclasses slots &key documentation defun)
  "Define a pattern. This macro automatically generates the pattern's class, its pstream class, and the function to create an instance of the pattern, and makes them external in the cl-patterns package.

NAME is the name of the pattern. Typically a word or two that describes its function, prefixed with p.

SUPERCLASSES is a list of superclasses of the pattern. Most patterns just subclass the 'pattern' class.

SLOTS is a list of slots that the pattern and pstreams derived from it have. Each slot can either be just a symbol, or a slot definition a la `defclass'. You can provide a default for the slot with the :initform key as usual, and you can set a slot as a state slot (which only appears in the pattern's pstream class) by setting the :state key to t.

DOCUMENTATION is a docstring describing the pattern. We recommend providing at least one example, and a \"See also\" section to refer to similar pattern classes.

DEFUN can either be a full defun form for the pattern, or an expression which will be inserted into the pattern creation function prior to initialization of the instance. Typically you'd use this for inserting `assert' statements, for example.

See also: `pattern', `pdef', `all-patterns'"
  (let* ((superclasses (or superclasses (list 'pattern)))
         (slots (mapcar #'ensure-list slots))
         (name-pstream (pattern-pstream-class-name name))
         (super-pstream (if (eql 'pattern (car superclasses))
                            'pstream
                            (pattern-pstream-class-name (car superclasses)))))
    (labels ((desugar-slot (slot)
               "Convert a slot into something appropriate for defclass to handle."
               (destructuring-bind (name . rest) slot
                 (append (list name)
                         (remove-from-plist rest :state)
                         (unless (position :initarg (keys rest))
                           (list :initarg (make-keyword name))))))
             (optional-slot-p (slot)
               "Whether the slot is optional or not. A slot is considered optional if an initform is provided."
               (position :initform (keys (cdr slot))))
             (state-slot-p (slot)
               "Whether the slot is a pstream state slot or not. Pstream state slots only appear as slots for the pattern's pstream class and not for the pattern itself."
               (position :state (keys (cdr slot))))
             (function-lambda-list (slots)
               "Generate the lambda list for the pattern's creation function."
               (let (optional-used)
                 (mappend (fn (unless (state-slot-p _)
                                (if (optional-slot-p _)
                                    (prog1
                                        (append (unless optional-used
                                                  (list '&optional))
                                                (list (list (car _) (getf (cdr _) :initform))))
                                      (setf optional-used t))
                                    (list (car _)))))
                          slots)))
             (make-defun (pre-init)
               `(defun ,name ,(function-lambda-list slots)
                  ,documentation
                  ,@(when pre-init (list pre-init))
                  (make-instance ',name
                                 ,@(mappend (fn (unless (state-slot-p _)
                                                  (list (make-keyword (car _)) (car _))))
                                            slots))))
             (add-doc-to-defun (sexp)
               (if (and (listp sexp)
                        (position (car sexp) (list 'defun 'defmacro))
                        (not (stringp (fourth sexp))))
                   (append (subseq sexp 0 3) (list documentation) (subseq sexp 3))
                   sexp)))
      `(progn
         (defclass ,name ,superclasses
           ,(mapcar #'desugar-slot (remove-if #'state-slot-p slots))
           ,@(when documentation
               `((:documentation ,documentation))))
         (defmethod print-object ((,name ,name) stream)
           (print-unreadable-object (,name stream :type t)
             (format stream "誉"
                     (mapcar (lambda (slot) (slot-value ,name slot))
                             ',(mapcar #'car (remove-if (lambda (slot)
                                                          (or (state-slot-p slot)
                                                              ;; FIX: don't show arguments that are set to the defaults?
                                                              ))
                                                        slots))))))
         (defclass ,name-pstream (,super-pstream ,name) ; FIX: this will overwrite custom pstream classes when redefining the pattern class. should we refrain from redefining the pstream class if it's already defined? or is it possible to remove this definition entirely and just use the standard pstream class by default?
           ,(mapcar #'desugar-slot (remove-if-not #'state-slot-p slots))
           (:documentation ,(format nil "pstream for `A'." (string-downcase name))))
         ,(let* ((gen-func-p (or (null defun)
                                 (and (listp defun)
                                      (position (car defun) (list 'assert 'check-type)))))
                 (pre-init (when gen-func-p
                             defun)))
            (if gen-func-p
                (make-defun pre-init)
                (add-doc-to-defun defun)))
         (pushnew ',name *patterns*)))))

(defvar *max-pattern-yield-length* 256
  "The default maximum number of events or values that will be used by functions like `next-n' or patterns like `protate', in order to prevent hangs caused by infinite-length patterns.")

(defvar *default-pattern-length* :inf
  "The default value of a pattern's LENGTH parameter.")

(defvar *default-pattern-repeats* :inf
  "The default value of a pattern's REPEATS parameter.")

;;; pattern

(defgeneric pattern-source (pattern)
  (:documentation "The source object that this object was created from. For example, for a `pstream', this would be the pattern that `as-pstream' was called on."))

(defgeneric pstream-count (pattern)
  (:documentation "The number of pstreams that have been made of this pattern."))

(defclass pattern ()
  ((play-quant :initarg :play-quant :documentation "A list of numbers representing when the pattern's pstream can start playing. See `play-quant' and `quant'.")
   (end-quant :initarg :end-quant :accessor end-quant :type list :documentation "A list of numbers representing when a pattern can end playing and when a `pdef' can be swapped out for a new definition. See `end-quant' and `quant'.")
   (end-condition :initarg :end-condition :initform nil :accessor end-condition :type (or null function) :documentation "Nil or a function that is called by the clock with the pattern as its argument to determine whether the pattern should end or swap to a new definition.")
   (source :initarg :source :initform nil :accessor pattern-source :documentation "The source object that this object was created from. For example, for a `pstream', this would be the pattern that `as-pstream' was called on.")
   (parent :initarg :parent :initform nil :documentation "When a pattern is embedded in another pattern, the embedded pattern's parent slot points to the pattern it is embedded in.")
   (loop-p :initarg :loop-p :documentation "Whether or not the pattern should loop when played.")
   (cleanup :initarg :cleanup :initform nil :documentation "A list of functions that are run when the pattern ends or is stopped.")
   (pstream-count :initform 0 :accessor pstream-count :documentation "The number of pstreams that have been made of this pattern.")
   (metadata :initarg :metadata :initform (make-hash-table) :type hash-table :documentation "Hash table of additional data associated with the pattern, accessible with the `pattern-metadata' function."))
  (:documentation "Abstract pattern superclass."))

(defun set-parents (pattern)
  "Loop through PATTERN's slots and set the \"parent\" slot of any patterns to this pattern."
  (labels ((set-parent (list parent)
             "Recurse through LIST, setting the parent of any pattern found to PARENT."
             (typecase list
               (list
                (mapc (lambda (x) (set-parent x parent)) list))
               (pattern
                (setf (slot-value list 'parent) parent)))))
    (dolist (slot (mapcar #'closer-mop:slot-definition-name (closer-mop:class-slots (class-of pattern))) pattern)
      (when (and (not (eql slot 'parent))
                 (slot-boundp pattern slot))
        (set-parent (slot-value pattern slot) pattern)))))

(defmethod initialize-instance :after ((pattern pattern) &key)
  (set-parents pattern))

(defun pattern-p (object)
  "True if OBJECT is a pattern.

See also: `pattern', `defpattern'"
  (typep object 'pattern))

(defun all-patterns ()
  "Get a list of the names of all defined pattern classes.

See also: `all-pdefs'"
  *patterns*)

(defmethod play-quant ((pattern pattern))
  (if (slot-boundp pattern 'play-quant)
      (slot-value pattern 'play-quant)
      (list 1)))

(defmethod (setf play-quant) (value (pattern pattern))
  (setf (slot-value pattern 'play-quant) (ensure-list value)))

(defmethod end-quant ((pattern pattern))
  (when (slot-boundp pattern 'end-quant)
    (slot-value pattern 'end-quant)))

(defmethod (setf end-quant) (value (pattern pattern))
  (setf (slot-value pattern 'end-quant) (ensure-list value)))

(defmethod play ((pattern pattern))
  (clock-add (as-pstream pattern) *clock*))

(defmethod launch ((pattern pattern))
  (play pattern))

(defmethod playing-p ((pattern pattern) &optional (clock *clock*))
  (when clock
    (find pattern (clock-tasks clock)
          :key (fn (slot-value _ 'item)))))

(defmethod loop-p ((pattern pattern))
  (when (slot-boundp pattern 'loop-p)
    (slot-value pattern 'loop-p)))

(defmethod (setf loop-p) (value (pattern pattern))
  (setf (slot-value pattern 'loop-p) value))

(defmethod dur ((pattern pattern))
  (reduce #'+ (next-upto-n pattern) :key #'dur))

(defun pattern-parent (pattern &key (num 1) (accumulate nil) (class 'pattern))
  "Get the NUM-th containing pattern of PATTERN, or nil if there isn't one. If CLASS is specified, only consider patterns of that class.

See also: `pattern-children'"
  (check-type num (integer 0))
  (let ((i 0)
        res)
    (until (or (>= i num)
               (null pattern))
      (setf pattern (slot-value pattern 'parent))
      (when (typep pattern class)
        (incf i)
        (when accumulate
          (appendf res pattern))))
    (if accumulate
        res
        pattern)))

(defun pattern-children (pattern &key (num 1) (accumulate nil) (class 'pattern))
  "Get a list of all the direct child patterns of PATTERN, including any in slots or lists.

See also: `pattern-parent'"
  (let ((cur (list pattern))
        res)
    (dotimes (n num res)
      (setf cur (remove-if-not (lambda (pattern) (typep pattern class))
                               (mapcan #'%pattern-children cur)))
      (if accumulate
          (appendf res cur)
          (setf res cur)))))

(defmethod %pattern-children ((object t))
  nil)

(defmethod %pattern-children ((pattern pattern))
  (mapcan (lambda (slot)
            (copy-list (ensure-list (slot-value pattern (closer-mop:slot-definition-name slot)))))
          (closer-mop:class-direct-slots (class-of pattern))))

(defgeneric pattern-metadata (pattern &optional key)
  (:documentation "Get the value of PATTERN's metadata for KEY. Returns true as a second value if the metadata had an entry for KEY, or nil if it did not."))

(defmethod pattern-metadata ((pattern pattern) &optional key)
  (with-slots (metadata) pattern
    (if key
        (gethash key metadata)
        metadata)))

(defun (setf pattern-metadata) (value pattern key)
  (setf (gethash key (slot-value pattern 'metadata)) value))

(defgeneric peek (pattern)
  (:documentation "\"Peek\" at the next value of a pstream, without advancing its current position.

See also: `next', `peek-n', `peek-upto-n'"))

(defun peek-n (pstream &optional (n *max-pattern-yield-length*))
  "Peek at the next N results of a pstream, without advancing it forward in the process.

See also: `peek', `peek-upto-n', `next', `next-n'"
  (check-type n (integer 0))
  (unless (pstream-p pstream)
    (return-from peek-n (peek-n (as-pstream pstream) n)))
  (with-slots (number future-number) pstream
    (loop :for i :from 0 :below n
          :collect (pstream-elt-future pstream (+ number (- future-number) i)))))

(defun peek-upto-n (pstream &optional (n *max-pattern-yield-length*))
  "Peek at up to the next N results of a pstream, without advancing it forward in the process.

See also: `peek', `peek-n', `next', `next-upto-n'"
  (check-type n (integer 0))
  (unless (pstream-p pstream)
    (return-from peek-upto-n (peek-upto-n (as-pstream pstream) n)))
  (with-slots (number future-number) pstream
    (loop :for i :from 0 :below n
          :for res := (pstream-elt-future pstream (+ number (- future-number) i))
          :until (eop-p res)
          :collect res)))

(defgeneric next (pattern)
  (:documentation "Get the next value of a pstream, function, or other object, advancing the pstream forward in the process.

See also: `next-n', `next-upto-n', `peek'"))

(defmethod next ((object t))
  object)

(defmethod next ((pattern pattern))
  (next (as-pstream pattern)))

(defmethod next ((function function))
  (funcall function))

(defun next-n (pstream &optional (n *max-pattern-yield-length*))
  "Get the next N outputs of a pstream, function, or other object, advancing the pstream forward N times in the process.

See also: `next', `next-upto-n', `peek', `peek-n'"
  (check-type n (integer 0))
  (let ((pstream (pattern-as-pstream pstream)))
    (loop :repeat n
          :collect (next pstream))))

(defun next-upto-n (pstream &optional (n *max-pattern-yield-length*))
  "Get a list of up to N results from PSTREAM, not including the end of pattern.

See also: `next', `next-n', `peek', `peek-upto-n'"
  (check-type n (integer 0))
  (let ((pstream (pattern-as-pstream pstream)))
    (loop
      :for number :from 0 :upto n
      :while (< number n)
      :for val := (next pstream)
      :if (eop-p val)
        :do (loop-finish)
      :else
        :collect val)))

(defgeneric bsubseq (object start-beat &optional end-beat)
  (:documentation "\"Beat subseq\" - get a list of all events from OBJECT whose `beat' is START-BEAT or above, and below END-BEAT.

See also: `events-in-range'"))

(defgeneric events-in-range (pstream min max)
  (:documentation "Get all the events from PSTREAM whose start beat are MIN or greater, and less than MAX."))

(defmethod events-in-range ((pattern pattern) min max)
  (events-in-range (as-pstream pattern) min max))

;;; pstream

;; FIX: can we avoid making this inherit from pattern?
(defclass pstream (pattern #+#.(cl:if (cl:find-package "SEQUENCE") '(:and) '(:or)) sequence)
  ((number :initform 0 :documentation "The number of outputs yielded from this pstream and any sub-pstreams that have ended.") ; FIX: rename to this-index ?
   (pattern-stack :initform (list) :documentation "The stack of pattern pstreams embedded in this pstream.")
   (pstream-count :initarg :pstream-count :accessor pstream-count :type integer :documentation "How many times a pstream was made of this pstream's source prior to this pstream. For example, if it was the first time `as-pstream' was called on the pattern, this will be 0.")
   (beat :initform 0 :reader beat :type number :documentation "The number of beats that have elapsed since the start of the pstream.")
   (history :type vector :documentation "The history of outputs yielded by the pstream.")
   (history-number :initform 0 :documentation "The number of items in this pstream's history. Differs from the number slot in that all outputs are immediately included in its count.")
   (start-beat :initarg :start-beat :initform nil :documentation "The beat number of the parent pstream when this pstream started.")
   (future-number :initform 0 :documentation "The number of peeks into the future that have been made in the pstream. For example, if `peek' is used once, this would be 1. If `next' is called after that, future-number decreases back to 0.")
   (future-beat :initform 0 :documentation "The current beat including all future outputs (the `beat' slot does not include peeked outputs)."))
  (:documentation "\"Pattern stream\". Keeps track of the current state of a pattern in process of yielding its outputs."))

(defmethod initialize-instance :before ((pstream pstream) &key)
  (with-slots (history) pstream
    (setf history (make-array *max-pattern-yield-length* :initial-element nil))))

(defmethod initialize-instance :after ((pstream pstream) &key)
  (set-parents pstream))

(defmethod print-object ((pstream pstream) stream)
  (with-slots (number) pstream
    (print-unreadable-object (pstream stream :type t)
      (format stream "S S" :number number))))

(defun pstream-p (object)
  "True if OBJECT is a pstream.

See also: `pstream', `as-pstream'"
  (typep object 'pstream))

(defmethod loop-p ((pstream pstream))
  (if (slot-boundp pstream 'loop-p)
      (slot-value pstream 'loop-p)
      (loop-p (slot-value pstream 'source))))

(defmethod ended-p ((pstream pstream))
  (with-slots (number future-number) pstream
    (and (not (zerop (- number future-number)))
         (eop-p (pstream-elt pstream -1)))))

(defmethod events-in-range ((pstream pstream) min max)
  (while (and (<= (beat pstream) max)
              (not (ended-p pstream)))
    (let ((next (next pstream)))
      (unless (typep next '(or null event))
        (error "events-in-range can only be used on event streams."))))
  (loop :for i :across (slot-value pstream 'history)
        :if (and i
                 (>= (beat i) min)
                 (< (beat i) max))
          :collect i
        :if (or (eop-p i)
                (>= (beat i) max))
          :do (loop-finish)))

(defgeneric last-output (pstream)
  (:documentation "Returns the last output yielded by PSTREAM.

Example:

;; (defparameter *pstr* (as-pstream (pseq '(1 2 3) 1)))
;; (next *pstr*) ;=> 1
;; (last-output *pstr*) ;=> 1

See also: `ended-p'"))

(defmethod last-output ((pstream pstream))
  (with-slots (number future-number) pstream
    (let ((idx (- number future-number)))
      (when (plusp idx)
        (pstream-elt pstream (- idx (if (ended-p pstream) 2 1)))))))

(defun value-remaining-p (value)
  "True if VALUE represents that a pstream has outputs \"remaining\"; i.e. VALUE is a symbol (i.e. :inf), or a number greater than 0.

See also: `remaining-p', `decf-remaining'"
  (typecase value
    (null nil)
    (symbol (eql value :inf))
    (number (plusp value))
    (otherwise nil)))

(defun remaining-p (pattern &optional (repeats-key 'repeats) (remaining-key 'current-repeats-remaining))
  "True if PATTERN's REMAINING-KEY slot value represents outputs \"remaining\" (see `value-remaining-p'). If PATTERN's REMAINING-KEY slot is unbound or 0, and REPEATS-KEY is not nil, then it is automatically set to the `next' of PATTERN's REPEATS-KEY slot. Then if that new value is 0 or nil, remaining-p returns nil. Otherwise, :reset is returned as a generalized true value and to indicate that `next' was called on PATTERN's REPEATS-KEY slot.

See also: `value-remaining-p', `decf-remaining'"
  (labels ((set-next ()
             (setf (slot-value pattern remaining-key) (next (slot-value pattern repeats-key)))
             (when (value-remaining-p (slot-value pattern remaining-key))
               :reset)))
    (if (not (slot-boundp pattern remaining-key))
        (set-next)
        (let ((rem-key (slot-value pattern remaining-key)))
          (typecase rem-key
            (null nil)
            (symbol (eql rem-key :inf))
            (number (if (plusp rem-key)
                        t
                        (set-next))) ; if it's already set to 0, it was decf'd to 0 in the pattern, so we get the next one. if the next is 0, THEN we return nil.
            (otherwise nil))))))

(defun decf-remaining (pattern &optional (key 'current-repeats-remaining))
  "Decrease PATTERN's KEY value.

See also: `remaining-p'"
  (when (numberp (slot-value pattern key))
    (decf (slot-value pattern key))))

(defmethod peek ((pstream pstream))
  (with-slots (number future-number) pstream
    (pstream-elt-future pstream (- number future-number))))

(defmethod peek ((pattern pattern))
  (next (as-pstream pattern)))

(defmethod next ((pstream pstream))
  ;; fallback method; patterns should override their pstream subclasses with their own behaviors
  nil)

(defvar *post-pattern-output-processors* (list 'remap-instrument-to-parameters)
  "List of functions that are applied as the last step of pattern output generation. Each output yielded by an \"outermost\" pattern (i.e. one without a `pattern-parent') will be processed (along with the pstream as a second argument) through each function in this list, allowing for arbitrary transformations of the generated outputs. The return value of each function is used as the input to the next function, and the return value of the last function is used as the output yielded by the pattern.

This can be used, for example, to implement mappings from friendly instrument names to the full parameters needed to specify the instrument in question for backends such as MIDI which require it; in fact this feature is already implemented more conveniently with the setf-able `instrument-mapping' function.

See also: `*instrument-map*', `remap-instrument-to-parameters'")

(defvar *instrument-map* (make-hash-table :test #'equal)
  "Hash table mapping instrument names (as symbols) to arbitrary parameter lists. Used by `remap-instrument-to-parameters' as part of post-pattern output processing. Any events whose :instrument is not found in this table will not be affected.

See also: `remap-instrument-to-parameters'")

(defun remap-instrument-to-parameters (output &optional pstream)
  "Remap OUTPUT's instrument key to arbitrary parameters specified in `*instrument-map*'. If OUTPUT is not an event or the instrument is not found in the map, it is passed through unchanged.

See also: `instrument-mapping', `*instrument-map*', `*post-pattern-output-processors*'"
  (declare (ignore pstream))
  (unless (event-p output)
    (return-from remap-instrument-to-parameters output))
  (when-let ((mapping (gethash (event-value output :instrument) *instrument-map*)))
    (etypecase mapping
      (symbol
       (setf (event-value output :instrument) mapping))
      (list
       (doplist (key value mapping)
         (setf (event-value output key) value)))))
  output)

(defun instrument-mapping (instrument)
  "Get a mapping from INSTRUMENT (an instrument name as a string or symbol) to a plist of parameters which should be set in the event by `remap-instrument-to-parameters'.

See also: `remap-instrument-to-parameters', `*instrument-map*'"
  (gethash instrument *instrument-map*))

(defun (setf instrument-mapping) (value instrument)
  "Set a mapping from INSTRUMENT (an instrument name as a string or symbol) to a plist of parameters which will be set in the event by `remap-instrument-to-parameters'. Setting an instrument to nil with this function removes it from the map.

See also: `instrument-mapping', `remap-instrument-to-parameters', `*instrument-map*'"
  (assert (or (typep value '(or symbol number))
              (and (listp value)
                   (evenp (list-length value))))
          (value)
          "S's VALUE argument must be a symbol, a number, or a plist; got S instead" 'instrument-mapping value)
  (if value
      (setf (gethash instrument *instrument-map*) value)
      (remhash instrument *instrument-map*)))

(defmethod next :around ((pstream pstream))
  (labels ((get-value-from-stack (pattern)
             (with-slots (number pattern-stack) pattern
               (if pattern-stack
                   (let* ((popped (pop pattern-stack))
                          (nv (next popped)))
                     (if (eop-p nv)
                         (get-value-from-stack pattern)
                         (progn
                           (push popped pattern-stack)
                           nv)))
                   (prog1
                       (let ((res (call-next-method)))
                         (typecase res
                           (pattern
                            (if (typep pattern '(or function t-pstream))
                                res
                                (progn ; if `next' returns a pattern, we push it to the pattern stack as a pstream
                                  (let ((pstr (as-pstream res)))
                                    (setf (slot-value pstr 'start-beat) (beat pattern))
                                    (push pstr pattern-stack))
                                  (get-value-from-stack pattern))))
                           (t res)))
                     (incf number))))))
    (with-slots (number history history-number future-number) pstream
      (let ((result (if (plusp future-number)
                        (let ((result (elt history (- number future-number))))
                          (decf future-number)
                          (when (event-p result)
                            (incf (slot-value pstream 'beat) (event-value result :delta)))
                          result)
                        (let ((result (restart-case
                                          (get-value-from-stack pstream)
                                        (yield-output (&optional (value 1))
                                          :report (lambda (s) (format s "Yield an alternate output for S." pstream))
                                          :interactive (lambda ()
                                                         (format *query-io* "&Enter a form to yield: ")
                                                         (finish-output *query-io*)
                                                         (list (eval (read *query-io*))))
                                          value))))
                          (when (event-p result)
                            (setf result (copy-event result))
                            (when (and (null (raw-event-value result :beat))
                                       (null (slot-value pstream 'parent)))
                              (setf (beat result) (slot-value pstream 'future-beat)))
                            (incf (slot-value pstream 'beat) (event-value result :delta))
                            (incf (slot-value pstream 'future-beat) (event-value result :delta)))
                          (setf (elt history (mod history-number (length (slot-value pstream 'history)))) result)
                          (incf history-number)
                          result))))
        (unless (pattern-parent pstream)
          (dolist (proc *post-pattern-output-processors*)
            (setf result (funcall proc result pstream))))
        result))))

(defgeneric as-pstream (thing)
  (:documentation "Return THING as a pstream object.

See also: `pattern-as-pstream'"))

(defun pattern-as-pstream (thing)
  "Like `as-pstream', but only converts THING to a pstream if it is a pattern."
  (if (typep thing 'pattern)
      (as-pstream thing)
      thing))

(defgeneric t-pstream-value (object)
  (:documentation "The value that is yielded by the t-pstream."))

(defgeneric t-pstream-length (object)
  (:documentation "The number of times to yield the value."))

(defclass t-pstream (pstream)
  ((value :initarg :value :initform nil :accessor t-pstream-value :documentation "The value that is yielded by the t-pstream.")
   (length :initarg :length :initform 1 :accessor t-pstream-length :documentation "The number of times to yield the value."))
  (:documentation "Pattern stream object that by default yields its value only once."))

(defun t-pstream (value &optional (length 1))
  "Make a t-pstream object with the value VALUE."
  (check-type length (or (integer 0) (eql :inf)))
  (make-instance 't-pstream
                 :value value
                 :length length))

(defmethod print-object ((t-pstream t-pstream) stream)
  (with-slots (value length) t-pstream
    (print-unreadable-object (t-pstream stream :type t)
      (format stream "S S" value length))))

(defun t-pstream-p (object)
  "True if OBJECT is a `t-pstream'.

See also: `t-pstream', `as-pstream'"
  (typep object 't-pstream))

(defmethod as-pstream ((value t))
  (t-pstream value))

(defmethod next ((t-pstream t-pstream))
  (with-slots (value length number) t-pstream
    (when (and (not (eql :inf length))
               (>= number length))
      (return-from next eop))
    (if (functionp value)
        (funcall value)
        value)))

(defmethod as-pstream ((pattern pattern))
  (let* ((class (class-of pattern))
         (name (class-name class))
         (slots (remove 'parent (mapcar #'closer-mop:slot-definition-name (closer-mop:class-slots class)))))
    (apply #'make-instance
           (pattern-pstream-class-name name)
           (mapcan (fn (when (slot-boundp pattern _)
                         (let ((kw (make-keyword _)))
                           (list kw (funcall (if (member kw (list :length :repeats))
                                                 #'as-pstream
                                                 #'pattern-as-pstream)
                                             (slot-value pattern _))))))
                   slots))))

(defmethod as-pstream :around ((object t))
  (let ((pstream (call-next-method)))
    (with-slots (pstream-count source) pstream
      (setf pstream-count (if (slot-exists-p object 'pstream-count)
                              (slot-value object 'pstream-count)
                              0)
            source object))
    (when (slot-exists-p object 'pstream-count)
      (incf (slot-value object 'pstream-count)))
    pstream))

(defmethod as-pstream ((pstream pstream)) ; prevent pstreams from being "re-converted" to pstreams
  pstream)

(define-condition pstream-out-of-range ()
  ((index :initarg :index :reader pstream-elt-index))
  (:report (lambda (condition stream)
             (format stream "The index D falls outside the scope of the pstream's history." (pstream-elt-index condition)))))

(defun pstream-elt-index-to-history-index (pstream index)
  "Given INDEX, an absolute index into PSTREAM's history, return the actual index into the current recorded history of the pstream.

See also: `pstream-history-advance-by'"
  (check-type index (integer 0))
  (with-slots (history) pstream
    (mod index (length history))))

(defun pstream-elt (pstream n)
  "Get the Nth item in PSTREAM's history. For negative N, get the -Nth most recent item.

Example:

;; (let ((pstream (as-pstream (pseq '(1 2 3)))))
;;   (next pstream) ;=> 1
;;   (pstream-elt pstream 0) ;=> 1 ; first item in the pstream's history
;;   (next pstream) ;=> 2
;;   (pstream-elt pstream 1) ;=> 2 ; second item in the pstream's history
;;   (pstream-elt pstream -1)) ;=> 2 ; most recent item in the pstream's history

See also: `pstream-elt-future', `phistory'"
  (check-type n integer)
  (unless (pstream-p pstream)
    (return-from pstream-elt (pstream-elt (as-pstream pstream) n)))
  (with-slots (history history-number) pstream
    (let ((real-index (if (minusp n)
                          (+ history-number n)
                          n)))
      (if (and (>= real-index (max 0 (- history-number (length history))))
               (< real-index history-number))
          (elt history (pstream-elt-index-to-history-index pstream real-index))
          (error 'pstream-out-of-range :index n)))))

(defun pstream-history-advance-by (pstream index) ; FIX: add tests for this
  "Convert a history index (i.e. a positive number provided to `pstream-elt-future') to the amount that the history must be advanced by.

If the provided index is before the earliest item in history, the result will be a negative number denoting how far beyond the earliest history the index is.
If the provided index is within the current history, the result will be zero.
If the provided index is in the future, the result will be a positive number denoting how far in the future it is.

See also: `pstream-elt-index-to-history-index'"
  (check-type index (integer 0))
  (with-slots (history history-number) pstream
    (let ((history-length (length history)))
      (if (< index (- history-number history-length))
          (- history-number history-length)
          (if (>= index history-number)
              (- index (1- history-number))
              0)))))

(defun pstream-elt-future (pstream n)
  "Get the element N away from the most recent in PSTREAM's history. Unlike `pstream-elt', this function will automatically peek into the future for any positive N.

Example:

;; (let ((pstream (as-pstream (pseq '(1 2 3)))))
;;   (pstream-elt-future pstream 0) ;=> 1
;;   (next pstream) ;=> 1
;;   (pstream-elt-future pstream 1) ;=> 2
;;   (next pstream)) ;=> 2

See also: `pstream-elt', `phistory'"
  (check-type n integer)
  (unless (pstream-p pstream)
    (return-from pstream-elt-future (pstream-elt-future (as-pstream pstream) n)))
  (when (minusp n)
    (return-from pstream-elt-future (pstream-elt pstream n)))
  (with-slots (history history-number future-number) pstream
    (let ((advance-by (pstream-history-advance-by pstream n)))
      (when (or (minusp advance-by)
                (> (+ future-number advance-by) (length history)))
        ;; the future and history are recorded to the same array.
        ;; since the array is of finite size, requesting more from the future than history is able to hold would result in the oldest elements of the future being overwritten with the newest, thus severing the timeline...
        (error 'pstream-out-of-range :index n))
      (let ((prev-future-number future-number))
        (setf future-number 0) ; temporarily set it to 0 so the `next' method runs normally
        (loop :repeat advance-by
              :for next := (next pstream)
              :if (event-p next)
                :do (decf (slot-value pstream 'beat) (event-value next :delta)))
        (setf future-number (+ prev-future-number advance-by))))
    (let ((real-index (pstream-elt-index-to-history-index pstream n)))
      (elt history real-index))))

;;; pbind

(defvar *pbind-special-init-keys* nil
  "The list of special keys for pbind that alters it during its initialization.

See also: `define-pbind-special-init-key'")

(defvar *pbind-special-wrap-keys* nil
  "The list of special keys for pbind that causes the pbind to be replaced by another pattern during its initialization.

See also: `define-pbind-special-wrap-key'")

(defvar *pbind-special-process-keys* nil
  "The list of special keys for pbind that alter the outputs of the pbind.

See also: `define-pbind-special-process-key'")

(defclass pbind (pattern)
  ((pairs :initarg :pairs :initform nil :accessor pbind-pairs :documentation "The pattern pairs of the pbind; a plist mapping its keys to their values."))
  (:documentation "Please refer to the `pbind' documentation."))

(defun pbind (&rest pairs)
  "pbind yields events determined by its PAIRS, which are a list of keys and values. Each key corresponds to a key in the resulting events, and each value is treated as a pattern that is evaluated for each step of the pattern to generate the value for its key.

Example:

;; (next-n (pbind :foo (pseq '(1 2 3)) :bar :hello) 4)
;;
;; ;=> ((EVENT :FOO 1 :BAR :HELLO) (EVENT :FOO 2 :BAR :HELLO) (EVENT :FOO 3 :BAR :HELLO) EOP)

See also: `pmono', `pb'"
  (assert (evenp (length pairs)) (pairs) "S's PAIRS argument must be a list of key/value pairs." 'pbind)
  (when (> (count :pdef (keys pairs)) 1)
    (warn "More than one :pdef key detected in pbind."))
  (let* ((res-pairs nil)
         (pattern-chain nil)
         (pattern (make-instance 'pbind)))
    (doplist (key value pairs)
      (when (pattern-p value)
        (setf (slot-value value 'parent) pattern))
      (cond ((position key *pbind-special-init-keys*)
             (when-let ((result (funcall (getf *pbind-special-init-keys* key) value pattern)))
               (appendf res-pairs result)))
            ((position key *pbind-special-wrap-keys*)
             (unless (null res-pairs)
               (setf (slot-value pattern 'pairs) res-pairs)
               (setf res-pairs nil))
             (unless (null pattern-chain)
               (setf pattern (apply #'pchain (append pattern-chain (list pattern))))
               (setf pattern-chain nil))
             (setf pattern (funcall (getf *pbind-special-wrap-keys* key) value pattern)))
            (t
             (unless (typep pattern 'pbind)
               (appendf pattern-chain (list pattern))
               (setf pattern (make-instance 'pbind)))
             (appendf res-pairs (list key (if (and (eql key :embed)
                                                   (typep value 'symbol))
                                              (pdef value)
                                              value))))))
    (unless (null res-pairs)
      (setf (slot-value pattern 'pairs) res-pairs))
    (appendf pattern-chain (list pattern))
    (unless (length= 1 pattern-chain)
      (setf pattern (apply #'pchain pattern-chain)))
    ;; process quant keys.
    (doplist (k v pairs)
      (when (member k (list :quant :play-quant :end-quant))
        (funcall (fdefinition (list 'setf (ensure-symbol k 'cl-patterns))) (next v) pattern)))
    ;; process :pdef key.
    (when-let ((pdef-name (getf pairs :pdef)))
      (pdef pdef-name pattern))
    pattern))

(pushnew 'pbind *patterns*)

(setf (documentation 'pbind 'type) (documentation 'pbind 'function))

(defmethod print-object ((pbind pbind) stream)
  (format stream "(S誉ю忾钿箪雉鲠祯疴轭ю衢蝮┅ㄤ彐礤翳镤ヰ狒翦蝾汨殪潋孱è疴轭疴轭洎磲疸犷灬礅溽箪雉戾è箪雉钺礤ㄣ祜箦颦盹鸷箪雉溴骈铋糸镱钺礤箪雉┅ㄣ镳扉篝ㄥ铙躜瀛扉篝ㄩㄥ耢箪雉钺礤ю衢蝮祜镳烘矧雯猴箪雉鲠祯疴轭箪雉钺礤衡＇沅潋恒镬戾泗雯箪雉鲠祯疴轭箪雉钺礤┅┅┅ㄣ祜箦颦盹鸷沆狍蟓溟蝈泗箪雉ㄦ轭洵沆狍ю忾钿┅┅ㄤ彐礤翳镤脲è疴轭疴轭洎脲箪雉鲠祯疴轭ю衢蝮┅ㄤ彐鲠疳趑弪瞽骢钽糸镱趄犷箪狒轱铙铋⒃桢扉篝镦钺礤镦骢钽糸镱犷翳骘蝽翳妁鏖祆忮趄犷箪狒邃麸轭囵猝犷雉桢疳趑弪磲泸矬渝犰箫噤彐轭瀛疳趑弪瞽骢钽糸镱趄犷箪狒轱瞌ㄤ彐磲泸溴骈铄疳趑弪瞽骢钽糸镱趄犷箪狒轱ㄦ躅泗轱疳趑弪瞟⒛彐轭趄犷箪狒轱骝镯普蚊陨衔麸辛栽乓轭囵猝啜箦翩ㄧ弭疳趑弪瞽骢钽糸镱趄犷箪狒轱铙К骢钽糸镱К疳趑弪瞟ㄤ彐轭瀛疳趑弪瞽骢钽糸镱趄犷箪狒轱皤ㄤ彐轭瀛疳趑弪瞽骢钽糸镱趄犷箪狒轱瓠ㄤ彐轭瀛疳趑弪瞽骢钽糸镱趄犷箪狒轱皙ㄤ彐轭瀛疳趑弪瞽骢钽糸镱趄犷箪狒轱鸠ㄤ彐轭瀛疳趑弪瞽骢钽糸镱趄犷箪狒轱蝻躅痤狎蝻躅洎ㄤ彐躅疳趑弪瞽趄犷箪狒瀛箦箦皓⒃蜥铙灬翦优匦麸翳羼蹰鲠戾铘疳趑弪狍疱嗒疳趑弪瞽骢钽糸镱趄犷箪狒轱铙К矧疳篌轸翳蝻蹒躅汨犷珏殒翳弪轶铒趄犷箪狒轱町渝犰箫囵猸趄犷箪狒瀛怙澌骢钽糸镱螫豉疱汜箦箦铛祆箦皓ㄡ麸箦皓扉篝戾舄è骈蝮ㄣ狎箦皓蝈篝ㄣ潋箦皓趄犷箪狒邃ㄧ弭疳趑弪瞽骢钽糸镱趄犷箪狒轱铙骈蝮舂ㄨ遽扉篝ㄩㄦ轭洵殒ㄦ豉疱Ж矧疳趑弪扉篝┅蝈篝矧趄犷箪狒邃骈蝮舂骈蝮舂┅啜黎遽括殒趄犷箪狒邃磲疸狎＇疳趑弪瞽趄犷箪狒瀛箦蝈篝蝈篝┅┅┅ㄤ彐躅疴趄犷箪狒瀛怙澌骢钽糸镱ㄢ镤⒃蜥铙灬翦骢钽糸镱轭孪馁麸翳彘羼蹰鲠戾铘疳趑弪狍疱嗒疳趑弪瞽骢钽糸镱趄犷箪狒轱铙М渝犰箫囵狒翦蝾趄犷箪狒瀛箦皈祜镳烘矧雯猴怙澌衡＇沅潋恒镬戾泗恒镬戾泗疳趑弪瞽趄犷箪狒瀛箦雯┅换粕睾犰祜脲麸忮扉篝蟋轭麒殂汜箦蝈篚祠狎溴篝蝓泗躜邃楫瀹疴衡灬扉篝烘镲衡狎疸沆弩ㄡ薄穿┅蝈篚祠轭骘躜ㄅ峙卧浩舷耗找悲穿ㄤ彐磲泸疴钺礤怙澌疳轵螬疴轶泔铞孱殄钽磲泸铿黩狃痖铉翳骢钽糸镱犰轸镦囵忾钿犷囵溴妲麒殪犰箫痱秭殇轭徜溟糸镱犰簌铘狲篚玑虍瘟团轶翳钺礤镦翳疳趑弪筢礤狍疴轭洄吼溴脲矧囵溴妲轸箦戽┈犷辛梢轶翳筢礤狍轭蝈珲灬疴轭洚涉辛梢轶镱禊镱屐屙孱衄疴镳弪狒弩扉脲囵溴妲雉桢蝼轶轸镳弪狒弩扉脲囵忾钿М澡屮痱弩箝镱轭辛梢狎犰箫狨麸磲糸汜祆趄犷箪狒邃麸羼蹰鲠戾铘疳趑弪铙殒狃痨殂徕戾骘屮犴痨搴换疴烘镲衡狎ǐ痼弪殄螬痼羼扉篝暴┅轶翳筢礤狍换疴烘镲衡狎皤痼弪殄螬痼羼扉篝暴┅渝犰箫囵忾钿К囵溴妲ㄩ戾铉翳疳轵螬啜痄彐钺礤鲤衢蝮啜痄彐钺礤疴轭括疴趄犷箪狒瀛怙澌骢钽糸镱疳轵螬┅┅瘐箬铄ю疳趑弪铙ㄤ彐沆狍疴轭洵痼趄遽疴轭痼趄遽愆īê滹沲礤铘狒轱痼趄遽骘囵忾钿Б┅ㄤ彐礤翳镤痱轭舡镡赍泗è疴轭疴轭洵痼趄遽愆篝蝈犴痱轭舡躅蝈徜徕戾镡赍泗疴轭篝蝈犴呼疱舂ㄦ矧磲篝蝈犴誉" (slot-value pbind 'pairs))))

(defmethod as-pstream ((pbind pbind))
  (let ((name (class-name (class-of pbind)))
        (slots (mapcar #'closer-mop:slot-definition-name (closer-mop:class-slots (class-of pbind)))))
    (apply #'make-instance
           (pattern-pstream-class-name name)
           (loop :for slot :in slots
                 :for slot-kw := (make-keyword slot)
                 :for bound := (slot-boundp pbind slot)
                 :if bound
                   :collect slot-kw
                 :if (eql :pairs slot-kw)
                   :collect (mapcar 'pattern-as-pstream (slot-value pbind 'pairs))
                 :if (and bound (not (eql :pairs slot-kw)))
                   :collect (slot-value pbind slot)))))

(defmacro define-pbind-special-init-key (key &body body)
  "Define a special key for pbind that alters the pbind during its initialization, either by embedding a plist into its pattern-pairs or in another way. These functions are called once, when the pbind is created, and must return a plist if the key should embed values into the pbind pairs, or NIL if it should not."
  `(setf (getf *pbind-special-init-keys* ,(make-keyword key))
         (lambda (value pattern)
           (declare (ignorable value pattern))
           ,@body)))

;; (define-pbind-special-init-key inst ; FIX: this should be part of event so it will affect the event as well. maybe just rename to something else?
;;   (list :instrument value))

(define-pbind-special-init-key loop-p
  (setf (loop-p pattern) value)
  nil)

(defmacro define-pbind-special-wrap-key (key &body body)
  "Define a special key for pbind that replaces the pbind with another pattern during the pbind's initialization. Each encapsulation key is run once on the pbind after it has been initialized, altering the type of pattern returned if the return value of the function is non-NIL."
  `(setf (getf *pbind-special-wrap-keys* ,(make-keyword key))
         (lambda (value pattern)
           (declare (ignorable value pattern))
           ,@body)))

(define-pbind-special-wrap-key pfor
  (pfor pattern value))

(define-pbind-special-wrap-key parp ; deprecated
  (parp pattern value))

(define-pbind-special-wrap-key pfin
  (pfin pattern value))

(define-pbind-special-wrap-key pfindur
  (pfindur pattern value))

(define-pbind-special-wrap-key psync
  (destructuring-bind (quant &optional maxdur) (ensure-list value)
    (psync pattern quant (or maxdur quant))))

(define-pbind-special-wrap-key pdurstutter
  (pdurstutter pattern value))

(define-pbind-special-wrap-key pr
  (pr pattern value))

(define-pbind-special-wrap-key pn
  (pn pattern value))

(define-pbind-special-wrap-key ptrace
  (if value
      (if (eql t value)
          (ptrace pattern)
          (pchain pattern
                  (pbind :- (ptrace value))))
      pattern))

(define-pbind-special-wrap-key pmeta
  (if (eql t value)
      (pmeta pattern)
      pattern))

(define-pbind-special-wrap-key pchain ; basically the same as the :embed key, but we have it anyway for convenience.
  (pchain pattern value))

(define-pbind-special-wrap-key pparchain
  (pparchain pattern value))

(defmacro define-pbind-special-process-key (key &body body)
  "Define a special key for pbind that alters the pattern in a nonstandard way. These functions are called for each event created by the pbind and must return an event if the key should embed values into the event stream, or `eop' if the pstream should end."
  `(setf (getf *pbind-special-process-keys* ,(make-keyword key))
         (lambda (value)
           ,@body)))

(define-pbind-special-process-key embed
  value)

(defmethod next ((pbind pbind-pstream))
  (labels ((accumulator (pairs)
             (let ((key (car pairs))
                   (val (cadr pairs)))
               (when (and (pstream-p val)
                          (null (slot-value val 'start-beat)))
                 (setf (slot-value val 'start-beat) (beat pbind)))
               (let ((next-val (next val)))
                 (when (eop-p next-val)
                   (return-from accumulator eop))
                 (if (position key (keys *pbind-special-process-keys*))
                     (setf *event* (combine-events *event*
                                                   (funcall (getf *pbind-special-process-keys* key) next-val)))
                     (setf (event-value *event* key) next-val))
                 (if-let ((cddr (cddr pairs)))
                   (accumulator cddr)
                   *event*)))))
    (let ((*event* (make-default-event)))
      (when (eop-p *event*)
        (return-from next eop))
      (setf (slot-value *event* '%beat) (+ (or (slot-value pbind 'start-beat) 0) (beat pbind)))
      (if-let ((pairs (slot-value pbind 'pairs)))
        (accumulator pairs)
        *event*))))

(defmethod as-pstream ((pbind pbind-pstream))
  pbind)

;;; prest
;; FIX: allow `prest' to be used as an event on its own (it should parse as (event :type :rest :dur VALUE))

(defclass prest ()
  ((value :initarg :value :initform 1))
  (:documentation "An object representing a rest. When set as a value in an event, the event's :type becomes :rest and the prest's value slot is used as the actual value for the event key instead."))

(defun prest (&optional (value 1))
  "Make a prest object, which, when used in a `pbind' or similar event pattern, turns the current event into a rest and yields VALUE for the key's value.

Note that this is not a pattern; it is just a regular function that returns a prest object.

Example:

;; (next-upto-n (pbind :degree (pseq (list 0 1 (prest 2) 3) 1)))
;; ;=> ((EVENT :DEGREE 0) (EVENT :DEGREE 1) (EVENT :TYPE :REST :DEGREE 2) (EVENT :DEGREE 3))

See also: `pbind', `pbind''s :type key"
  (make-instance 'prest :value value))

(defmethod print-object ((prest prest) stream)
  (format stream "(S S)" 'prest (slot-value prest 'value)))

(defmethod rest-p ((prest prest))
  t)

;;; pmono

(defun pmono (instrument &rest pairs)
  "pmono defines a mono instrument event pstream. It's effectively the same as `pbind' with its :type key set to :mono.

See also: `pbind'"
  (assert (evenp (length pairs)) (pairs) "S's PAIRS argument must be a list of key/value pairs." 'pmono)
  (apply #'pbind
         :instrument instrument
         :type :mono
         pairs))

(pushnew 'pmono *patterns*)

;;; pseq

(defpattern pseq (pattern)
  (list
   (repeats :initform *default-pattern-repeats*)
   (offset :initform 0)
   (current-repeats-remaining :state t))
  :documentation "Sequentially yield items from LIST, repeating the whole list REPEATS times. OFFSET is the offset to index into the list.

Example:

;; (next-n (pseq '(5 6 7) 2) 7)
;; ;=> (5 6 7 5 6 7 EOP)
;;
;; (next-upto-n (pseq '(5 6 7) 2 1))
;; ;=> (6 7 5 6 7 5)

See also: `pser', `eseq'")

(defmethod next ((pseq pseq-pstream))
  (with-slots (number list offset) pseq
    (let ((list (next list)))
      (when (and (plusp number)
                 (zerop (mod number (length list))))
        (decf-remaining pseq))
      (let ((off (next offset)))
        (if (and (not (eop-p off))
                 (remaining-p pseq)
                 list)
            (elt-wrap list (+ off number))
            eop)))))

;;; pser

(defpattern pser (pattern)
  (list
   (length :initform *default-pattern-length*)
   (offset :initform 0)
   (current-repeats-remaining :state t)
   (current-index :state t))
  :documentation "Sequentially yield values from LIST, yielding a total of LENGTH values.

Example:

;; (next-n (pser '(5 6 7) 2) 3)
;;
;; ;=> (5 6 EOP)

See also: `pseq'")

(defmethod next ((pser pser-pstream))
  (with-slots (list offset current-index) pser
    (let ((remaining (remaining-p pser 'length))
          (list (next list))
          (off (next offset)))
      (when (or (not remaining)
                (eop-p off))
        (return-from next eop))
      (decf-remaining pser)
      (when (eql :reset remaining)
        (setf current-index 0))
      (prog1
          (elt-wrap list (+ off current-index))
        (incf current-index)))))

;;; pk

(defpattern pk (pattern)
  (key
   (default :initform 1))
  :documentation "Yield the value of KEY in the current `*event*' context, returning DEFAULT if that value is nil.

Example:

;; (next-upto-n (pbind :foo (pseq '(1 2 3) 1) :bar (pk :foo)))
;; ;=> ((EVENT :FOO 1 :BAR 1) (EVENT :FOO 2 :BAR 2) (EVENT :FOO 3 :BAR 3))

See also: `pbind', `event-value', `*event*'")

(defmethod as-pstream ((pk pk))
  (with-slots (key default) pk
    (make-instance 'pk-pstream
                   :key key
                   :default default)))

(defmethod next ((pk pk-pstream))
  (with-slots (key default) pk
    (or (event-value *event* key)
        (if (string= :number key)
            (slot-value pk 'number)
            default))))

;;; prand

(defpattern prand (pattern)
  (list
   (length :initform *default-pattern-length*)
   (current-repeats-remaining :state t))
  :documentation "Yield random values from LIST.

Example:

;; (next-n (prand '(1 2 3) 5) 6)
;; ;=> (3 2 2 1 1 EOP)

See also: `pxrand', `pwrand', `pwxrand'")

(defmethod next ((prand prand-pstream))
  (unless (remaining-p prand 'length)
    (return-from next eop))
  (decf-remaining prand)
  (random-elt (next (slot-value prand 'list))))

;;; pxrand

(defpattern pxrand (pattern)
  (list
   (length :initform *default-pattern-length*)
   (last-result :state t)
   (current-repeats-remaining :state t))
  :documentation "Yield random values from LIST, never repeating equal values twice in a row.

Example:

;; (next-upto-n (pxrand '(1 2 3) 4))
;; ;=> (3 1 2 1)

See also: `prand', `pwrand', `pwxrand'")

(defmethod initialize-instance :after ((pxrand pxrand) &key)
  (with-slots (list) pxrand
    (assert (or (not (listp list))
                (position-if-not (lambda (i) (eql i (car list))) list))
            (list)
            "S's input list must have at least two non-eql elements." 'pxrand)))

(defmethod next ((pxrand pxrand-pstream))
  (with-slots (list last-result) pxrand
    (unless (remaining-p pxrand 'length)
      (return-from next eop))
    (decf-remaining pxrand)
    (let ((clist (next list)))
      (setf last-result (loop :for res := (random-elt clist)
                              :if (or (not (slot-boundp pxrand 'last-result))
                                      (not (eql res last-result)))
                                :return res)))))

;;; pwrand

(defpattern pwrand (pattern)
  (list
   (weights :initform :equal)
   (length :initform *default-pattern-length*)
   (current-repeats-remaining :state t))
  :documentation "Yield random elements from LIST weighted by respective values from WEIGHTS.

Example:

;; (next-upto-n (pwrand '(1 2 3) '(7 5 3) 10))
;; ;=> (1 1 2 2 2 1 2 1 1 3)

See also: `prand', `pxrand', `pwxrand'")

(defmethod next ((pwrand pwrand-pstream))
  (with-slots (list weights) pwrand
    (unless (remaining-p pwrand 'length)
      (return-from next eop))
    (decf-remaining pwrand)
    (let* ((clist (next list))
           (cweights (cumulative-list (if (eql weights :equal)
                                          (let ((len (length clist)))
                                            (make-list len :initial-element (/ 1 len)))
                                          (normalized-sum (mapcar #'next (next weights))))))
           (num (random 1.0)))
      (nth (index-of-greater-than num cweights) clist))))

;;; pwxrand

(defpattern pwxrand (pattern)
  (list
   (weights :initform :equal)
   (length :initform *default-pattern-length*)
   (last-result :state t)
   (current-repeats-remaining :state t))
  :documentation "Yield random elements from LIST weighted by respective values from WEIGHTS, never repeating equivalent values twice in a row. This is effectively `pxrand' and `pwrand' combined.

Example:

;; (next-upto-n (pwxrand '(1 2 3) '(7 5 3) 10))
;; ;=> (1 2 1 2 1 3 1 2 1 2)

See also: `prand', `pxrand', `pwrand'")

(defmethod initialize-instance :after ((pwxrand pwxrand) &key)
  (with-slots (list weights) pwxrand
    (assert (or (not (listp list))
                (and (position-if-not (fn (eql _ (car list))) list)
                     (or (not (listp weights))
                         (find-if-not 'numberp weights)
                         (let ((effective-list (loop :for index :from 0
                                                     :for elem :in list
                                                     :for weight := (elt-wrap weights index)
                                                     :if (plusp weight)
                                                       :collect elem)))
                           (position-if-not (fn (eql _ (car effective-list))) effective-list)))))
            (list)
            "S's input list must have at least two non-eql accessible elements." 'pwxrand)))

(defmethod next ((pwxrand pwxrand-pstream))
  (with-slots (list weights last-result) pwxrand
    (unless (remaining-p pwxrand 'length)
      (return-from next eop))
    (decf-remaining pwxrand)
    (let* ((clist (next list))
           (clist-length (length clist))
           (cweights (next weights))
           (cweights (cumulative-list (if (eql cweights :equal)
                                          (make-list clist-length :initial-element (/ 1 clist-length))
                                          (normalized-sum (mapcar #'next cweights))))))
      (setf last-result (loop :for res := (nth-wrap (index-of-greater-than (random 1.0) cweights) clist)
                              :if (or (not (slot-boundp pwxrand 'last-result))
                                      (not (eql res last-result)))
                                :return res)))))

;;; pfunc

(defpattern pfunc (pattern)
  ((func :type (or function-designator pattern))
   (length :initform *default-pattern-length*)
   (current-repeats-remaining :state t))
  :documentation "Yield the result of evaluating FUNC. Note that the current event of the parent pattern is still accessible via the `*event*' special variable.

Example:

;; (next-upto-n (pfunc (lambda () (random 10)) 4))
;; ;=> ((5 2 8 9))
;;
;; (next-upto-n (pbind :foo (pwhite 0 10 4)
;;                     :bar (pfunc (lambda ()
;;                                   (if (> (event-value *event* :foo) 5) :greater :lesser)))))
;; ;=> ((EVENT :FOO 0 :BAR :LESSER) (EVENT :FOO 6 :BAR :GREATER)
;;      (EVENT :FOO 7 :BAR :GREATER) (EVENT :FOO 8 :BAR :GREATER))

See also: `pf', `pnary', `plazy', `pif'")

(defmethod initialize-instance :after ((pfunc pfunc) &key)
  (check-type (slot-value pfunc 'func) (or function-designator pattern)))

(defmethod as-pstream ((pfunc pfunc))
  (with-slots (func length) pfunc
    (make-instance 'pfunc-pstream
                   :func (pattern-as-pstream func)
                   :length (as-pstream length))))

(defmethod next ((pfunc pfunc-pstream))
  (unless (remaining-p pfunc 'length)
    (return-from next eop))
  (decf-remaining pfunc)
  (with-slots (func) pfunc
    (etypecase func
      (function-designator (funcall func))
      (pstream (let ((nxt (next func)))
                 (if (eop-p nxt)
                     eop
                     (funcall nxt)))))))

;;; pf

(defmacro pf (&body body)
  "Convenience macro for `pfunc' that automatically wraps BODY in a lambda."
  `(pfunc (lambda () ,@body)))

(pushnew 'pf *patterns*)

;;; pr

(defpattern pr (pattern)
  (pattern
   (repeats :initform *default-pattern-repeats*)
   (current-value :state t :initform nil)
   (current-repeats-remaining :state t))
  :documentation "Repeat each value from PATTERN REPEATS times. If REPEATS is 0, the value is skipped.

Example:

;; (next-upto-n (pr (pseries) (pseq '(1 3 0 2) 1)))
;; ;=> (0 1 1 1 3 3)

See also: `pdurstutter', `pn', `pdrop', `pfor'")

(defmethod as-pstream ((pr pr))
  (with-slots (pattern repeats) pr
    (make-instance 'pr-pstream
                   :pattern (as-pstream pattern)
                   :repeats (pattern-as-pstream repeats))))

(defmethod next ((pr pr-pstream))
  (with-slots (pattern repeats current-value current-repeats-remaining) pr
    (while (or (not (slot-boundp pr 'current-repeats-remaining))
               (and current-repeats-remaining
                    current-value
                    (not (value-remaining-p current-repeats-remaining))))
      (setf current-value (next pattern))
      (when (or (eop-p current-value)
                (and (slot-boundp pr 'current-repeats-remaining)
                     (eop-p current-repeats-remaining)))
        (return-from next eop))
      (setf current-repeats-remaining
            (let ((*event* (if (event-p current-value)
                               (if *event*
                                   (combine-events *event* current-value)
                                   current-value)
                               *event*)))
              (if (typep repeats 'function)
                  (let ((arglist (function-arglist repeats)))
                    (if (null arglist)
                        (funcall repeats)
                        (funcall repeats current-value)))
                  (next repeats)))))
    (when (value-remaining-p current-repeats-remaining)
      (decf-remaining pr)
      current-value)))

;;; plazy

(defpattern plazy (pattern)
  (func
   (repeats :initform *default-pattern-repeats*)
   (current-pstream :state t :initform nil)
   (current-repeats-remaining :state t :initform nil))
  :documentation "Evaluates FUNC to generate a pattern, which is then yielded from until its end, at which point FUNC is evaluated again to generate the next pattern. The pattern is generated a total of REPEATS times.

Example:

;; (next-n (plazy (lambda () (if (= 0 (random 2)) (pseq '(1 2 3) 1) (pseq '(9 8 7) 1)))) 6)
;; ;=> (9 8 7 1 2 3)

See also: `pfunc'")

(defmethod as-pstream ((plazy plazy))
  (with-slots (func repeats) plazy
    (make-instance 'plazy-pstream
                   :func func
                   :repeats (as-pstream repeats))))

(defmethod next ((plazy plazy-pstream))
  (with-slots (func repeats current-pstream current-repeats-remaining) plazy
    (labels ((set-current-pstream ()
               (unless (remaining-p plazy)
                 (return-from next eop))
               (setf current-pstream (as-pstream (funcall func)))
               (decf-remaining plazy)))
      (unless current-repeats-remaining
        (setf current-repeats-remaining (next repeats)))
      (unless current-pstream
        (set-current-pstream))
      (let ((nv (next current-pstream)))
        (if (eop-p nv)
            (progn
              (set-current-pstream)
              (next current-pstream))
            nv)))))

;;; protate

(defpattern protate (pattern)
  (pattern
   (shift :initform 0))
  :documentation "Rotate PATTERN N outputs forward or backward, wrapping the shifted items to the other side, a la `alexandria:rotate'.

Example:

;; (next-upto-n (protate (pseq '(1 2 3 4 5) 1) 2))
;; ;=> (4 5 1 2 3)

See also: `pdrop', `phistory', `pscratch'")

(defmethod as-pstream ((protate protate))
  (with-slots (pattern shift) protate
    (make-instance 'protate-pstream
                   :pattern (pattern-as-pstream pattern)
                   :shift (pattern-as-pstream shift))))

(defmethod next ((protate protate-pstream))
  (with-slots (pattern shift number) protate
    (when (zerop number)
      (next-upto-n pattern))
    (let ((actual-index (- number (next shift)))
          (hn (slot-value pattern 'history-number)))
      (if (>= number (1- hn))
          eop
          (pstream-elt pattern (mod actual-index (1- hn)))))))

;;; pn

(defpattern pn (pattern)
  (pattern
   (repeats :initform *default-pattern-repeats*)
   (current-repeats-remaining :state t)
   (current-pstream :state t :initform nil))
  :documentation "Embed the full PATTERN into the pstream REPEATS times.

Example:

;; (next-upto-n (pn (pwhite 0 5 1) 5))
;; ;=> (2 4 2 1 0)

See also: `pr'")

(defmethod as-pstream ((pn pn)) ; need this so that PATTERN won't be automatically converted to a pstream when the pn is.
  (with-slots (pattern repeats) pn
    (make-instance 'pn-pstream
                   :pattern pattern
                   :repeats (as-pstream repeats))))

(defmethod next ((pn pn-pstream))
  (with-slots (pattern current-pstream) pn
    (let ((rem (remaining-p pn)))
      (when (eql :reset rem)
        (setf current-pstream (as-pstream pattern)))
      (let ((nv (next current-pstream)))
        (while (and (eop-p nv) rem)
          (decf-remaining pn)
          (setf rem (remaining-p pn)
                current-pstream (as-pstream pattern)
                nv (next current-pstream)))
        (if rem
            nv
            eop)))))

;;; pshuf

(defpattern pshuf (pattern)
  (list
   (repeats :initform *default-pattern-repeats*)
   (shuffled-list :state t)
   (current-repeats-remaining :state t))
  :documentation "Shuffle LIST, then yield each item from the shuffled list, repeating it REPEATS times.

Example:

;; (next-upto-n (pshuf '(1 2 3) 2))
;;
;; ;=> (3 1 2 3 1 2)

See also: `prand'")

(defmethod as-pstream ((pshuf pshuf))
  (with-slots (list repeats) pshuf
    (let ((list (typecase list
                  (pattern (next-upto-n list))
                  (function (funcall list))
                  (list list))))
      (make-instance 'pshuf-pstream
                     :list (next list)
                     :repeats (as-pstream repeats)))))

(defmethod next ((pshuf pshuf-pstream))
  (with-slots (list number shuffled-list) pshuf
    (when (and (zerop (mod number (length list)))
               (plusp number))
      (decf-remaining pshuf))
    (let ((rem (remaining-p pshuf)))
      (unless rem
        (return-from next eop))
      (when (eql :reset rem)
        (setf shuffled-list (shuffle (copy-list list)))) ; alexandria:shuffle destructively modifies the list, so we use copy-list so as to avoid unexpected side effects.
      (nth (mod number (length shuffled-list))
           shuffled-list))))

;;; pwhite

(defpattern pwhite (pattern)
  ((lo :initform 0.0)
   (hi :initform 1.0)
   (length :initform *default-pattern-length*)
   (current-repeats-remaining :state t))
  :documentation "Linearly-distributed random numbers between LO and HI, inclusive.

Example:

;; (next-upto-n (pwhite 0 10 16))
;; ;=> (7 2 4 5 7 10 4 8 10 2 3 5 9 2 5 4)

See also: `pexprand', `pbrown', `pgauss', `prand'"
  ;; FIX: see about using a symbol for the unprovided slots and just check/process this initialization code in the `next' method instead.
  :defun (defun pwhite (&optional (lo 0.0 lo-provided-p) (hi 1.0 hi-provided-p) (length *default-pattern-length*))
           ;; if only one argument is provided, we use it as the "hi" value.
           (make-instance 'pwhite
                          :lo (if hi-provided-p
                                  lo
                                  0.0)
                          :hi (if hi-provided-p
                                  hi
                                  (if lo-provided-p
                                      lo
                                      1.0))
                          :length length)))

(defmethod next ((pwhite pwhite-pstream))
  (with-slots (lo hi) pwhite
    (unless (remaining-p pwhite 'length)
      (return-from next eop))
    (decf-remaining pwhite)
    (let ((nlo (next lo))
          (nhi (next hi)))
      (when (or (eop-p nlo)
                (eop-p nhi))
        (return-from next eop))
      (random-range nlo nhi))))

;;; pbrown

;; FIX: make the initforms of hi onward a special symbol, and then check for that symbol in `next', rather than checking against the arguments of the `pbrown' function.
(defpattern pbrown (pattern)
  ((lo :initform 0.0)
   (hi :initform 1.0)
   (step :initform 0.125)
   (length :initform *default-pattern-length*)
   (current-repeats-remaining :state t)
   (current-value :state t :initform nil))
  :documentation "Brownian motion within a range; each output randomly a maximum of STEP away from the previous. LO and HI define the lower and upper bounds of the range. STEP defaults to 1 if LO and HI are integers.

Example:

;; (next-upto-n (pbrown 0 10 1 10))
;; ;=> (2 3 3 3 4 3 4 5 6 5)

See also: `pwhite', `pexprand', `pgauss'"
  ;; if only one argument is provided, we use it as the "hi" value.
  :defun (defun pbrown (&optional (lo 0.0 lo-provided-p) (hi 1.0 hi-provided-p) (step 0.125 step-provided-p) (length *default-pattern-length*))
           (let ((lo (if hi-provided-p
                         lo
                         (if (integerp lo) 0 0.0)))
                 (hi (if hi-provided-p
                         hi
                         (if lo-provided-p lo 1))))
             (make-instance 'pbrown
                            :lo lo
                            :hi hi
                            :step (if step-provided-p
                                      step
                                      (if (and (integerp lo)
                                               (integerp hi))
                                          1
                                          0.125))
                            :length length))))

(defmethod next ((pbrown pbrown-pstream))
  (unless (remaining-p pbrown 'length)
    (return-from next eop))
  (decf-remaining pbrown)
  (with-slots (lo hi step current-value) pbrown
    (let ((nlo (next lo))
          (nhi (next hi))
          (nstep (next step)))
      (when (member eop (list nlo nhi nstep))
        (return-from next eop))
      (unless current-value
        (setf current-value (random-range (min nlo nhi)
                                          (max nlo nhi))))
      (incf current-value (random-range (* -1 nstep) nstep))
      (setf current-value (clamp current-value nlo nhi)))))

;;; pexprand
;; FIX: integer inputs should result in integer outputs

(defpattern pexprand (pattern)
  ((lo :initform 0.0001)
   (hi :initform 1.0)
   (length :initform *default-pattern-length*)
   (current-repeats-remaining :state t))
  :documentation "Exponentially-distributed random numbers between LO and HI. Note that LO and HI cannot be 0, and that LO and HI must have the same sign or else complex numbers will be output.

Example:

;; (next-upto-n (pexprand 1.0 8.0 4))
;; ;=> (1.0420843091865208d0 1.9340168112124456d0 2.173209129035095d0 4.501371557329618d0)

See also: `pwhite', `pbrown', `pgauss', `prand'")

(defmethod next ((pexprand pexprand-pstream))
  (with-slots (lo hi) pexprand
    (unless (remaining-p pexprand 'length)
      (return-from next eop))
    (decf-remaining pexprand)
    (let ((nlo (next lo))
          (nhi (next hi)))
      (assert (and (numberp lo)
                   (not (zerop lo)))
              (lo)
              "Got a zero for S's S slot" 'pexprand 'lo)
      (assert (and (numberp hi)
                   (not (zerop hi)))
              (hi)
              "Got a zero for S's S slot" 'pexprand 'hi)
      (when (or (eop-p nlo)
                (eop-p nhi))
        (return-from next eop))
      (exponential-random-range nlo nhi))))

;;; pgauss

(defpattern pgauss (pattern)
  ((mean :initform 0.0)
   (deviation :initform 1.0)
   (length :initform *default-pattern-length*)
   (current-repeats-remaining :state t))
  :documentation "Random numbers distributed along a normal (gaussian) curve. MEAN is the \"center\" of the distribution, DEVIATION is the standard deviation (i.e. the higher the value, the further the outputs are spread from MEAN).

Example:

;; (next-n (pgauss) 4)
;; ;=> (0.08918811646370092d0 0.1745957067161632d0 0.7954678768273173d0 -1.2215823449671597d0)

See also: `pwhite', `pexprand', `pbrown'")

(defmethod next ((pgauss pgauss-pstream))
  (with-slots (mean deviation) pgauss
    (unless (remaining-p pgauss 'length)
      (return-from next eop))
    (decf-remaining pgauss)
    (let ((nmean (next mean))
          (ndev (next deviation)))
      (when (or (eop-p nmean)
                (eop-p ndev))
        (return-from next eop))
      (random-gauss nmean ndev))))

;;; pseries

(defpattern pseries (pattern)
  ((start :initform 0)
   (step :initform 1)
   (length :initform *default-pattern-length*)
   (current-repeats-remaining :state t)
   (current-value :state t))
  :documentation "Yield START, then generate subsequent outputs by adding STEP, for a total of LENGTH outputs.

Example:

;; (next-upto-n (pseries 1 2 4))
;; ;=> (1 3 5 7)

See also: `pseries*', `pgeom', `paccum'")

(defmethod next ((pseries pseries-pstream))
  (with-slots (start step current-value) pseries
    (unless (slot-boundp pseries 'current-value)
      (setf current-value (next start)))
    (unless (and (remaining-p pseries 'length)
                 current-value)
      (return-from next eop))
    (decf-remaining pseries)
    (let ((nxt (next step)))
      (prog1
          current-value
        (if (numberp nxt)
            (incf current-value nxt) ; FIX: current-value should be CURRENT value, not the next one! also write tests for this!
            (setf current-value eop))))))

;;; pseries*

(defun pseries* (&optional (start 0) (end 1) length)
  "Syntax sugar to generate a `pseries' whose values go from START to END linearly over LENGTH steps. If LENGTH is not provided, it is calculated such that the step will be 1. Note that LENGTH cannot be infinite since delta calculation requires dividing by it.

Based on the Pseries extension from the ddwPatterns SuperCollider library.

Example:

;; (pseries* 0 10 16)
;; ;=> (pseries 0 2/3 16)
;;
;; (next-upto-n *)
;; ;=> (0 2/3 4/3 2 8/3 10/3 4 14/3 16/3 6 20/3 22/3 8 26/3 28/3 10)

See also: `pseries', `pgeom', `pgeom*'"
  (check-type length (or null (integer 2) pattern))
  (let ((length (or length
                    (max 2 (round (1+ (abs (- end start))))))))
    (pseries start (/ (- end start) (1- length)) length)))

(pushnew 'pseries* *patterns*)

;;; pgeom

(defpattern pgeom (pattern)
  ((start :initform 1)
   (grow :initform 2)
   (length :initform *default-pattern-length*)
   (current-repeats-remaining :state t)
   (current-value :state t))
  :documentation "Yield START, then generate subsequent outputs by multiplying by GROW, for a total of LENGTH outputs.

Example:

;; (next-upto-n (pgeom 1 2 4))
;; ;=> (1 2 4 8)

See also: `pseries', `paccum'")

(defmethod next ((pgeom pgeom-pstream))
  (with-slots (start grow current-value) pgeom
    (unless (slot-boundp pgeom 'current-value)
      (setf current-value (next start)))
    (unless (remaining-p pgeom 'length)
      (return-from next eop))
    (decf-remaining pgeom)
    (if (zerop (slot-value pgeom 'number))
        current-value
        (let ((n (next grow)))
          (if (eop-p n)
              eop
              (setf current-value (* current-value n)))))))

;;; pgeom*

(defun pgeom* (&optional (start 0.01) (end 1) (length 16))
  "Syntax sugar to generate a `pgeom' whose values go from START to END exponentially over LENGTH steps. LENGTH cannot be infinite since delta calculation requires dividing by it.

Based on the Pgeom extension from the ddwPatterns SuperCollider library.

Example:

;; (pgeom* 1 100 8)
;; ;=> (pgeom 1 1.9306977 8)
;;
;; (next-upto-n *)
;; ;=> (1 1.9306977 3.7275934 7.196856 13.894953 26.826954 51.79474 99.999985)
;; ;; Note that due to floating point rounding errors the last output may not always be exactly END.

See also: `pgeom', `pseries', `pseries*'"
  (check-type length (or (integer 2) pattern))
  (pgeom start (expt (/ end start) (/ 1 (1- length))) length))

(pushnew 'pgeom* *patterns*)

;;; ptrace

(defpattern ptrace (pattern)
  ((trace :initform t)
   (prefix :initform nil)
   (stream :initform t))
  :documentation "Print the PREFIX and each output of TRACE to STREAM. If TRACE is t, print `*event*'. If TRACE is a different symbol, print the value of that symbol in `*event*'. Otherwise, ptrace yields TRACE unaffected.

See also: `debug-backend', `debug-backend-recent-events'")

(defmethod as-pstream ((ptrace ptrace))
  (with-slots (trace prefix stream) ptrace
    (make-instance 'ptrace-pstream
                   :trace (pattern-as-pstream trace)
                   :prefix (pattern-as-pstream prefix)
                   :stream (pattern-as-pstream stream))))

(defmethod next ((ptrace ptrace-pstream))
  (with-slots (trace prefix stream) ptrace
    (let ((prefix (next prefix))
          (stream (next stream)))
      (if (eql trace t)
          (progn
            (format stream "&@[A ]S%" prefix *event*)
            t)
          (typecase trace
            ((or list symbol)
             (progn
               (format stream "&@[A ]雍誉［夯蔺ア痱彐轼磲疸狎灬礅溽簌礅镬扉篝簌礅镬ㄥ鲥铘鲠祯弼孱舄簌礅镬┅ㄥ铙躜瀛扉篝趄徙濠┅舂雉桢蝼轶戾è蝈铄趄徙濠┅ㄦ矧磲篝蝈犴累蔺誉ア痱彐轼蝈螬蝈螬┅┅┅换痨徙ㄤ彐疳趑弪痨徙疳趑弪瞟扉篝蝈疱狒洪铋翩矧溴驷蹯舡疳趑弪瞽蝈疱狒螵ㄣ躜蝈铘蝈疱狒后翎翦洪铋翩矧癌ㄣ躜蝈铘蝈疱狒蟓蝈磲轭轭后翎翦舂轰镢蹴孱翎糸镱①殄熹遽汨鲠祯骝镯躺釉轭箦聃孱沐涉翳鲠祯轶扉篝翳骈蝮屐屙孱镦翳狒扉篝轶殄熹邃澡箦泔钿糸礤翳狒篚猸扉篝轶孱泔躅翦蝈洮轸箦泔钿屐屙孱鏖祆忮殄熹邃翳翳轵糸礤翳翳轵屐屙孱衄犷箫镱遗信猎泔铘蝻祗翳铛礅弪镦糸礤躺釉轶蝈疱狒邃砒犴痨搴换铄舡躔麸痨徙扉篝扉篝旦畅换唤ū旦渝犰箫囵疳綮徙濮ㄤ彐礤翳镤铄è痨徙痨徙瀛痼趄遽愆鏖翳箪雉铛礅弪扉篝沲蝌孱舡蝈疱狒痨徙麒孱ㄡ钿铒铛礅弪癌盹铛礅弪戾铉翳扉篝┅┅ㄩ钽沲蝌孱舡蝈疱狒ㄤ邈姝蝈磲轭轭痨徙濠躅戾篌ㄩ痨躞铛礅弪ㄡ钿铒ㄥ钿邃痨徙濠蝈磲轭轭绛痨徙濠蝈磲轭轭绛痨徙濠蝈趱蝾骝镯铄屣皓戾舄è盹盹铛礅弪戾铉翳扉篝┅蝈篚祠铄铘盹扉篝┅┅ㄩ扉篝蝈篚祠ㄥ祠黩狃蝈篚祠沲蝌孱舡蝈疱狒蝈篚祠┅┅换痧狒灬沐ㄤ彐疳趑弪痧狒灬沐疳趑弪瞟扉篝蝈疱狒洪铋翩矧溴驷蹯舡疳趑弪瞽蝈疱狒螵ㄣ躜蝈铘蝈疱狒蟓蝈磲轭轭后翎翦舂轰镢蹴孱翎糸镱①殄熹遽汨鲠祯骝镯躺釉轭箦聃孱沐矧镱秕麴豸骝镯遽汨疳趑弪轭躺釉疱泫沆镦翳扉篝涉镱镦翳疳趑弪铙屙忮滗邃轭躺釉孱潴箫镱弪翳犷翳雉桢蝮轸轶箝眇禊蝈盹鲥犷翳痧狒灬沐泔铘轭蹂麸殄熹骝镯翳蝈篝镦翳躺釉澡孱糸蝈躺釉轶殄熹邃翳蝻蹒麸翎镦遗信猎糸礤螽砒犴痨搴换铄舡躔麸痧狒灬沐扉篝痼羼扉篝畅暴换痼羼扉篝订博┅换唤ū订渝犰箫囵灬沐Бㄤ彐礤翳镤狍痼趄遽è痧狒灬沐痧狒灬沐┅鏖翳箪雉蝈疱狒扉篝痧狒灬沐磲脲轭篝犷沐ю疳綮徙瀛痼趄遽红轶磲疸狎＇疳趑弪瞽狍痼趄遽扉篝候屦遽趔ㄡ蟓痼趄遽蝈疱狒螬┅ㄤ彐礤翳镤铄è痧狒灬沐痧狒灬沐痼趄遽愆鏖翳箪雉铛礅弪扉篝痧狒灬沐麒孱ㄡ钿铒弪镳铛礅弪┅铒戾铉翳扉篝┅弪镳盹铛礅弪戾铉翳扉篝┅┅ㄤ邈姝蝈磲轭轭痧狒灬沐┅麒孱矧铒扉篝ㄡ钿痨躞铛礅弪ㄥ钿邃痧狒灬沐┅铒蝈磲轭轭绛痧狒灬沐┅蝈趱蝾骝镯铄屣皓戾è蝈篚祠铄ㄥ祠黩狃扉篝铛礅弪┅┅躅戾篌ㄥ镳蝈篚祠蝈趱蝾骝镯铄蝈篚祠┅箦翩扉篝蝈盹鲥殒＇孱溴洵扉篝┅铄痧狒灬沐┅┅换痤狎ㄤ彐疳趑弪痤狎疳趑弪瞟镳弪狒矧疳趑弪铙洪铋翎蜱吼狒翦蝾螬轰镢蹴孱翎糸镱①殄熹翳蝈篚祠镦狃痨轭闲乓猎弦麸遽汨鲠祯殄熹邃怡遽汨疳趑弪轭辛栽乓斡砒犴痨搴换铄舡躔麸痤狎痼羼扉篝ЙЛИН博博换唤ù暴渝犰箫囵骢钽К囵К囵К囵К囵Б轰彐躅ㄤ彐躅痤狎镳弪狒矧蝈篝疳趑弪铙磲脲轭篝犷沐ю钺蝙猴疱蜥麸镳弪狒矧吼狒翦蝾疳趑弪铙┅ㄤ彐礤翳镤狍痼趄遽è痤狎痤狎┅鏖翳箪雉镳弪狒矧疳趑弪铙痤狎磲脲轭篝犷沐ю钺蝙痼趄遽猴疱蜥麸疳趑弪瞽狍痼趄遽镳弪狒矧吼狒翦蝾磲疸狎＇疳趑弪瞽狍痼趄遽疳趑弪铙┅┅ㄤ彐礤翳镤铄è痤狎痤狎痼趄遽愆鏖翳箪雉镳弪狒矧疳趑弪铙痤狎戾è镳ㄩ痼趄遽憝镳弪狒矧铄镳弪狒矧镳弪狒矧┅铄趔磲疸狎＇铄疳趑弪铙┅麒孱矧痫箝糸镱屣铄趔ㄥ镳镳┅蝈趱蝾骝镯铄屣皓蝈篝狎舡汜箦ㄨ犷潇弪忾钿è豉疱弪蝻灬礅溽ㄣ麒孱戾è蝈篝狎ㄦ轭洵蝈篝狎蝈趄鏖翳痱弩舡鲠祯弩悌┅ㄩ铞镫瀛蝈篝狎蝈篝狎舂┅┅ㄡ痧禊＇眭祠榄汨犷铄飙骢钽犰镳铄趔┅蝈趄鏖翳痱弩舡鲠祯弩ī呼弩灬礅溽ㄣㄡ钿豉疱豉疱弪蝻颟ㄥ耢ь蹴忮豉疱弪蝻颦屮疱泗邃豉疱悌豉疱豉疱弪蝻颦溽趱悌ю蝈篝┅灬忮祗è蝈痨徙瀛痱弩趔扉篝磲疸狎灬礅溽ㄩ翦愆豉疱汜箦轸屙扉篝蝈痨徙瀛痱弩趔轸屙┅痱弩箪雉鲠祯轸屙鲠祯濠轸屙┅扉篝┅ㄡ痧禊＇眭祠榄汨犷铄飙骢钽犰镳蝈痨徙瀛痱弩趔铄趔┅┅┅┅ㄥ鲠飙麒孱ê泔眇殪瀛麸痨弼屐红镝洵麸痨弼屐哄邈豸濠ㄤ彐躅磲脲疳趑弪瞽骘颦骢钽糸镱ㄦ躅泗轱瞟⑶孱弪狒涕箴镦黩狃疱骢钽糸镱钺礤鹌瘴迷上麒矬溴骈铋糸镱轶痤狎普蚊陨衔┊渝犰箫囵钺蝙Б戾舄è疳舡簌ㄩ铘弪ㄣ镱汜⑿簌礅镬钺礤骢钽糸镱┅с飙疳趑弪铙┅ㄡ蜱箪轶ㄦ躅泗轱瞽狎珈轶骢钽糸镱┅ㄦ躅悱钺礤篝蜷铉滹黝汜箦骢钽糸镱┅ㄦ蹯飙骢钽钺礤ㄩㄥ耢ㄦ轭洵疳汶徵с飙疳趑弪铙簌礅镬疳汶徵骢钽糸镱┅骢钽钺礤ㄣ镱汜篝蜷铉滹黝汜箦疳汶徵瀛钺礤簌礅镬疳汶徵骢钽糸镱┅⒑骢钽钺礤┅疳蝮邃眭祠轲戾鲠祯瀛扉篝疳蝮瀛矧溟钺蝙灬礅溽扉篝狎珞扉篝┅ㄨ狍蝈篝翳轵疳蝮邃┅ㄡ蜱ㄡ痧孱ㄦ轵篝疳蝮邃磲疸狎＇汜箦泔钿疳蝮邃┅ㄥ铙躜瀛扉篝翳轵疳蝮邃┅┅啜痱镧ㄤ彐躅疳舡簌狎珞扉篝ㄣ镱汜⒂铘狲篚玑骘痤狎＇骢钽钺礤┊渝犰箫囵钺蝙К啖骢祆骢钽钺礤Бìㄩ栳蟓蝈篝п痧禊ф躅汜祆＇痤狎＇骢钽糸镱泪蜱螬瘐箬铄К疳舡簌疳趑弪铙┅┅．啜痱镧括磲疸狎＇磲脲疳趑弪瞽骘颦骢钽糸镱Ж窘冀羼黩狃┅换痱弪犷珏ㄤ彐疳趑弪痱弪犷珏疳趑弪瞟ㄩ铕豸骝镯蜥铉麸蜥铉濠轰镢蹴孱翎糸镱⒁屙狃晌姓骝镯镱蜥铉瀣箴邈殒殄怡埔贤伊吻努麸犷雉桢蜥铉瀣箴邈殒殄怡韵伊吻女物翦翳狒翳轶轶彐驽泗轹屐泔铞孱殄钽黩狃疱秭弪囵钺蝙犷囹弪犷珏Щ翳躞秕箬秕熹箦囹弪犷珏骘盹蝈轭骘蝽狒轱町渝犰箫囹弪犷珏К囵钺蝙Бㄤ彐礤翳镤狍痼趄遽è痱弪犷珏痱弪犷珏┅鏖翳箪雉ㄩ铕豸骝镯蜥铉麸蜥铉濠痱弪犷珏磲脲轭篝犷沐ю蝈蜥铉瀛痼趄遽洪铕豸疳趑弪瞽狍痼趄遽轭瘐舂烘蝻憝蜥铉疳趑弪瞽狍痼趄遽骝镯蜥铉濠呼锃蜥铉疳趑弪瞽狍痼趄遽麸蜥铉濠┅ㄤ彐礤翳镤铄è痱弪犷珏痱弪犷珏痼趄遽愆鏖翳箪雉ㄩ铕豸骝镯蜥铉麸蜥铉濠痱弪犷珏戾è轭瘐铄轭瘐舂ㄦ蝻憝蜥铉铄骝镯蜥铉濠麸蜥铉铄麸蜥铉濠┅麒孱礤礅弪屣扉篝轭瘐骝镯蜥铉麸蜥铉濠蝈趱蝾骝镯铄屣皓蝈蜥铉轭瘐骝镯蜥铉麸蜥铉濠┅换痼扉溴ㄤ彐疳趑弪痼扉溴疳趑弪瞟扉篝蝈疱狒洪铋翩矧溴驷蹯舡疳趑弪瞽蝈疱狒螵戾洪铋翩矧畅篝屦洪铋翩矧暴篝狎洪铋翩矧癌黩狃狒孱洪铋翩矧舂ㄣ躜蝈铘蝈疱狒蟓蝈磲轭轭后翎翦舂ㄣ躜蝈铘蝈疱狒后翎翦洪铋翩矧铋飑蝈磲轭轭绛沲蝌孱舡箦珥孱后翎翦洪铋翩矧铋飑ㄣ躜蝈铘鲠祯后翎翦洪铋翩矧铋飑ㄣ躜蝈铘扉篝戾铉翳后翎翦洪铋翩矧铋飑轰镢蹴孱翎糸镱④⒂扉溴堍徙蝻篌箦泗轱铙镦躺釉遗信猎轶翳麸翎铛礅弪镦箦泗轱铙麸秕麴豸膛翳戾铉翳镦翳箦泗轱町釉判轶翳铛礅弪麸轭泸屙孱翳篝狎轭溴怡徭翦遽汨箦泗轱瞵犷釉烈轶翳轭轸獒轭溴轭麸躺釉翳狒翳骈蝮箦泗轱篝狎趔骝镯滓列猎盼默麒孱趄蹂礤犷翳狒犷轭溴秕趔殇镦翳扉篝鏖祆黩狃狎秕钿阻孱驷祗瀣轭溴弩秕趔殇镦翳扉篝蝈篚祠轭铋飚砒犴痨搴换铄舡躔麸痼扉溴扉篝订舂换唤ū癌渝犰箫囵筱蜥翥瑙ㄤ彐礤翳镤狍痼趄遽è痼扉溴痼扉溴┅鏖翳箪雉扉篝蝈疱狒戾篝屦篝狎黩狃狒孱洎痼扉溴磲脲轭篝犷沐ю箪殇瀛痼趄遽红轶铄扉篝候屦遽趔疳趑弪瞽狍痼趄遽蝈疱狒螬红孱疳趑弪瞽狍痼趄遽戾瞟后翦疳趑弪瞽狍痼趄遽篝屦后翎螋疳趑弪瞽狍痼趄遽篝狎舂瑚蜥瓠狒孱铄黩狃狒孱洎恒躜蝈铘蝈疱狒候屙衢铋铉沲蝌孱舡箦珥孱戾瞟┅ㄤ彐礤翳镤铄è痼扉溴痼扉溴痼趄遽愆鏖翳箪雉扉篝蝈疱狒戾篝屦篝狎黩狃狒孱沲蝌孱舡蝈疱狒蟓蝈磲轭轭沲蝌孱舡蝈疱狒蝈磲轭轭绛沲蝌孱舡箦珥孱沲蝌孱舡鲠祯沲蝌孱舡扉篝戾铉翳痼扉溴躅戾篌沲蝌孱舡鲠祯箦翩沲蝌孱舡鲠祯铄篝狎舂┅躅戾篌沲蝌孱舡扉篝戾铉翳箦翩沲蝌孱舡扉篝戾铉翳戾铉翳扉篝┅灬忮祗è珏舡铄īㄩㄡ钿铒黩狃狒孱洎矧黹铛箴沲蝌孱舡鲠祯濠窘沲蝌孱舡鲠祯沲蝌孱舡扉篝戾铉翳┅屣ㄥ祠黩狃扉篝沲蝌孱舡鲠祯濠┅躅戾篌箪雉怙躅漯痼扉溴с躜蝈铘蝈疱狒蟓蝈磲轭轭绌箦翩沲蝌孱舡蝈疱狒蟓蝈磲轭轭铄蝈疱狒螬┅躅戾篌鲠祯瀛蝈磲轭轭绛沲蝌孱舡蝈疱狒蟓蝈磲轭轭绌蝈趱蝾骝镯铄屣皓ㄩ鲠祯瀛蝈磲轭轭绛蝈磲轭轭绛沲蝌孱舡箦珥孱舂痱镧ㄧ弭铄舂ㄤ邈姝蝈磲轭轭痼扉溴蝈磲轭轭绛沲蝌孱舡箦珥孱舂ㄩ钽沲蝌孱舡鲠祯濠痱镧ㄤ邈姝蝈磲轭轭痼扉溴箦翩蝈磲轭轭绛沲蝌孱舡箦珥孱铄戾瞟ㄩ钽沲蝌孱舡蝈疱狒螬箦翩沲蝌孱舡鲠祯ǐ铄篝狎舂í铄篝屦沲蝌孱舡蝈疱狒螬┅铄痼扉溴┅┅┅换痂轶麸蝙ㄤ彐疳趑弪痂轶麸蝙疳趑弪瞟疳趑弪篝屦疳趑弪瞟轰镢蹴孱翎糸镱⒁彐弪忉汶麸辛栽乓围栝篝矧殄熹轭翳秕麴豸狒翳轭溴痱秭殇邃怡釉判辛栽乓萎物翦翳狒辛栽乓轶篝殪徜鲠钽邃镱沐疱弼孱衄犷殒釉判辛栽乓殄熹铛礅弪痫轭糸铉麸犷弼孱轭辛栽乓翳狒栳箢忮孱殄熹邃弭ㄩ瀹殒辛栽乓栳镱禊徜鲠钽邃镱沐怩釉判辛栽乓殄熹骘轸秕麴豸┈痂轶麸蝙殄熹铋飚砒犴痨搴换铄舡痂轶麸蝙痼弪殄螬痼羼Ж暴┅畅换唤ò紊暴渝犰箫囵筱蜥翥瑙ㄤ彐礤翳镤狍痼趄遽è痂轶麸蝙痂轶麸蝙┅鏖翳箪雉疳趑弪篝屦疳趑弪瞟痂轶麸蝙磲脲轭篝犷沐ю栝篝矧痼趄遽吼狒翦蝾ㄡ蟓痼趄遽疳趑弪瞟后翦瓠疳趑弪疳趑弪瞽狍痼趄遽篝屦疳趑弪瞟┅ㄤ彐礤翳镤铄è痂轶麸蝙痂轶麸蝙痼趄遽愆鏖翳箪雉疳趑弪篝屦疳趑弪瞟痂轶麸蝙戾è铄舡篝屦铄篝屦疳趑弪瞟┅ㄩㄥ镳铄舡篝屦屣痱镧铄疳趑弪瞟ㄨ犷潇弪汜箦痼趄遽憝屐疳趑弪铄舡篝屦痼趄遽憝秕舡镦蜥铉ī铋飑┅┅┅换痼泸狒汨换换蜗耘痼泸狒汨礤汨犷轶箦屙麸忮箪殓梏禊溟骀弪孱艉换篚疱蜚镬扉溴蚝换畜泸狒汨ㄐ箦蜷弩ò暴畜羼ㄛ爆爆爆齿轭姗┊狍郁蝈犴铄粑ū博换艾爆铂艾爆铂超艾爆铂超换换扉箴换铄舡痼泸狒汨痼弪殄暴痼羼扉篝畅洪铈┅辈换ò畅换粕睾滹沲礤铘翳轶轭筱溟骀弪孱沐螽矧ㄤ彐疳趑弪痼泸狒汨疳趑弪瞟疳趑弪篝屦疳趑弪ㄣ躜蝈铘轭溴后翎翦洪铋翩矧癌轰镢蹴孱翎糸镱④⒂泸狒汨弩堍徙蝻篌翳鲠祯弩殄熹邃怡痼趄遽憩箝黹灬轭泔钽屦麸栾氖黹玷筱蜥翥蝈泔蜾犰翦蜷铉翳铒蝽犰骒秣镦痨狴忉汶辛栽乓轶翳箫躜沐疳趑弪瞵犷釉判辛栽乓溴翦蝽轭弩翳轭泸屙孱镦翳轭溴轭麸翳痼趄遽栝篝矧箩箦镱翳疳趑弪矧殓轭犰禊骝镯翳滗餍狒翦蝾吁疱蛎镬扉溴扉怛狎砒犴痨搴换铄舡躔麸痼泸狒汨痼弪殄螬痼羼Ж博博┅换唤ò穿渝犰箫囵栝篝矧Бㄤ彐礤翳镤狍痼趄遽è痼泸狒汨痼泸狒汨┅鏖翳箪雉疳趑弪篝屦疳趑弪瞟痼泸狒汨磲脲轭篝犷沐ю筱蜥翥璀痼趄遽吼狒翦蝾ㄡ蟓痼趄遽疳趑弪瞟后翦瓠疳趑弪疳趑弪瞽狍痼趄遽篝屦疳趑弪瞟┅ㄤ彐礤翳镤铄è痼泸狒汨痼泸狒汨痼趄遽愆鏖翳箪雉疳趑弪篝屦疳趑弪沲蝌孱舡轭溴痼泸狒汨戾è铠铄篝屦疳趑弪瞟┅麒孱ㄥ镳铠舂蝈趱蝾骝镯铄屣皓痱镧痼趄遽憝屐舡骢趱蝈疳趑弪沲蝌孱舡轭溴箦翩沲蝌孱舡轭溴磲ǐ沲蝌孱舡轭溴铠舂癌┅┅换痖ㄤ彐疳趑弪痖疳趑弪瞟è翦篝趄蹂ㄦ犰箦洪铋翩矧铋飑轰镢蹴孱翎糸镱④⑸孳屮痱弩箝镱骘疳趑弪铙耘釉轶弼犰踽翦骘遽汨篝屦犷殒轸铒瞽铋飕翳鲠祯镦砸张鏖祆忮殄熹邃雉桢蝼轶翳鲠祯镦屏逃鏖祆忮物翦翳狒砸张犷屏逃汜忮疳趑弪铙犷殒翳妁狎瀣翳妁狎镱禊徜鲠钽邃轭翳彘蝈箴邈糸鲥汜箦蟋铒骘弼弪篝屦砒犴痨搴换铄舡痖痼羼Ж铋铋铋飑痼羼Ж畅痼羼Ж珐┅俯换唤ū供渝犰箫囵灬К囵骢钽Бㄤ彐礤翳镤铄è痖痖姝痼趄遽愆鏖翳箪雉翦篝趄蹂驷祗濠痖戾è铠铄翦篝┅ㄩㄥ镳铠舂屣ㄩ铠铄趄蹂铄驷祗濠┅┅换疰矧换粕睾箬秕熹翳轶忮扉脲囵汨衢瞌犷徙沐痿犷狎忾趄狎铛礅弪镦轭瘐疳趑弪铙ㄤ彐疳趑弪疰矧疳趑弪瞟疳趑弪狎疱珑獒麸ㄣ躜蝈铘疳趑弪瞽弼孱后翎翦洪铋翩矧铋飑ㄣ躜蝈铘狎疱珑獒麸颦篝蝈犴后翎翦洪铋翩矧铋飑轰镢蹴孱翎糸镱⒘蝠彗玳狒矧疳趑弪罨遽汨弼孱殄熹邃怡辛栽乓轶怙躅麸嗒弼孱舄犷翳孱翳孱糸蝈豉镦翳烈信乔闪韵疳趑弪轶殄熹邃砒犴痨搴换铄舡躔麸疰矧疴轭烘镲痼羼Ж畅暴换疴轭衡狎痼羼Ж订暴┅换唤è胖盼浩舷郝烈穿ㄅ峙卧浩舷郝烈旦ㄅ峙卧浩舷郝烈订换ㄅ峙卧浩舷郝烈穿ㄅ峙卧浩舷郝烈旦ㄅ峙卧浩舷郝烈订换ㄅ峙卧浩舷郝烈穿ㄅ峙卧浩舷郝烈旦ㄅ峙卧浩舷郝烈订渝犰箫囵簌恣囵礤翎К囵颛ㄤ彐礤翳镤狍痼趄遽è疰矧疰矧┅鏖翳箪雉疳趑弪狎疱珑獒麸颟疰矧戾è痼趄ㄡ蟓痼趄遽疳趑弪瞟┅磲脲轭篝犷沐ю骘颦痼趄遽吼狒翦蝾痼趄横蝠彗玳狒矧狎疱珑獒麸恒躜蝈铘疳趑弪瞽弼孱铄痼趄恒躜蝈铘狎疱珑獒麸颦篝蝈犴ㄡ蟓痼趄遽狎疱珑獒麸颟┅┅ㄤ彐礤翳镤铄è疰矧疰矧痼趄遽愆鏖翳箪雉疳趑弪狎疱珑獒麸沲蝌孱舡疳趑弪瞽弼孱沲蝌孱舡狎疱珑獒麸颦篝蝈犴疰矧麒孱ㄥ镳沲蝌孱舡疳趑弪瞽弼孱舂蝈趱蝾骝镯铄屣皓戾è铠戾è弼孱舄ㄣ镯忾铄弼孱趔磲脲溴驷蹯舡弼孱舂沲蝌孱舡疳趑弪瞽弼孱舂┅铄沲蝌孱舡狎疱珑獒麸颦篝蝈犴┅┅躅戾篌ㄥ镳铠舂蝈趱蝾骝镯铄铠舂箦翩沲蝌孱舡疳趑弪瞽弼孱铄疳趑弪瞟沲蝌孱舡狎疱珑獒麸颦篝蝈犴ㄡ蟓痼趄遽狎疱珑獒麸颟铄疰矧┅┅换疳蝠ㄤ屦蝈汜翦洎换粕睾徭翦溴痱邈狒轱瞵蝈泸遽翦疳蝠狍犷徙趱犰狎疱珑獒糸镱疳趑弪町疱蜩狃祜镫麸梏麴蠛玳翳踱泔懑汜蝌殄蜾秣畀眭翎翦骢欤泸遽糸铉犷狎疱珑轱骝镯岘篚篝衢铄洵铒翦犷洵岘汨矧骘麽麸轫痨屙孱犷狎疱珑獒糸镱疳趑弪羁蹰镳瑚轸璀溴痱邈狒轱ê麽蝾轭绌ㄤ彐躅疳蝠é蝈篝蝈篝⒛屦蝈汜翦犰獒骘囵骘颛ㄡ痧禊＇疰矧蝈篝┅ㄥ痫螋ю狎皓换疰轭ㄤ彐疳趑弪疰轭疳趑弪瞟疳趑弪泔躅舂轰镢蹴孱翎糸镱①殄熹躔麸孟瘴秕麴豸骝镯辛栽乓萎砒犴痨搴换铄舡疰轭痼羼Ж畅洪铈畅旦换唤ū紊紊泰渝犰箫囵骈钿躜Бㄤ彐礤翳镤狍痼趄遽è疰轭疰轭┅鏖翳箪雉ㄣ秕铘疳趑弪瞟疰轭磲脲轭篝犷沐ю骈瞽痼趄遽吼狒翦蝾ㄡ蟓痼趄遽疳趑弪瞟恒秕铘铄泔躅舂┅粕睾箬秕熹忮徕戾麸躞狍玑翦疳趑弪町蝈盹鲥翳轶麒镬狍痼趄遽忪镢麒孱轸轶痫篌殁戾ㄤ彐礤翳镤铄è疰轭疰轭痼趄遽愆鏖翳箪雉疳趑弪泔躅铛礅弪疰轭躅戾篌铛礅弪泔躅舂蝈趱蝾骝镯铄屣皓铄疳趑弪瞟┅换疰轭漉ㄤ彐疳趑弪疰轭漉疳趑弪瞟疳趑弪漉麸戾蜥钽洪铋翩矧癌ㄣ躜蝈铘漉后翎翦舂ㄥ灬痼邃漉后翎翦洪铋翩矧癌轰镢蹴孱翎糸镱①殄熹弼孱趔骝镯辛栽乓躅糸翳彘麸翎漉蜥糸镱轶鏖翳轭韵膛伊蚊镦恼椰矧珧遽翦翳犷恼耶令弼孱趔翳狒黠蹯孱忮镱恼狎沲箬矧舢涉辛栽乓秕麴豸铛礅弪蟋翳彘麸翎篚轶扉黹翦轭篝遽洚砒犴痨搴换铄舡疰轭漉疴轭轰躜烘镲痼弪殄螬博畅换唤è胖盼耗找浩舷癌ㄅ峙卧耗找浩舷暴畔些换换铄舡躔麸疰轭漉瘅栝翦穿倍┅换唤ū博换蝈漉沐＇换唤倍渝犰箫囵骈瞌囵簌钽Бㄤ彐礤翳镤狍痼趄遽è疰轭漉疰轭漉颟鏖翳箪雉疳趑弪漉麸戾蜥钽濠疰轭漉磲脲轭篝犷沐ю骈钿躜痼趄遽吼狒翦蝾疳趑弪瞽狍痼趄遽疳趑弪瞟轰躜ㄡ蟓痼趄遽漉颟呼镬弪犷沐铄麸戾蜥钽濠┅ㄤ彐礤翳镤铄è疰轭漉疰轭漉颦痼趄遽愆灬忮祗è珏舡溴祠ㄥ雯ㄥ豉疱汜箦弼ㄥ鲥铘ㄥ鲥铘鲠祯弼轰屐翎┅扉篝蝈漉沐＇磲磲疸狎＇珏舡溴祠弼┅铛礅弪弼┅┅鏖翳箪雉疳趑弪漉麸戾蜥钽沲蝌孱舡漉屐狃箦洵漉颟疰轭漉戾è瞽弼孱铄疳趑弪瞟┅麒孱ㄥ镳瞽弼孱舂蝈趱蝾骝镯铄屣皓躅戾篌箪雉怙躅漯疰轭漉с躜蝈铘漉颟箦翩沲蝌孱舡漉铄漉颟┅麒孱ㄥ镳沲蝌孱舡漉颟蝈趱蝾骝镯铄屣皓戾è蝈ㄩ矧ㄥ耢洪铈沲蝌孱舡漉颟窘沲蝌孱舡漉ǐ屐狃箦洵漉ㄧ弭溴祠瞽弼孱舂┅瞽弼孱戾è翡躜ō沲蝌孱舡漉屐狃箦洵漉颟┅麒孱窘麸戾蜥钽翡躜蝈趱蝾骝镯铄屣皓ㄩㄥ鲥铘瞽弼孱舂ㄣ镯忾铄弼孱趔瞽弼孱ㄥ鲥铘轰躜翡躜┅翡躜┅┅ㄩ钽屐狃箦洵漉ㄧ弭溴祠蝈螬蝈螬┅┅换痼钽ㄤ彐疳趑弪痼钽疳趑弪瞟疳趑弪簌钽聃犷磲漉洪铋翩矧铋飑麸戾蜥钽洪铋翩矧爱鞍暴ㄥ灬痼邃漉后翎翦洪铋翩矧癌轰镢蹴孱翎糸镱①殄熹弼孱趔骝镯辛栽乓躅糸翳彘麸翎漉蜥糸镱轶鏖翳轭韵膛伊蚊镦土啬找沲趑轭镦犷弼孱趔翳狒黠蹯屮翦钿疳篝土啬找涉辛栽乓孱潴忮骘蝈土啬找蝈篝轶徜溴麸翳痼趄遽麸蝻躅轸漉蜥糸镱躔麸翳铄狎弩眭祠轲戾镦淤蚊颜廖援砒犴痨搴换铄舡躔麸痼钽疴轭轰躜痼羼Ж旦暴倍┅换换唤è胖盼耗找旦ㄅ峙卧涸傩阂庞耗找畅换换铄舡躔麸痼钽疴轭轰躜痼羼Ж旦旦倍┅换换唤è胖盼耗找旦ㄅ峙卧耗找旦ㄅ峙卧耗找旦ㄅ峙卧耗找耗盘粤暴渝犰箫囵骈钿躜Бㄤ彐礤翳镤狍痼趄遽è痼钽痼钽┅鏖翳箪雉疳趑弪簌钽聃犷磲漉麸戾蜥钽濠痼钽磲脲轭篝犷沐ю簌钽痼趄遽吼狒翦蝾ㄡ蟓痼趄遽疳趑弪瞟后钽聃犷铄簌钽聃犷舂喉狲漉铄磲漉颟呼镬弪犷沐铄麸戾蜥钽濠┅ㄤ彐礤翳镤铄è痼钽痼钽痼趄遽愆鏖翳箪雉疳趑弪簌钽聃犷磲漉麸戾蜥钽屐狃箦洵漉颟痼钽麒孱ㄡ钿磲漉窘屐狃箦洵漉ō磲漉麸戾蜥钽濠┅蝈趱蝾骝镯铄屣皓戾舄è钼骜铄舡忮狒骘颦聃犷簌钽聃犷屐狃箦洵漉颟瞽弼孱铄疳趑弪瞟瞽弼孱ㄩㄥ镳瞽弼孱舂戾è溟骀ō钼骜屐狃箦洵漉颟┅ㄩ痨躞溟骀ㄥ鲥铘呼疱候弩轰躜溟骀蝈趱蝾骝镯铄屣皓┅瞽弼孱舂瞽弼孱ㄩ磲漉ㄣ镯忾铄弼孱趔瞽弼孱ㄥ鲥铘轰躜黹ㄥ鲥铘鲠祯瞽弼孱轰躜ō磲漉屐狃箦洵漉颟┅瞽弼孱舂┅ㄩ钽屐狃箦洵漉ㄥ鲥铘鲠祯瞽弼孱轰躜┅瞽弼孱舂┅换痄躜篝豸翦换粕睾磲脲鲥蝮轱麒弪弼孱趔箅轲疱鏖翳狎趱蝾邃麸蝈篝轭篝遽麸脲屦翳泔蝌邈漉颟ㄤ彐疳趑弪痄躜篝豸翦疳趑弪瞟疳趑弪ㄣ躜蝈铘鲠祯后翎翦洪铋翩矧铋飑ㄣ躜蝈铘蝈疱狒蟓蝈磲轭轭后翎翦洪铋翩矧癌轰镢蹴孱翎糸镱①殄熹遽汨秕麴豸骝镯辛栽乓糸礤蟋溟鲩溟铉轸怡萎涉翳秕麴豸骝镯辛栽乓轶犷弼孱衄轸漉轶溟鲩溴浠殒轸铛礅弪翳铛礅弪轸箦戽轶溟鲩溴轭篝遽镦忮轭殄熹邃溟蝈泗禊砒犴痨搴换铄舡痄躜篝豸翦痼羼Ж旦痼羼Ж博┅供换唤ū悲悲弹弹紊泰换换铄舡痄躜篝豸翦疴轭轰躜痼羼Ж旦┅换痼羼Ж博┅换供换唤è胖盼耗找悲畅ㄅ峙卧耗找悲畅ㄅ峙卧耗找悲畅ㄅ峙卧耗找暴ㄅ峙卧耗找暴ㄅ峙卧耗找畅ㄅ峙卧耗找弹博ㄅ峙卧耗找弹博紊泰渝犰箫囵颛ㄤ彐礤翳镤狍痼趄遽è痄躜篝豸翦痄躜篝豸翦颟鏖翳箪雉疳趑弪瞟痄躜篝豸翦磲脲轭篝犷沐ю漉蝮趱趑弪痼趄遽吼狒翦蝾ㄡ蟓痼趄遽疳趑弪瞟侯疳趑弪瞽狍痼趄遽瞟┅ㄤ彐礤翳镤铄è痄躜篝豸翦痄躜篝豸翦颦痼趄遽愆鏖翳箪雉疳趑弪沲蝌孱舡鲠祯沲蝌孱舡蝈疱狒蟓蝈磲轭轭绌痄躜篝豸翦麒殪ㄡ钿沲蝌孱舡蝈疱狒蟓蝈磲轭轭弪镳沲蝌孱舡蝈疱狒蟓蝈磲轭轭绌箦翩沲蝌孱舡蝈疱狒蟓蝈磲轭轭铄瞟戾è铄疳趑弪瞟┅麒孱ㄥ镳沲蝌孱舡蝈疱狒蟓蝈磲轭轭绌蝈趱蝾骝镯铄屣皓麒孱ㄡ钿沲蝌孱舡蝈疱狒蟓蝈磲轭轭铒弪镳沲蝌孱舡蝈疱狒蟓蝈磲轭轭绌┅箦翩沲蝌孱舡鲠祯ㄩㄥ镳濠屣ㄣ豉疱汜箦ㄥ鲥铘ㄣ镯忾铄弼孱趔ㄥ鲥铘轰躜ǒㄥ鲥铘鲠祯轰躜沲蝌孱舡蝈疱狒蟓蝈磲轭轭绌┅铛礅弪ǒ沲蝌孱舡蝈疱狒蟓蝈磲轭轭绌┅┅┅麒孱沲蝌孱舡蝈疱狒蟓蝈磲轭轭ㄤ邈姝蝈磲轭轭痄躜篝豸翦颟沲蝌孱舡鲠祯濠┅换疴遽ㄤ彐疳趑弪疴遽疳趑弪瞟ī轰镢蹴孱翎糸镱①殄熹翳铛礅弪镦忮狒屐狃箦箝钽翳疴遽麽屙忮滗邃轭翳痼趄遽懋砒犴痨搴换铄舡疴轭轰躜痼羼Ж畅烘镲疴遽舂畅换唤è胖盼耗找浩舷癌ㄅ峙卧耗找浩舷暴ㄅ峙卧耗找浩舷畅渝犰箫囵忮狒К噔遽臾囵蝓瞌ㄤ彐礤翳镤铄è疴遽疴遽舡痼趄遽愆ㄢ遽疳趑弪瞽疳蝈铘疴遽恒灬篌ю忾钿┅换疴遽舄ㄤ彐疳趑弪疴遽舄疳趑弪瞟è翎箅后翎翦洪铋翩矧铋飑轰镢蹴孱翎糸镱①殄熹翳铛礅弪镦忮狒镱翳嗒沆镢氇镦翳沲蝌孱疳趑弪町深雉桢黠蜾蟋疴遽舄轶沆镢氕簌钽邃躅扉脲囵忮狒К麒殂轶疳趑弪瞽簌钽邃砒犴痨搴换铄舡疴轭轰躜痼羼Ж畅烘镲疴遽舄┅畅换唤è胖盼耗找浩舷舶癌ㄅ峙卧耗找浩舷舶暴ㄅ峙卧耗找浩舷舶畅渝犰箫囵忮狒К噔遽臾囵蝓瞌ㄤ彐礤翳镤铄è疴遽舄疴遽舄痼趄遽愆ㄢ遽沆镢氇┅换痿轫ㄤ彐疳趑弪痿轫疳趑弪瞟è灬篝忮狒汨邈脲后翎翦洪铋翩矧铋飑翦眇锃狒忮狒后翎翦洪铋翩矧铋飑ㄥ灬痼邃糸礤后翎翦洪铋翩矧癌轰镢蹴孱翎糸镱①殄熹翳铛礅弪镦箦泔钿屐狃箦箝钽痿轫麽屙忮滗邃轭翳痼趄遽懋物翦歪玳鲥轭徙沲蜥翦蝈篚祠殒翳沆镢毵翦眇汨犷珏镢沲盹蝈骝羼蹂铘禊翳犷弼孱趔轭翳疳蝈铘疴轭洚砒犴痨搴换箦翩翦眇沆镢氇暴栋滦换铄舡疴轭轰躜呼轫痿轫濠博换唤è胖盼耗找涸赏癌ㄅ峙卧耗找涸赏碑癌渝犰箫囵忮狒К囵蝓瞌噔遽臾ㄤ彐礤翳镤铄è痿轫痿轫瀛痼趄遽愆粕睾翎脲轭麸徙泔躅翳痱弼轱躞翦眇殒轸栳忮孱汨犷珏箝钽翳灬篝忮狒汨邈脲洚鏖翳箪雉灬篝忮狒汨邈脲翦眇锃狒忮狒屐狃箦洵糸礤痿轫鏖翳箪雉翦眇铹沆镢氇戾è忮狒ㄢ遽疳趑弪瞽疳蝈铘痿轫恒灬篌ю忾钿┅┅痱镧ㄩ钽屐狃箦洵糸礤ㄩ铛祆灬篝忮狒汨邈脲洎ㄤ躜漉蜥糸镱ō忮狒灬篝忮狒汨邈脲洎翦眇铹┅箦翩灬篝忮狒汨邈脲忮狒翦眇锃狒忮狒翦眇铹┅┅换痖钿屮换韵南犰翦蝾狒鲥蝮轱翳狒镱禊汜祆＇铄镱轭溴疳遽汨糸礤翳疳趑弪瞽狍痼趄遽镦扉篝疳栳孱溴洚换韵南痖钿屮翳狒镱禊屙忮潴屐屙孱骝镯篚怵狒翦蝾蟋灬吁疱蛎镬扉溴颛畜鏖翥璞ㄤ彐疳趑弪痖钿屮疳趑弪瞟扉篝疳轭溴疳黩狃洪铋翩矧铋飑轰镢蹴孱翎糸镱⒄箦晌呐丨辛麸轭溴轭麸翳扉篝蝈趱蝾邃怡躺釉辛援滓列轶麒弭桢轭溴弩翳狒狎秕镦蜥铉鏖祆忮黩狃疱ㄩ舂矧鏖祆箝眇禊蝈趱蝾铋飚砒犴痨搴换铄舡痖钿屮扉篝构垢狗痼羼扉篝畅┅穿换换唤ü垢狗紊泰换换铄舡躔麸痖钿屮扉篝构垢狗痼弪殄订舂换换唤ü垢狗构垢狗渝犰箫囵麽祀Бㄤ彐礤翳镤狍痼趄遽è痖钿屮痖钿屮┅鏖翳箪雉扉篝疳轭溴疳黩狃皓痖钿屮磲脲轭篝犷沐ю轭溴痼趄遽红轶舡疳疳趑弪瞽狍痼趄遽扉篝疳舂洪钿屮疳疳趑弪瞽狍痼趄遽轭溴疳舂瑚蜥瓠黩狃皓┅ㄤ彐礤翳镤铄è痖钿屮痖钿屮痼趄遽愆粕睾磲脲翳轶黠螂骘孱鲥祜疱狍麇祆翳轭溴箬秕熹铒忮铒蝽犰辁邃鏖翳箪雉扉篝疳轭溴疳黩狃皓痖钿屮戾è扉篝铄扉篝疳舂ㄩ澍铄轭溴疳舂┅麒孱矧ㄥ镳殇ㄥ镳扉篝┅蝈趱蝾骝镯铄屣皓ㄦ躅汜祆ㄩ黩狃ь翳黩狃ь翳殇扉篝┅┅换痱躅ㄤ彐疳趑弪痱躅疳趑弪瞟疳趑弪ㄤ躜洪铋翩矧暴ㄣ躜蝈铘漉后翎翦洪铋翩矧癌轰镢蹴孱翎糸镱⒁躅辛栽乓堍轭溴疱钿孱綮堍镦轸疳蝈铘栾熹轭遽汨鲠祯骘恼忮狒螽裴汨镦辛栽乓围秕麴豸轶趄遽翦狍殒轸灬篝邃恼忮狒蟋忮轭泔铘轭躏躞禊殄熹邃漉蜷铉翳狒糸礤忮骘蝈盹鲩铉镱麸翳铄秕麴豸砒犴痨搴换铄舡躔麸疴轭烘镲痼羼Ж旦换衡狎痱躅痼羼Ж俯换痼羼Ж爱爱暴┅┅换唤è胖盼浩舷郝烈穿换ㄅ峙卧浩舷郝烈旦换ㄅ峙卧浩舷郝烈旦换ㄅ峙卧浩舷郝烈订换ㄅ峙卧浩舷郝烈俯渝犰箫噔遽臾囵忮狒Бㄤ彐礤翳镤狍痼趄遽è痱躅痱躅┅鏖翳箪雉疳趑弪漉颟痱躅躅戾篌疳趑弪瞽疳蝈铘痱躅恒灬篌ю忾钿ㄥ蝌矧汜铑雉忮躞邃秕趔殇镦疴轭洧痱躅┅磲脲轭篝犷沐ю蝓瞽痼趄遽吼狒翦蝾ㄡ蟓痼趄遽疳趑弪瞟轰躜疳趑弪瞽狍痼趄遽漉颟恒躜蝈铘漉癌┅ㄤ彐礤翳镤铄è痱躅痱躅痼趄遽愆鏖翳箪雉疳趑弪漉沲蝌孱舡漉漉颦栝篝矧铛礅弪痱躅戾è忮狒ㄢ遽疳趑弪瞽疳蝈铘痱躅恒灬篌ю忾钿┅┅ㄦ戾è铄舡漉ī戾è铠铄漉颟┅躅戾篌ㄥ镳铠舂铄疳趑弪瞟ㄩ钽沲蝌孱舡漉铠舂┅┅麒孱弪镳铛礅弪铄舡漉颟麒殪ㄡ钿矧铒痼趄遽憝漉颟铒ㄥ钿邃漉颟┅冀沲蝌孱舡漉忮狒螬铄舡漉颟┅灬篝秕麴豸疳趑弪瞟┅换痼ㄤ彐疳趑弪痼疳趑弪瞟疳趑弪ㄣ躜蝈铘痼趄遽后翎翦洪铋翩矧屣皓轰镢蹴孱翎糸镱⒄箦疳趑弪镦簌礅镬麸屙忮囵溴妲螽辛栽乓轶翳箫躜沐疳趑弪翳狒殄熹簌礅镬钺黹铉翳痄彐麸屙忮洚砒犴痨搴换痄彐烘镲痼羼Ж畅暴换换痄彐衡狎痼羼Ж订暴换换铄舡躔麸痼痼羼Ж烘镲衡狎暴┅换换唤ū订渝犰箫囵溴妲囵疳颛囵礤翎Бㄤ彐礤翳镤狍痼趄遽è痼痼愆鏖翳箪雉疳趑弪瞟痼磲脲轭篝犷沐ю簌憝痼趄遽吼狒翦蝾ㄡ蟓痼趄遽疳趑弪瞟┅ㄤ彐礤翳镤铄è痼痼憝痼趄遽愆灬忮祗è磲忮痄彐ㄩ姝戾è痄彐ㄡ钿簌礅镬痄彐疳趑弪┅┅痄彐┅鏖翳箪雉疳趑弪沲蝌孱舡痼趄遽愆痼戾è铄沲蝌孱舡痼趄遽愆┅ㄩㄥ镳瞟戾è铄舡痄彐铄疳趑弪瞟┅麒孱ㄥ镳铄舡痄彐蝈趱蝾骝镯铄屣皓箦翩沲蝌孱舡痼趄遽ㄡ蟓痼趄遽ㄩ扉篝铄舡痄彐痧狎磲疸狎＇磲忮痄彐铄舡痄彐┅磲忮痄彐铄舡痄彐┅┅铄痼愆瞟┅┅换疸栳轭ㄤ彐疳趑弪疸栳轭疳趑弪瞟疳趑弪铙轰镢蹴孱翎糸镱⒚镯忾铄眭祠轲戾疳趑弪铙轭麸镱弼孱篝蝈犴砒犴痨搴换铄舡疸栳轭疴轭烘镲痼羼Ж畅┅疴轭衡狎痼羼Ж供暴┅穿换换唤è胖盼浩舷郝烈珐ㄅ峙卧浩舷郝烈俯ㄅ峙卧浩舷郝烈供紊泰渝犰箫囵忾钿Ё哄礅邃脲轰彐躅ㄤ彐躅疸栳轭é蝈篝疳趑弪铙磲脲轭篝犷沐ю汨衢吼狒翦蝾疳趑弪铙┅ㄤ彐礤翳镤狍痼趄遽è疸栳轭疸栳轭┅鏖翳箪雉疳趑弪铙疸栳轭磲脲轭篝犷沐ю汨衢瞽痼趄遽吼狒翦蝾磲疸狎＇疳趑弪瞽狍痼趄遽疳趑弪铙┅┅ㄤ彐礤翳镤铄è疸栳轭疸栳轭痼趄遽愆鏖翳箪雉疳趑弪铙疸栳轭戾è悱弼孱磲脲溴驷蹯舡弼孱舂┅ㄤ镬轶疳趑弪疳趑弪铙悱弼孱舂箦翩悱弼孱ㄣ镯忾铄弼孱趔悱弼孱戾è弼孱舄悱弼孱舂铄疳趑弪瞟┅┅┅换痄殒ㄤ彐疳趑弪痄殒疳趑弪瞟疳趑弪瞟轰镢蹴孱翎糸镱⑾豸瘐翳溟骀弪孱沐忮赭邋篚沣弩箝鲥秕麴豸镦辛栽乓萎砒犴痨搴换铄舡痄殒痼羼Ж畅暴穿换换唤ō紊泰渝犰箫囵溴祠岌ㄤ彐礤翳镤铄è痄殒痄殒姝痼趄遽愆鏖翳箪雉疳趑弪瞟痄殒麒孱弪镳箪雉鲠祯疳趑弪ц轶麸蝙铛礅弪┅铄疳趑弪瞟戾è灬篝痼趄遽憝屐疳趑弪暴铄铄疳趑弪瞟┅麒孱矧ㄥ镳灬篝ㄥ镳铄舂蝈趱蝾骝镯铄屣皓ō铄灬篝┅┅换痄屐翎ㄤ彐疳趑弪痄屐翎疳趑弪瞟疳趑弪ㄣ沆洪铋翩矧穿轰镢蹴孱翎糸镱⑾豸瘐翳溟骀弪孱沐忮赭邋篚沣弩箝鲥秕麴豸镦辛栽乓维狍篚黹铉辛栽乓蝈篝狎趔弼弪觅锰秕麴豸螽疹扉脲囵溟骀К痄屐翎轶黩轸翦鏖翳轸躞狍轭瘐骘囵忾钿Ё轰屐翎脲轭黹钿涉辛栽乓围篚沣弩箝鲥鲠祯弩黠蹯蝈篚祠轭铄玑糸鲥溟骀弪孱沐痄屐翎轭篝遽黩狃翳溴祠狎秕钿麸翳铄眭祠轲戾镦觅锰女澡轶黠蹯犰祜秕麸骘屮犴痨瀣篚痧禊翳铛礅弪镦翳忮狒翳狒遽汨弼孱镢沲蝮镱蜥翳弪翳犷箴邈殒轭翳溴祠忮赭邋遽汨弼孱舢澡轶轶镦泔躜箦徙栝弼徕戾躞轭疴轭洄衡遽脲狍麇祆栾麇鲥翳狒礤翳镤蝈聃轵弩翳疴轭麸疱咫狒骢趱蝈鲠祯弩麒殂轶铒犰麽溴箝蜥忪瀹箩箦镱翳疳趑弪矧殓轭犰禊骝镯翳滗餍狒翦蝾吁疱蛎镬扉溴扉怛狎砒犴痨搴换铄舡痄屐翎痼羼Ж畅穿俯换换唤ū暴换换铄舡痄屐翎痼羼Ж旦穿俯换换唤ū畅渝犰箫囵溟骀К囵忾钿Ё衡遽脲ㄤ彐礤翳镤狍痼趄遽è痄屐翎痄屐翎┅鏖翳箪雉疳趑弪泫沆濠痄屐翎磲脲轭篝犷沐ю溴祠岘痼趄遽吼狒翦蝾ㄡ蟓痼趄遽疳趑弪瞟恒沆铄泫沆濠┅ㄤ彐礤翳镤铄è痄屐翎痄屐翎痼趄遽愆鏖翳箪雉疳趑弪泫沆栝篝矧铛礅弪痄屐翎麒孱弪镳栝篝矧铛礅弪铄疳趑弪瞟戾è祧矧痼趄遽憝屐疳趑弪暴癌ㄣ铄疳趑弪瞟┅麒孱矧ㄥ镳祧ㄥ镳泠┅蝈趱蝾骝镯铄屣皓ō泠ō祧ㄣ彘扉铉怡ō祧泠泫沆濠┅┅换痄蝻ㄤ彐疳趑弪痄蝻疳趑弪瞟疳趑弪洪铋翩矧癌轰镢蹴孱翎糸镱⒛蝻翳骈蝮秕麴豸骝镯辛栽乓犷殄熹翳蝈篝涉轶铄玑糸鲥潋镳翳灬篝秕麴豸骝镯辛栽乓轭篝遽洚砒犴痨搴换铄舡痄蝻痼羼Ж穿暴博穿换换唤ǔ紊紊泰渝犰箫囵蝻翎翦Бㄤ彐礤翳镤狍痼趄遽è痄蝻痄蝻皓换粕睾汨邈翳狒轶铒忾珑弪矧箜犰戾翳犷栝篝矧犰祜黧鏖翳箪雉疳趑弪瞟痄蝻磲脲轭篝犷沐ю潋镳痼趄遽吼狒翦蝾ㄡ蟓痼趄遽疳趑弪瞟侯瞟┅ㄤ彐礤翳镤铄è痄蝻痄蝻瓠痼趄遽愆鏖翳箪雉疳趑弪铛礅弪痄蝻ㄩ黹铛箴瞟麒孱ㄥ镳痼趄遽憝屐舡骢趱蝈疳趑弪ō铛礅弪瞟┅蝈趱蝾骝镯铄屣皓麒孱弪镳箪雉鲠祯疳趑弪ц轶麸蝙铛礅弪┅ㄤ雉轫弩ㄩ瞟铄疳趑弪瞟┅铄疳趑弪瞟┅换痧狎ㄤ彐疳趑弪痧狎疳趑弪瞟疳趑弪铙痼趄遽眢后翎翦洪铋翩矧铋飑轰镢蹴孱翎糸镱⒚镯忾铄眭祠轲戾弼孱疳趑弪铙轭麸镱痼趄遽鏖翳犰弼孱趔轭翦眇矧犰矧溴虍辛栽乓斡轶翳扉篝镦疳趑弪铙矧疳趑弪殄熹轭扉篝镦疳趑弪铙澡痧狎孱潴麒孱犰镦翳疳趑弪铙轭辛栽乓斡孱洚砒犴痨搴换铄舡躔麸痧狎扉篝疴轭轰躜痤悲穿换疴轭轰躜痤帛穿┅┅换换唤è胖盼耗找悲耗盘粤癌ㄅ峙卧耗找帛耗盘粤悲博换ㄅ峙卧耗找悲耗盘粤悲订ㄅ峙卧耗找帛耗盘粤悲畅换ㄅ峙卧耗找悲耗盘粤悲畅ㄅ峙卧耗找帛耗盘粤悲订换ㄅ峙卧耗找悲耗盘粤悲博ㄅ峙卧耗找帛耗盘粤帛畅渝犰箫囵簌恣ㄤ彐礤翳镤铄è痧狎痧狎痼趄遽愆鏖翳箪雉疳趑弪铙痼趄遽眢栝篝矧铛礅弪痧狎灬忮祗è铄舡轭痼趄遽眢ī矧盹篝＇痼趄遽眢弘妁＇忮狒屣皓磲忮蝈箦舡痼趄遽眢ī躅戾篌蝈盹鲥屣痼趄遽眢戾è铄舡扉篝铄疳趑弪铙┅麒孱ㄥ镳铄舡扉篝蝈趱蝾骝镯磲忮蝈箦舡痼趄遽眢铋飑箦翩痼趄遽眢磲疸狎灬礅溽ㄩ翦愆ㄡ蟓痼趄遽ㄩ簌礅镬轸屙ㄦ轭洵痄彐轸屙轸屙┅铄舡扉篝┅┅┅麒孱弪镳栝篝矧铛礅弪磲忮蝈箦舡痼趄遽眢┅戾舄è铄舡疳铄舡轭痼趄遽眢┅铠铄铄舡疳舂┅ㄩㄥ镳铠舂痱镧蝈盹鲥痼趄遽眢铄舡疳舂戾è铋铄舡轭痼趄遽眢┅ㄩㄥ镳铋皓屣ㄥ鲥铘呼疱候弩轰屐翎ōㄢ遽铋皓ㄢ遽痧狎┅┅┅ㄣ镯忾铄弼孱趔铠ㄥ鲥铘轰屐翎ōㄢ遽铄舡轭痼趄遽眢┅ㄢ遽痧狎┅┅┅┅换痿ㄤ彐疳趑弪痿疳趑弪瞟疳趑弪ㄤ躜洪铋翩矧穿疳趑弪瞽秕麴豸后翎翦舂轰镢蹴孱翎糸镱⒃轫弩趄弭汨辛栽乓箫轸麸翎漉蜥糸镱轶恼耶物翦翳狒镱禊翳骈蝮嗒磲疳趑弪瞽殄熹戾铉翳弼孱趔骝镯辛栽乓狎泔铙殇弪邃犷翳狒翳妁狎汜煦蹯狒邃轫礤溟狒屐狒痼趄遽泸遽糸镱糸礤蜥翳弪翳犷灬殪狍翳痼趄遽殄熹螽砒犴痨搴换铄舡躔麸痿疴轭轰躜痤穿旦换换唤è胖盼耗找弹穿ㄅ峙卧耗找弹穿ㄅ峙卧耗找弹穿ㄅ峙卧耗找弹穿渝犰箫囵骈钿躜К囵簌钽Бㄤ彐礤翳镤狍痼趄遽è痿痿螬鏖翳箪雉疳趑弪漉颟痿戾舄è痼趄ㄡ蟓痼趄遽疳趑弪瞟蝈铄舡躔麸痼趄┅磲脲轭篝犷沐ю趔痼趄遽吼狒翦蝾痼趄轰躜铄漉颟吼狒翦蝾秕麴豸ㄣ镥蜚蝈鲥泗矧┅┅ㄤ彐礤翳镤铄è痿痿蟓痼趄遽愆鏖翳箪雉疳趑弪漉疳趑弪瞽秕麴豸铛礅弪痿麒孱窘铛礅弪戾铉翳疳趑弪瞽秕麴豸螬蝈趱蝾骝镯铄屣皓戾è眭ǒ漉ㄢ遽疳趑弪瞟┅ㄥㄥ祠疳趑弪瞽秕麴豸铛礅弪┅ㄣ镯忾铄弼孱趔弼ㄥ鲥铘轰躜í眭ㄥ鲥铘鲠祯弼轰躜┅┅┅换瘅犰ㄤ彐疳趑弪瘅犰疳趑弪瞟扉篝篝屦疳趑弪ㄤ轵邈糸镱疳趑弪洪铋翩矧暴篝狎舡痫洪铋翩矧癌ㄣ躜蝈铘轭溴后翎翦舂ㄣ躜蝈铘溟蝈泗轱洪铋翩矧后翎翦舂轰镢蹴孱翎糸镱④⒆犰胲秭弪翳鲠祯弩轭躺釉怡躞轭翳徙沲眭灬翦鲠祯镦翳秕麴豸镦釉判辛栽乓狍翳轭溴留翳忮玳铑轭镦翳瘅犰犷遽汨糸礤翳篝狎矧孱镦翳扉篝轶疳篌邃翳秕麴豸镦纳遗迷上苇辛栽乓轶翎脲犷躞邃狍翳眭祠轲扉弪骘鲠祯弩骝镯釉判辛栽乓萎釉烈原邢轶翳轭溴轭躺釉骘瘅犰麸翎脲轸骈蝮秕麴豸骝镯砒犴痨搴换换躞轭痼羼扉篝暴狍翳纳遗迷上苇辛栽乓汜躞弩翳瘅犰毵秕麴豸麸堍痖铉痫铉堍换铄舡瘅犰扉篝畅痼羼扉篝暴痼羼扉篝暴┅卑换换唤ò畅渝犰箫囵轭溴К囵怛秣瞌囵徙沲恣ㄤ彐礤翳镤狍痼趄遽è瘅犰瘅犰氅鏖翳箪雉扉篝篝屦疳趑弪溟蝈泗轱瞽疳趑弪篝狎舡痫螬瘅犰磲脲轭篝犷沐ю麽祀痼趄遽红轶铄扉篝后翦瓠疳趑弪疳趑弪瞽狍痼趄遽篝屦疳趑弪瞟轰轵邈糸镱疳趑弪疳趑弪瞽狍痼趄遽溟蝈泗轱瞽疳趑弪瞟后翎螋痫疳趑弪瞽狍痼趄遽篝狎舡痫螬┅ㄤ彐礤翳镤铄è瘅犰瘅犰氕痼趄遽愆鏖翳箪雉扉篝篝屦疳趑弪溟蝈泗轱瞽疳趑弪篝狎舡痫沲蝌孱舡轭溴沲蝌孱舡溟蝈泗轱铛礅弪瘅犰麒孱弪镳铛礅弪箦翩沲蝌孱舡轭溴铄篝狎舡痫螬箦翩沲蝌孱舡溟蝈泗轱铄溟蝈泗轱瞽疳趑弪瞟蝈趱蝾骝镯铄铘沲蝌孱舡轭溴扉篝┅戾è铙铄篝屦疳趑弪瞟┅麒孱矧ㄥ镳铙皓ㄥ镳沲蝌孱舡轭溴ㄥ镳沲蝌孱舡溟蝈泗轱瞟蝈趱蝾骝镯铄屣皓灬忮祗è铄舡轭溴īǐ沲蝌孱舡轭溴í铙沲蝌孱舡溟蝈泗轱瞟┅戾è铄舡轭溴铄舡轭溴┅麒孱矧黹铛箴铄舡轭溴窘铄舡轭溴戾铉翳扉篝┅箦翩沲蝌孱舡溟蝈泗轱铄溟蝈泗轱瞽疳趑弪瞟┅麒孱ㄥ镳沲蝌孱舡溟蝈泗轱瞟蝈趱蝾骝镯铄屣皓箦翩沲蝌孱舡轭溴盹铄舡轭溴戾铉翳扉篝┅┅┅ㄥ祠黩狃扉篝沲蝌孱舡轭溴┅换痧狎汨衢ㄤ彐疳趑弪痧狎汨衢疸栳轭疳趑弪铙轰镢蹴孱翎糸镱⒚镯忾铄眭祠轲戾疳趑弪铙轭麸箦鲥蜥弼孱篝蝈犴螽澡弼孱殄熹邃怡翳骈蝮疳趑弪鏖祆忮躞邃狍翳轭瘐弼孱麸翳箦泔钿疳趑弪瞵犷箫镱澡弼孱趔殄熹邃怡遽汨疳趑弪鏖祆忮泔祆邈翦轭麸扉篝犷殄熹邃怡翳痧狎汨衢町澡轶疳趑弪轶彐驽泗轹屐囵疳颛犷囵汨衢瞌泔礅轭邃砒犴痨搴换铄舡躔麸痧狎汨衢疴轭烘镲痼弪殄畅疴轭衡狷皤痣烘镲暴烘镲皤痣烘镲畅┅换唤èㄅ峙卧浩舷癌ㄅ峙卧浩舷郝邻暴换è胖盼浩舷暴ㄅ峙卧浩舷郝邻博换è胖盼浩舷博ㄅ峙卧浩舷郝邻畅┅渝犰箫囵疸К囵疳颛囵汨衢瞌囵忾钿Ё哄礅邃脲轰彐躅ㄤ彐躅痧狎汨衢é蝈篝疳趑弪铙磲脲轭篝犷沐ю疳蜚栳轭吼狒翦蝾疳趑弪铙┅ㄤ彐礤翳镤狍痼趄遽è痧狎汨衢痧狎汨衢瞟鏖翳箪雉疳趑弪铙痧狎汨衢磲脲轭篝犷沐ю疳蜚栳轭痼趄遽吼狒翦蝾磲疸狎＇狍痼趄遽疳趑弪铙┅┅ㄤ彐礤翳镤铄è痧狎汨衢痧狎汨衢瞽痼趄遽愆鏖翳箪雉疳趑弪铙痧狎汨衢戾è悱弼孱磲脲溴驷蹯舡弼孱舂┅祜镳烘矧疳趑弪洪疳趑弪铙轰箦翩悱弼孱ㄣ镯忾铄弼孱趔悱弼孱戾è弼孱舄ㄣ镳弼孱悱弼孱舂┅铄疳趑弪瞟┅洪ㄥ镳悱弼孱舂候弭躜屣哄祗恒镬戾泗悱弼孱舂┅换痧ㄤ彐磲泸痧é怙澌疳轵螬⒂铘狲篚玑骘囵疳蜚栳轭翳狒狨麸磲糸汜祆箴扉趔辛梢怡涵簌礅镬螽砒犴痨搴换痧烘镲痼羼扉篝畅暴换涵换衡狎皤痣烘镲博换唤èㄅ峙卧浩舷暴ㄅ峙卧浩舷郝烈畅换è胖盼浩舷博ㄅ峙卧浩舷郝烈穿换è胖盼浩舷畅ㄅ峙卧浩舷郝烈旦┅渝犰箫囵疳蜚栳轭Б灬忮祗è痧悱箴扉疳轵螬戾è痫痫箝糸镱涵疳轵螬┅ㄩ痫扉篝篚怏羼疳轵痫螬痧悱箴扉篚怏羼疳轵ū痫螬┅疳轵螬┅啜痧狎汨衢括祜镳烘矧洪痧悱箴扉疳轵螬恒镬戾泗ㄣ镱ю忾钿椹┅┅瘐箬铄ю疸疳趑弪铙换疸祯眇ㄤ彐疳趑弪疸祯眇疳趑弪瞟疳趑弪洪铋翩矧暴轰镢蹴孱翎糸镱⑶蝻躔秕麴豸镦翳箫躜沐疳趑弪轭麸扉篝镦躔麸轸屙遽汨砒犴痨搴换铄舡躔麸疸祯眇痼弪殄旦博换唤è暴ú畅ù┅渝犰箫囵徙祯眇Бㄤ彐礤翳镤铄è疸祯眇疸祯眇痼趄遽愆鏖翳箪雉疳趑弪瞟疸祯眇戾è铄铄瞟┅麒孱ㄥ镳铄舂蝈趱蝾骝镯铄屣皓矧铄舡躔麸疳趑弪铄舂屣皓┅换疳沆蹴ㄤ彐疳趑弪疳沆蹴疳趑弪瞟疳趑弪瞟轰镢蹴孱翎糸镱⒘豸镯狒殂犰禊珧秕秕麴豸镦翳箫躜沐疳趑弪轭麸扉篝镦躔麸轸屙遽汨疹扉脲囵沆蹴皈沆蹴箝轶狨麸磲糸汜祆箦麸翳戾铉翳镦翳祜铉弩扉篝轭翳鲠祯弩镦嗒弼孱舄К矧殒翳弪狎铒扉篝螽砒犴痨搴换铄舡躔麸疴轭烘镲痼羼Жūū博ū畅暴衡狎疳沆蹴痼弪殄螬┅换唤è胖盼浩舷ū郝烈ò┅ㄅ峙卧浩舷ū博郝烈ū博ㄅ峙卧浩舷ū畅郝烈ǔ旦┅渝犰箫囵沆蹴皈ㄤ彐礤翳镤铄è疳沆蹴疳沆蹴瓠痼趄遽愆鏖翳箪雉疳趑弪瞟疳沆蹴躅戾篌弼孱舄蝈趱蝾骝镯铄屣皓铄舡躔麸疳趑弪蝈漉沐＇磲磲疸狎ㄦ戾铉翳ㄥ铙躜瀛扉篝ㄥ擤┅脲弼孱舄┅┅┅换疳沣蹴换梏麴蠛疸懋疱徕镤觇醍邃醑琪蜷玷舫篝漤鸩滹泱吁疱蛎镬扉溴蜻嘛镫泔溴描ゲ安哎舶溴麂蝻馥舶犷浈舶汨蹉腱殁溴麂蝻疬扉獐滗餍狒翦蝾蟑儒祓嗅沣蹴梏盱ㄤ彐疳趑弪疳沣蹴疳趑弪瞟è镳弪狒矧洪铋翩矧＇篝狎洪铋翩矧癌篝屦洪铋翩矧暴戾铉翳洪铋翩矧溴驷蹯舡疳趑弪瞽戾铉翳祜洪铋翩矧铋飑ㄨ洪铋翩矧铋飑ㄢ秕钿怡洪铋翩矧铋飑ㄣ躜蝈铘鲠祯后翎翦舂ㄣ躜蝈铘蝈疱狒蟓蝈磲轭轭后翎翦舂轰镢蹴孱翎糸镱⑽蹴弪殂徙沲眭灬麸虍裴汨秕麴豸犷釉判轶躞邃狍翳轭瘐骘闲乓猎弦麸珏铄蜥翦翳铄秕麴豸阻孱滔壬犷孪瘴沫沦狎痱秭殇邃秕麴豸翳狒驷祆秕趔殇翳蜥铉滔壬狎黩狃疱忉汶轭箝溴鏖翳翳孪瘴沫沦骢钽糸镱翳鲠祯轶痱秭殇邃狍轸骈蝮狎珲礤铘犷滔犷壬狎痱秭殇邃狍轸箦泔钿犷翳轵洚箩箦镱翳疳趑弪矧殓轭犰禊骝镯翳滗餍狒翦蝾吁疱蛎镬扉溴扉怛狎砒犴痨搴换铄舡躔麸疳沣蹴＇暴旦筢礤狍痼弪殄暴换唤ò穿换铄舡躔麸疳沣蹴＇洪铈红鸿衡秕钿怡＇黩狃供筢礤狍徕秭瀣黩狃痖铉忮赭邋犷钞换唤ò博渝犰箫囵箦蜷弩К囵珏镯К囵麽祀Б轰彐躅ㄤ彐躅疳沣蹴é镳糸镱犰镳弪狒矧＇篝狎癌篝屦暴戾铉翳溴驷蹯舡疳趑弪瞽戾铉翳脲祜栝怙躅洵怡磲脲轭篝犷沐ю徙沲猴疱蜥麸镳弪狒矧后翎螋篝狎后翦篝屦红孱玺戾铉翳红祜鸿栝衡秕钿怡怙躅洵怡┅ㄤ彐礤翳镤铄è疳沣蹴疳沣蹴痼趄遽愆鏖翳箪雉镳弪狒矧篝狎篝屦戾铉翳祜栝怙躅洵怡沲蝌孱舡鲠祯濠疳沣蹴躅戾篌蝈磲轭轭绛疳沣蹴ъ孱玺瑭蝈趱蝾骝镯铄屣皓ㄤ邈姝蝈磲轭轭疳沣蹴箦翩沲蝌孱舡鲠祯ㄩ箪雉怙躅漯疳沣蹴с躜蝈铘鲠祯濠麒孱戾è蝈ㄦ躅汜祆ㄩ痼趄遽憝镳弪狒矧铄镳弪狒矧镳弪狒矧沲蝌孱舡鲠祯铄篝屦┅┅ㄩ怙躅洵怡麒孱戾è骢钽ㄩ痼趄遽憝怙躅洵怡铄怙躅洵怡怙躅洵怡┅祜铄祜┅ㄨ铄栝┅ㄦ躅汜祆骢钽蝈祜栝┅蝈螬铄篝狎舂┅┅换痼换梏麴蠛瘐篝雉岙忉箝箪徵弪矧绡忒筱桢祓儒祓渺狍箦蟑杏梏盱换梏麴蠛瘐篝雉岙忉箝箪徵弪矧绡忒筱桢祓儒祓怎麸蜷犰蟑杏唧趄遽磉疳趑弪铙梏盱ㄤ彐疳趑弪痼疳趑弪瞟疳趑弪痼趄遽愆轰镢蹴孱翎糸镱⑿蝈箦蝣痼趄遽篝狒徙蝻篌篚怏羼蹂铘汜祆麸噌蟓痼趄遽恣燥蝈箦翳痼趄遽憩箝眇禊蝈弼犰踽翦翳痼溴骈铋糸镱箩箦镱翳疳趑弪矧殓轭犰禊骝镯翳黹用屐灬铄秕吁疱蛎镬扉溴扉怛狎砒犴痨搴换ㄤ彐疳蜥礤翦痼舄痼痼弪殄螬┅换换铄舡躔麸痼舄穿换唤ò畅换换铄舡躔麸痼舄穿换唤ù珐换换ㄤ彐疳蜥礤翦痼舄痼痼弪殄螬┅换换铄舡躔麸痼舄穿换唤ò畅渝犰箫囵蝮К囵溴妲轰彐躅ㄤ彐躅痼疳趑弪瞟磲脲轭篝犷沐ю吼狒翦蝾疳趑弪瞟┅ㄤ彐礤翳镤狍痼趄遽è痼痼┅鏖翳箪雉疳趑弪痼趄遽愆痼磲脲轭篝犷沐ю蟓痼趄遽吼狒翦蝾疳趑弪吼篝蝈犴ㄩ箪雉怙躅漯痼ю篝蝈犴痼趄遽戾è痼趄ㄡ蟓痼趄遽疳趑弪瞟┅箦翩痼趄遽痼趄痼趄┅┅ㄤ彐礤翳镤铄è痼痼趄遽痼痼趄遽愆鏖翳箪雉痼趄遽愆痼痼趄遽铄痼趄遽愆┅换痱ㄤ彐躅痱疳趑弪镳糸镱犰蝈疱狒溴驷蹯舡疳趑弪瞽蝈疱狒螵┅⒂铘狲篚玑骘痱痼辛栽乓惟遗信猎萤阵彐蹯骘屮犴痨瀣麸孱篚蝈翳狒遽汨祜镳镦疳趑弪镱禊珏趔镱鲠祯骝镯翳囵螫渝犰箫囵颛囵螫痱痼疳趑弪瞟蝈疱狒螬瘐箬铄ю蝮疳趑弪铙换轲篝蝈犴ㄤ彐疳趑弪轲篝蝈犴疳趑弪瞟è疳趑弪铙洪铋翩矧铋飑ㄥ钿麒孱屙痿洪铋翩矧铋飑ㄧ蜥铛灬蜷豉洪铋翩矧悲旦祜汶后翎翦洪铋翩矧ㄢ艉磲脲蝈沲蝮轹瀛祜汶㈤痼趄遽疳趑弪铙箪雉祜汶┅轰镢蹴孱翎糸镱⑸铙弪翎忪痼趄遽砘痼趄遽翳狒汜忮汨犷珏麒殪轸蝓铑轭怡轭箦螋轭铄疳趑弪铙狒箴邈殒殄忮狒ㄤ彐礤翳镤狍痼趄遽è轲篝蝈犴轲篝蝈犴┅鏖翳箪雉疳趑弪铙孱洵麒孱屙痿珧犷蹯狎轸轲篝蝈犴戾è痼趄磲脲轭篝犷沐ч痼趄遽憝痼趄遽吼狒翦蝾铋哄钿麒孱屙痿孱洵麒孱屙痿虹蜥铛灬蜷豉珧犷蹯狎轸红镢ㄢ艉磲脲蝈沲蝮轹瀛祜汶㈤痼趄遽疳趑弪铙箪雉祜汶┅┅ㄤ镬轶疳疳趑弪铙痼趄ㄥ豉疱汜箦疳疳趑弪ㄩ痼趄遽憝轭箦螋痼趄疳癌扉篝ㄤ弩趄蹉趱蜷铉忾钿ㄢ遽蝈篝疳趑弪铙疳ㄤ镬轶疳疳趑弪铙ㄩ痼趄遽憝轭箦螋痼趄疳忮狒┅┅┅┅ㄤ彐礤翳镤铄è轲篝蝈犴轲篝蝈犴痼趄遽愆鏖翳箪雉疳趑弪铙孱洵麒孱屙痿珧犷蹯狎轸祜汶轲篝蝈犴灬忮祗è徙趱犰忮狒痼趄遽愆ǐ箪雉鲠祯痼趄遽篝狎舡忮狒ㄢ遽痼趄遽愆┅箫螋邃痼趄ī箫螋疳趑弪铙＇弘妁＇徙趱犰忮狒┅ㄢ艉鏖翳蝈沲蝮轹瀛祜汶桢熹祜汶ㄩ疳趑弪铙戾舄è铄舡痼趄ㄣ狎箫螋邃痼趄螬┅ㄥㄩ冀ㄡ泗踽飙忮狒铄舡痼趄ㄢ遽轲篝蝈犴┅戾è铠铄铄舡痼趄┅ㄩㄥ镳铠舂痱镧ㄤ屐弭彐疳趑弪铙铄舡痼趄铄轲篝蝈犴┅铠舂ㄥ鲥铘呼疱候弩轰屐翎珧犷蹯狎轸┅铄舡痼趄ㄣ狎箫螋邃痼趄螬┅ㄣ镯忾铄弼孱趔弼ㄥ鲥铘轰屐翎ㄩ铄舡痼趄黹珧犷蹯狎轸ōㄡ泗踽飙忮狒铄舡痼趄ㄢ遽轲篝蝈犴┅珧犷蹯狎轸┅┅ㄩ孱洵麒孱屙痿屣ㄥ鲥铘呼疱候弩轰屐翎珧犷蹯狎轸┅┅┅ㄤ彐珏铄蜷轲篝蝈犴轭箦螋ㄩ痼趄遽疳趑弪镳糸镱犰篝狎舡忮狒ê滹沲礤铘狒轱⑸铙弪辛栽乓轭麸尚釉遗镣狒釉烈原屡猎釉烈原屡猎溴驷蹯趔麸翳轲篝蝈犴沲蝌孱忮狒┅ㄤ彐礤翳镤轲篝蝈犴轭箦螋è轲篝蝈犴轲篝蝈犴痼趄遽愆疳趑弪镳糸镱犰篝狎舡忮狒鏖翳箪雉疳趑弪铙祜汶轲篝蝈犴戾è痼趄ㄡ蟓痼趄遽疳趑弪瞟┅箦翩箪雉鲠祯痼趄篝狎舡忮狒矧篝狎舡忮狒ㄢ遽轲篝蝈犴┅ㄢ艉鏖翳祜汶桢熹祜汶瘐箬痼趄疳趑弪铙┅┅ㄥ痫螋ч痼趄遽憝轭箦螋换疰殪翦ㄤ彐疳趑弪疰殪翦疳趑弪瞟疳趑弪痱邃殂狒洪铋翩矧ч溴铘轸┅轰镢蹴孱翎糸镱⒂腴屐屙孱趔镦箫躜沐疳趑弪翳狒幸拍擅猎蝈趱蝾驷祗骘虍涉幸拍擅猎轶铒骢钽糸镱箅轲轸屙翳狒狎噱耢麸轸砒犴痨搴换铄舡疰殪翦痼羼扉篝畅换博换订换唤ū畅换铄舡疰殪翦痼弪殄暴у鲥铕换订换唤ū北渝犰箫囵骈祠弪秕臾囵颛囵漉蝮趱趑弪Бㄤ彐礤翳镤铄è疰殪翦疰殪翦颦痼趄遽愆鏖翳箪雉疳趑弪痱邃殂狒濠疰殪翦戾è骢钽ㄩㄦ躅泗轱瞽溴箝珙狒矧痱邃殂狒濠痱邃殂狒灬礅溽ㄩ铕豸ㄥ耢轭瘐痱邃殂狒濠┅┅祜镳烘矧蝈航铄疳趑弪瞟洪矧ㄥ镳蝈螬ㄦ躅汜祆骢钽蝈螬候弭躜蝈螬┅换疰殪翦颦秕ㄤ彐躅疰殪翦颦秕疳趑弪痱邃殂狒濠⒂腴屐屙孱趔镦箫躜沐疳趑弪翳狒幸拍擅猎蝈趱蝾趄蹂骘虍涉幸拍擅猎轶铒骢钽糸镱箅轲轸屙翳狒狎噱耢麸轸渝犰箫囵骈祠弪Б疰殪翦疳趑弪ㄩㄦ躅泗轱瞽溴箝珙狒矧痱邃殂狒濠灬礅溽铒ㄦ躅汜祆痱邃殂狒┅灬礅溽铒ㄥ耢痱邃殂狒┅┅┅瘐箬铄ю骈祠弪秕疳趑弪铙换盹躞祜汜糸镱骢钽糸镱犰轸换粕睾翳轶箦屙麸忮鲥蝙箪秣黹玷忮铄沐篌狎麸躞破轭篝遽骘溴沐铘疱蜴矧磲钽瀹ㄤ彐鲠溟箴灬箦蝣弪＋扉铛ㄦ轭ㄦ蜷孱潇簌礅镬蹰镳虹弭孱鲳⒇那哂庞由衔咴傩泞┅扉篝壶北┅－扉铛铋⒃桢溟箴灬箦蝣弪豉疱翳狒囗秕箦祜汜糸镱箬秕熹溴驷蹯麸留翦眇趔麸狨麸溴翦泗翳泔蝌邈溟箴灬箦蝣弪镱祜徜ㄤ彐珏铄蜷筱蝈孱箝濯ㄤ轶痨狴箦蝣弪┅ㄤ彐礤翳镤筱蝈孱箝濯è溟箴灬箦蝣弪铛祆┅狯镩㈩狃痨殂徕戾礤翳镤殒祜徜邃躅溴犷躅篚痧矧翦溟箴灬箦蝣弪铋飑ㄤ彐礤翳镤筱蝈孱箝濯è溟箴灬箦蝣弪ㄥ耢壶北┅麒孱戾舄è筱蝈孱篝ㄦ轵篝ㄩ珙矧瀛弪蝻蝮蹰镳候躅痱镧蜥ЖⅧ蜥钿颌洪珙矧瀛弪蝻颦篝狒躞猴豸瘐红轭弩┅┅ㄩ澍箦狎汨沲蝌孱筱蝈孱篝颟┅磲疸狎＇疳蝮瀛轭翦珏篚怏羼篝蜷铉箴扉篚怏羼筱蝈孱篝ǐ殇┅恒栳颦忉Ж＼箴徙＼＼┅博┅ㄤ彐躅筱蝈孱箝é镳糸镱犰ㄤ轶痨狴箦蝣弪溟箴灬箦蝣弪┅⑶弭翳麸翎溟礤铙轱铙镦犰盹铋麸蝮腩秣怡纳有塘侪优抑乓筱蝈孱箝濯溟箴灬箦蝣弪┅ㄤ彐鲠筱蝈孱箝濯筱蝈孱箝濠⒃桢泔礅轭邃箝镦犰盹铋麸蝮痨蹒珏轭麸翳磲汨轭翳狒沆疳趑弪铙轶蝓铑轭镱涉秕徜矧蝈盹鲥溟箴灬徭翦祜徜轭沆疳趑弪铙秕磲栳鲥麸躔溽翦翳轶鲠蜷徕戾燥箦翳泔蝌邈鲠祯瀣蝓箦翩筱蝈孱箝濯筱蝈孱箝濠┊渝犰箫嗒溟箴灬箦蝣弪К囿泸邋瞽箝濮囗秕箦祜汜糸镱К囵盹躞濮ㄤ彐珏铄蜷盹躞瀛祜汜糸镱ㄤ轶痨狴箦蝣弪ê滹沲礤铘狒轱⑶弭翳盹躞祜汜糸镱轭骘蝽狒轱骘纳有塘侪优抑乓赠痖汜祆秕箬秕熹牾篝汜祆囗秕箦祜汜糸镱轭篝遽镦躞轭翳轶溟蝈泗禊┅ㄤ彐礤翳镤盹躞瀛祜汜糸镱è溟箴灬箦蝣弪舂ㄥ蝌矧⑼秕箦祜汜糸镱轭骘蝽狒轱罨躅篚痧矧翦溟箴灬箦蝣弪┅ㄤ彐礤翳镤盹躞瀛祜汜糸镱è溟箴灬箦蝣弪ㄥ耢壶北┅ㄤ弩趄蹉趱蜷铉忾钿筱蝈孱鏖钿秣磲疸狎灬礅溽篝颟戾è痫痫箝糸镱＼篝呼弩＇汨狎僵┅疳蝮瀛轭翦珏篚怏羼篝ū痫螬┅┅蹰镳候躅痱镧蜥ЖⅧ滹麸镬㈢弭盹躞屐镢狒轱睥箬屐膦猴豸瘐红轭弩┅换麇栳鲥麸躞蜥钿忮汜躞Ⅷ滹麸镬珏翡轶痨狴珏镯弭蝙镱禊箬秣翳珏镯弭蝙镦镱溟箴灬ㄤ镥铒黠螂骘眭祠榄盹铋麸箦趱痼┊梏麴蠛玳翳踱泔懑觑蜾犷箝篌屐滹麸镬轶篚弩潮换磲疸狎＇疳蝮瀛轭翦珏篝蜷铉箴扉蹰镳候躅痱镧蜥ЖⅧ滹麸镬㈢弭溟箴灬珏镯弭蝙猴豸瘐Ж后趄轭后趄轲疱舂┅麒孱戾舄è筱蝈孱篝．ㄦ轵篝ㄩ珙矧瀛弪蝻蝮蹰镳候躅痱镧蜥ЖⅧ蜥钿颌洪珙矧瀛弪蝻颦篝狒躞猴豸瘐红轭弩┅┅ㄩ澍箦狎汨沲蝌孱筱蝈孱篝颟箴扉磲疸狎＇疳蝮瀛轭翦珏篚怏羼篝蜷铉箴扉篚怏羼筱蝈孱篝ǐ殇┅恒栳颦忉Ж＼箴徙＼＼┅博┅筱蝈孱鏖漪ㄦ轵篝箴扉舂筱蝈孱桢殓梏箦泔钿箴扉舂┅扉篝壶葫后泸邋瞽鏖漪筱蝈孱鏖漪后泸邋瞽桢殓梏筱蝈孱桢殓梏后泸邋筱蝈孱瑚轭滹鏖钿秣┅┅ㄤ彐礤翳镤盹躞瀛祜汜糸镱è溟箴灬箦蝣弪ㄥ耢瑚狴灬钿┅ㄥ蝌矧⑼秕箦祜汜糸镱轭骘蝽狒轱轶沲蝌孱綮躅篚痧矧翦镱揍灬钿┅ㄤ彐躅盹躞瀛祜汜糸镱é镳糸镱犰ㄤ轶痨狴箦蝣弪溟箴灬箦蝣弪┅⑶弭痨轶镦翳盹躞祜汜糸镱麸翎溟箴灬箝瀣犷雉桢轭骘蝽狒轱珈遽铄徕秕纳有塘侪优抑乓纳有塘侪优抑乓汜忮壶北瑚狴灬钿弭悻怩箬秕熹溴驷蹯麸翳泔蝌邈溟箴灬箦蝣弪溴翦泗邃麒孱沆疳趑弪铙轶祜徜邃组祆箝珙犰犷弪蝻殒翳轭骘蝽狒轱汜铑雉忮溴翦蝽轭邃ㄩ瀹麒孱翳溟箴灬箦蝣弪轶铒溴翦泗邃矧铒篚痧矧翦洎蜗耘澡轶轶沲蝌孱綮镱禊轫痨屙孱翦镱丨忉箦簌篝屙蟋箫鏖祆铒黠螂镱组钿秣蟋歪阆蝇犷揍灬钿盹躞瀛祜汜糸镱溟箴灬箦蝣弪┅ㄥ痫螋Ж溟箴灬箦蝣弪筱蝈孱箝筱蝈孱箝濯盹躞瀛祜汜糸镱┅换痦秕箦痦秕箦痦秕箦ㄤ彐疳趑弪痦秕箦疳趑弪瞟矧殄铘狒轱ㄦ矧磲洪铋翩矧候屐狒轹濠轰镢蹴孱翎糸镱⑶弭翳盹躞痫箝糸镱镦翳溟箴灬箦蝣弪ㄇ丈┊弦膳卧猎上汜忮壶矧葫犷葡彝猎汜忮彘翳弪候屐狒轹ㄩ麒殂汜箦翳秕麴豸轶铛礅弪骝镯麸狍骝徙糸镱镦麸翎溟箴灬箝濠矧横怏镬豸ㄩ麒殂汜箦翳秕麴豸轶翳徙趱犰矧痫箝糸镱镦翳盹躞濠渝犰箫囵盹躞屮К囵盹躞妁К囗秕箦祜汜糸镱Бㄤ彐礤翳镤铄è痦秕箦痦秕箦痼趄遽愆鏖翳箪雉矧殄铘狒轱骘蝽狒痦秕箦戾è祜盹躞瀛祜汜糸镱┅ǒㄥ汜箦矧殄铘狒轱êㄧ弭祜壶┅êㄧ弭祜葫┅ㄥ汜箦骘蝽狒ê蝈灬糸鲥ㄧ弭祜ㄥ汜箦矧殄铘狒轱ê后泸邋瞽鏖漪瑭ê后泸邋瞽桢殓梏┅┅ê徕箫祯翦暴┅┅ㄤ彐躅痦秕箦é镳糸镱犰ㄦ矧磲候屐狒轹濠⑶弭翳盹躞濮痫箝糸镱镱筱蝈孱葡彝猎汜忮彘翳弪候屐狒轹ㄩ麒殂汜箦翳秕麴豸轶铛礅弪骝镯麸狍骝徙糸镱镦麸翎溟箴灬箝濠矧横怏镬豸ㄩ麒殂汜箦翳秕麴豸轶翳徙趱犰矧痫箝糸镱镦翳盹躞濠渝犰箫囵盹躞濮囵盹躞妁К囗秕箦祜汜糸镱Б痦秕箦壶骘蝽狒┅瘐箬铄ю盹躞屮疳趑弪铙ㄤ彐躅痦秕箦é镳糸镱犰ㄦ矧磲候屐狒轹濠⑶弭翳盹躞濮痫箝糸镱镱筱蝈孱葡彝猎汜忮彘翳弪候屐狒轹ㄩ麒殂汜箦翳秕麴豸轶铛礅弪骝镯麸狍骝徙糸镱镦麸翎溟箴灬箝濠矧横怏镬豸ㄩ麒殂汜箦翳秕麴豸轶翳徙趱犰矧痫箝糸镱镦翳盹躞濠渝犰箫囵盹躞濮囵盹躞屮К囗秕箦祜汜糸镱Б痦秕箦葫骘蝽狒┅瘐箬铄ю盹躞妁疳趑弪铙